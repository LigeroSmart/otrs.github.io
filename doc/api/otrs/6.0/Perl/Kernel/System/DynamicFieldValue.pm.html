<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.1//EN" "http://www.w3.org/TR/xhtml11/DTD/xhtml11.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en">
<head>
<!-- otrs.github.io -->
<link href="https://netdna.bootstrapcdn.com/font-awesome/4.0.3/css/font-awesome.css" rel="stylesheet">
<link href="https://fonts.googleapis.com/css?family=Roboto" rel="stylesheet" type="text/css">
<link rel="stylesheet" href="../../../../../../documentation.css">
<script src="https://code.jquery.com/jquery-1.11.0.min.js"></script>
<script type="text/javascript" src="../../../../../../documentation.js"></script>
<link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/9.7.0/styles/default.min.css">
<script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/9.7.0/highlight.min.js"></script>
<script>
$(document).ready(function() {
    // programlistings in manuals
    $('pre.programlisting').each(function(i, block) {
        hljs.highlightBlock(block);
    });
    // code snippets in Perl API docs
    $('.pod pre').addClass('perl').each(function(i, block) {
        hljs.highlightBlock(block);
    });
});</script>
<!-- otrs.github.io -->

	<title>Kernel::System::DynamicFieldValue</title>
<meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
  <script type="text/javascript">

  function toggleCode( id ) {
    if ( document.getElementById )
      elem = document.getElementById( id );
    else if ( document.all )
      elem = eval( "document.all." + id );
    else
      return false;

    elemStyle = elem.style;

    if ( elemStyle.display != "block" ) {
      elemStyle.display = "block"
    } else {
      elemStyle.display = "none"
    }

    return true;
  }
  document.writeln( "<style type=\"text/css\">div.method-source-code { display: none }</style>" )
  </script></head>
<body>
<div class="box">
  <h1 class="t1">OTRS 6.0 API Reference Perl</h1>
  <table>
    <tr>
      <td class="label">Description</td>
      <td class="cell">manuals and libraries</td>
    </tr>
  </table>
</div>
<div class="path">
  <a href="../../index.html">OTRS 6.0 API Reference Perl</a> &gt; Perl Modules &gt;
  Kernel::System::DynamicFieldValue
</div>
<div>
<a href="../../src/Kernel/System/DynamicFieldValue.pm">Source</a>
</div>
<div class="pod">
<!-- INDEX START -->
<h3 id="TOP">Index</h3>
<ul>
	<li><a href="#NAME">NAME</a></li>
	<li><a href="#DESCRIPTION">DESCRIPTION</a></li>
	<li><a href="#PUBLIC_INTERFACE">PUBLIC INTERFACE</a><br />
<ul>
	<li><a href="#new">new()</a></li>
	<li><a href="#ValueSet">ValueSet()</a></li>
	<li><a href="#ValueGet">ValueGet()</a></li>
	<li><a href="#ValueDelete">ValueDelete()</a></li>
	<li><a href="#AllValuesDelete">AllValuesDelete()</a></li>
	<li><a href="#ValueValidate">ValueValidate()</a></li>
	<li><a href="#HistoricalValueGet">HistoricalValueGet()</a></li>
	<li><a href="#ValueSearch">ValueSearch()</a></li>
</ul>
</li>
	<li><a href="#TERMS_AND_CONDITIONS">TERMS AND CONDITIONS</a></li>
</ul>
<hr />
<!-- INDEX END -->

<h1 id="NAME">NAME <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>Kernel::System::DynamicFieldValue</p>

<h1 id="DESCRIPTION">DESCRIPTION <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>DynamicField values backend</p>

<h1 id="PUBLIC_INTERFACE">PUBLIC INTERFACE <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<h2 id="new">new()</h2>

<p><a href="#" onclick="toggleCode('method_new');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_new">
                                        <pre>

sub new {
    my ( $Type, %Param ) = @_;

    # allocate new hash for object
    my $Self = {};
    bless( $Self, $Type );

    return $Self;
}
</pre></div>

<p>create a DynamicFieldValue object. Do not use it directly, instead use:</p>

<pre>    my $DynamicFieldValueObject = $Kernel::OM-&gt;Get('Kernel::System::DynamicFieldValue');
</pre><h2 id="ValueSet">ValueSet()</h2>

<p><a href="#" onclick="toggleCode('method_ValueSet');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_ValueSet">
                                        <pre>

sub ValueSet {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    for my $Needed (qw(FieldID ObjectID Value)) {
        if ( !$Param{$Needed} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Need $Needed!"
            );
            return;
        }
    }

    # return if no Value was provided
    if ( ref $Param{Value} ne 'ARRAY' || !$Param{Value}->[0] )
    {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "Need Param{Value}!"
        );
        return;
    }

    my @Values;

    my $Counter = 0;
    VALUE:
    while (1) {
        if ( ref $Param{Value}->[$Counter] ne 'HASH' ) {
            last VALUE;
        }

        if (
            (
                !defined $Param{Value}->[$Counter]->{ValueText}

                # do not accept an empty string as it is the same as NULL on oracle
                || !length $Param{Value}->[$Counter]->{ValueText}
            )
            && !defined $Param{Value}->[$Counter]->{ValueInt}
            && !defined $Param{Value}->[$Counter]->{ValueDateTime}
            )
        {
            last VALUE;
        }

        my %Value = (
            ValueText     => scalar $Param{Value}->[$Counter]->{ValueText},
            ValueInt      => scalar $Param{Value}->[$Counter]->{ValueInt},
            ValueDateTime => scalar $Param{Value}->[$Counter]->{ValueDateTime},
        );

        # data validation
        return if !$Self->ValueValidate( Value => \%Value );

        # data conversions

        # set ValueDateTime column to NULL
        if ( exists $Value{ValueDateTime} && !$Value{ValueDateTime} ) {
            $Value{ValueDateTime} = undef;
        }

        # set Int Zero
        if ( defined $Value{ValueInt} && !$Value{ValueInt} ) {
            $Value{ValueInt} = '0';
        }

        push @Values, \%Value;
        $Counter++;
    }

    # delete existing value
    $Self->ValueDelete(
        FieldID  => $Param{FieldID},
        ObjectID => $Param{ObjectID},
        UserID   => $Param{UserID},
    );

    # get database object
    my $DBObject = $Kernel::OM->Get('Kernel::System::DB');

    for my $Value (@Values) {

        # create a new value entry
        return if !$DBObject->Do(
            SQL =>
                'INSERT INTO dynamic_field_value (field_id, object_id, value_text, value_date, value_int)'
                . ' VALUES (?, ?, ?, ?, ?)',
            Bind => [
                \$Param{FieldID}, \$Param{ObjectID},
                \$Value->{ValueText}, \$Value->{ValueDateTime}, \$Value->{ValueInt},
            ],
        );
    }

    # delete cache
    $Self->_DeleteFromCache(%Param);

    return 1;
}
</pre></div>

<p>sets a dynamic field value. This is represented by one or more rows in the dynamic_field_value
table, each storing one text, date and int field. Please see how they will be returned by
<a href="#ValueGet">ValueGet()</a>.</p>

<pre>    my $Success = $DynamicFieldValueObject-&gt;ValueSet(
        FieldID  =&gt; $FieldID,                 # ID of the dynamic field
        ObjectID =&gt; $ObjectID,                # ID of the current object that the field
                                              #   must be linked to, e. g. TicketID
        Value    =&gt; [
            {
                ValueText          =&gt; 'some text',            # optional, one of these fields must be provided
                ValueDateTime      =&gt; '1977-12-12 12:00:00',  # optional
                ValueInt           =&gt; 123,                    # optional
            },
            ...
        ],
        UserID   =&gt; $UserID,
    );
</pre><h2 id="ValueGet">ValueGet()</h2>

<p><a href="#" onclick="toggleCode('method_ValueGet');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_ValueGet">
                                        <pre>

sub ValueGet {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    for my $Needed (qw(FieldID ObjectID)) {
        if ( !$Param{$Needed} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Need $Needed!"
            );
            return;
        }
    }

    # get cache object
    my $CacheObject = $Kernel::OM->Get('Kernel::System::Cache');

    #
    # Special caching strategy: cache all fields of an object in one cache file.
    #   This avoids too many cache files on systems with many fields for many objects.
    #

    my $CacheKey = 'ValueGet::ObjectID::' . $Param{ObjectID};

    my $Cache = $CacheObject->Get(
        Type => 'DynamicFieldValue',
        Key  => $CacheKey,
    );

    # Check if a cache entry exists
    if ( ref $Cache eq 'HASH' ) {

        if ( exists $Cache->{ $Param{FieldID} } ) {
            return $Cache->{ $Param{FieldID} };
        }
        return [];
    }

    # get database object
    my $DBObject = $Kernel::OM->Get('Kernel::System::DB');

    # We'll populate cache with all object's dynamic fields to reduce
    # number of db accesses (only one db query for all dynamic fields till
    # cache expiration); return only specified one dynamic field
    return if !$DBObject->Prepare(
        SQL =>
            'SELECT id, value_text, value_date, value_int, field_id
            FROM dynamic_field_value
            WHERE object_id = ?
            ORDER BY id',
        Bind => [ \$Param{ObjectID} ],
    );

    my %CacheData;

    while ( my @Data = $DBObject->FetchrowArray() ) {

        # cleanup time stamps (some databases are using e. g. 2008-02-25 22:03:00.000000
        # and 0000-00-00 00:00:00 time stamps)
        if ( $Data[2] ) {
            if ( $Data[2] eq '0000-00-00 00:00:00' ) {
                $Data[2] = undef;
            }
            $Data[2] =~ s/^(\d{4}-\d{2}-\d{2}\s\d{2}:\d{2}:\d{2})\..+?$/$1/;
        }

        push @{ $CacheData{ $Data[4] } }, {
            ID            => $Data[0],
            ValueText     => $Data[1],
            ValueDateTime => $Data[2],
            ValueInt      => $Data[3],
        };
    }

    # get the cache TTL (in seconds)
    my $CacheTTL = $Kernel::OM->Get('Kernel::Config')->Get('DynamicField::CacheTTL') || 60 * 60 * 12;

    # set cache
    $CacheObject->Set(
        Type  => 'DynamicFieldValue',
        Key   => $CacheKey,
        Value => \%CacheData,
        TTL   => $CacheTTL,
    );

    if ( exists $CacheData{ $Param{FieldID} } ) {
        return $CacheData{ $Param{FieldID} }
    }

    return [];
}
</pre></div>

<p>get a dynamic field value. For each table row there will be one entry in the
result list.</p>

<pre>    my $Value = $DynamicFieldValueObject-&gt;ValueGet(
        FieldID            =&gt; $FieldID,                 # ID of the dynamic field
        ObjectID           =&gt; $ObjectID,                # ID of the current object that the field
                                                        #   is linked to, e. g. TicketID
    );
    Returns [
        {
            ID                 =&gt; 437,
            ValueText          =&gt; 'some text',
            ValueDateTime      =&gt; '1977-12-12 12:00:00',
            ValueInt           =&gt; 123,
        },
    ];
</pre><h2 id="ValueDelete">ValueDelete()</h2>

<p><a href="#" onclick="toggleCode('method_ValueDelete');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_ValueDelete">
                                        <pre>

sub ValueDelete {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    for my $Needed (qw(FieldID ObjectID UserID)) {
        if ( !$Param{$Needed} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Need $Needed!"
            );
            return;
        }
    }

    # delete dynamic field value
    return if !$Kernel::OM->Get('Kernel::System::DB')->Do(
        SQL  => 'DELETE FROM dynamic_field_value WHERE field_id = ? AND object_id = ?',
        Bind => [ \$Param{FieldID}, \$Param{ObjectID} ],
    );

    # delete cache
    $Self->_DeleteFromCache(%Param);

    return 1;
}
</pre></div>

<p>delete a Dynamic field value entry. All associated rows will be deleted.</p>

<pre>    my $Success = $DynamicFieldValueObject-&gt;ValueDelete(
        FieldID            =&gt; $FieldID,                 # ID of the dynamic field
        ObjectID           =&gt; $ObjectID,                # ID of the current object that the field
                                                        #   is linked to, e. g. TicketID
        UserID  =&gt; 123,
    );
    Returns 1.
</pre><h2 id="AllValuesDelete">AllValuesDelete()</h2>

<p><a href="#" onclick="toggleCode('method_AllValuesDelete');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_AllValuesDelete">
                                        <pre>

sub AllValuesDelete {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    for my $Needed (qw(FieldID UserID)) {
        if ( !$Param{$Needed} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Need $Needed!"
            );
            return;
        }
    }

    # delete dynamic field value
    return if !$Kernel::OM->Get('Kernel::System::DB')->Do(
        SQL  => 'DELETE FROM dynamic_field_value WHERE field_id = ?',
        Bind => [ \$Param{FieldID} ],
    );

    # Cleanup entire cache!
    $Kernel::OM->Get('Kernel::System::Cache')->CleanUp(
        Type => 'DynamicFieldValue',
    );

    return 1;
}
</pre></div>

<p>delete all entries of a dynamic field .</p>

<pre>    my $Success = $DynamicFieldValueObject-&gt;AllValuesDelete(
        FieldID            =&gt; $FieldID,                 # ID of the dynamic field
        UserID  =&gt; 123,
    );
    Returns 1.
</pre><h2 id="ValueValidate">ValueValidate()</h2>

<p><a href="#" onclick="toggleCode('method_ValueValidate');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_ValueValidate">
                                        <pre>

sub ValueValidate {
    my ( $Self, %Param ) = @_;

    return if !IsHashRefWithData( $Param{Value} );

    my %Value = %{ $Param{Value} };

    # validate date
    if ( $Value{ValueDateTime} ) {

        # get time object
        my $TimeObject = $Kernel::OM->Get('Kernel::System::Time');

        # convert the DateTime value to system time to check errors
        my $SystemTime = $TimeObject->TimeStamp2SystemTime(
            String => $Value{ValueDateTime},
        );

        return if !defined $SystemTime;

        # convert back to time stamp to check errors
        my $TimeStamp = $TimeObject->SystemTime2TimeStamp(
            SystemTime => $SystemTime,
        );

        return if !$TimeStamp;

        # compare if the date is the same
        return if !( $Value{ValueDateTime} eq $TimeStamp )
    }

    # validate integer
    if ( $Value{ValueInt} ) {

        if ( $Value{ValueInt} !~ m{\A  -? \d+ \z}smx ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Invalid Integer '$Value{ValueInt}'!"
            );

            return;
        }
    }

    return 1;
}
</pre></div>

<p>checks if the given value is valid for the value type.</p>

<pre>    my $Success = $DynamicFieldValueObject-&gt;ValueValidate(
        Value    =&gt;  {
                ValueText          =&gt; 'some text',            # optional, one of these fields must be provided
                ValueDateTime      =&gt; '1977-12-12 12:00:00',  # optional
                ValueInt           =&gt; 123,                    # optional
            },
        UserID   =&gt; $UserID,
    );
</pre><h2 id="HistoricalValueGet">HistoricalValueGet()</h2>

<p><a href="#" onclick="toggleCode('method_HistoricalValueGet');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_HistoricalValueGet">
                                        <pre>

sub HistoricalValueGet {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    for my $Needed (qw(FieldID)) {
        if ( !$Param{$Needed} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Need $Needed!"
            );
            return;
        }
    }

    my $ValueType = 'value_text';
    if ( $Param{ValueType} && $Param{ValueType} eq 'DateTime' ) {
        $ValueType = 'value_date';
    }
    elsif ( $Param{ValueType} && $Param{ValueType} eq 'Integer' ) {
        $ValueType = 'value_int';
    }

    # get cache object
    my $CacheObject = $Kernel::OM->Get('Kernel::System::Cache');

    # check cache
    my $CacheKey = join '::', 'HistoricalValueGet::FieldID', $Param{FieldID}, 'ValueType',
        $ValueType;

    my $Cache = $CacheObject->Get(
        Type => 'DynamicFieldValue',
        Key  => $CacheKey,
    );

    # get data from cache
    return $Cache if ($Cache);

    # get database object
    my $DBObject = $Kernel::OM->Get('Kernel::System::DB');

    return if !$DBObject->Prepare(
        SQL =>
            "SELECT DISTINCT($ValueType) FROM dynamic_field_value WHERE field_id = ?",
        Bind => [ \$Param{FieldID} ],
    );

    my %Data;
    while ( my @Row = $DBObject->FetchrowArray() ) {

        # check if the value is already stored
        if ( $Row[0] && !$Data{ $Row[0] } ) {

            if ( $ValueType eq 'value_date' ) {

                # cleanup time stamps (some databases are using e. g. 2008-02-25 22:03:00.000000
                # and 0000-00-00 00:00:00 time stamps)
                if ( $Row[0] eq '0000-00-00 00:00:00' ) {
                    $Row[0] = undef;
                }
                $Row[0] =~ s/^(\d{4}-\d{2}-\d{2}\s\d{2}:\d{2}:\d{2})\..+?$/$1/;
            }

            # store the results
            $Data{ $Row[0] } = $Row[0];
        }
    }

    # get the cache TTL (in seconds)
    my $CacheTTL = $Kernel::OM->Get('Kernel::Config')->Get('DynamicField::CacheTTL') || 60 * 60 * 12;

    # set cache
    $CacheObject->Set(
        Type  => 'DynamicFieldValue',
        Key   => $CacheKey,
        Value => \%Data,
        TTL   => $CacheTTL,
    );

    return \%Data;
}
</pre></div>

<p>get all distinct values from a field stored on the database</p>

<pre>    my $HistoricalValues = $DynamicFieldValueObject-&gt;HistoricalValueGet(
        FieldID   =&gt; $FieldID,                  # ID of the dynamic field
        ValueType =&gt; 'Text',                    # or 'DateTime' or 'Integer'. Default 'Text'
    );
    Returns:
    $HistoricalValues{
        ValueA =&gt; 'ValueA',
        ValueB =&gt; 'ValueB',
        ValueC =&gt; 'ValueC'
    };
</pre><h2 id="ValueSearch">ValueSearch()</h2>

<p><a href="#" onclick="toggleCode('method_ValueSearch');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_ValueSearch">
                                        <pre>

sub ValueSearch {
    my ( $Self, %Param ) = @_;

    # check mandatory parameters
    if ( !$Param{FieldID} ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "Need FieldID!"
        );
        return;
    }

    for my $Param (qw( Search SearchSQL )) {
        if ( !defined $Param{$Param} || !length $Param{$Param} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Need $Param!"
            );
            return;
        }
    }

    my @Values;

    # Cache handling
    my $CacheObject = $Kernel::OM->Get('Kernel::System::Cache');

    my $CacheType      = 'DynamicFieldValue';
    my $CacheKey       = 'ValueSearch::' . $Param{FieldID};
    my $CacheSearchKey = $Param{Search};

    my $Cache = $CacheObject->Get(
        Type => $CacheType,
        Key  => $CacheKey,
    );

    # Check if a cache entry exists
    if (
        IsHashRefWithData($Cache)
        && exists $Cache->{$CacheSearchKey}
        && IsArrayRefWithData( $Cache->{$CacheSearchKey} )
        )
    {
        for my $Value ( @{ $Cache->{$CacheSearchKey} } ) {
            push @Values, $Value;
        }
    }

    return \@Values if @Values;

    my $DBObject = $Kernel::OM->Get('Kernel::System::DB');

    my $SQL = '
        SELECT id, field_id, object_id, value_text, value_date, value_int
        FROM  dynamic_field_value
        WHERE  dynamic_field_value.field_id = ?
            AND ';

    $SQL .= $Param{SearchSQL};

    return if !$DBObject->Prepare(
        SQL  => $SQL,
        Bind => [
            \$Param{FieldID},

            # @{ $QueryCondition{Values} },
        ],
    );

    while ( my @Data = $DBObject->FetchrowArray() ) {
        push @Values, {
            ID        => $Data[0],
            FieldID   => $Data[1],
            ObjectID  => $Data[2],
            ValueText => $Data[3],
            ValueDate => $Data[4],
            ValueInt  => $Data[5],
        };
    }

    # get the cache TTL (in seconds)
    my $CacheTTL = $Kernel::OM->Get('Kernel::Config')->Get('DynamicField::CacheTTL') || 60 * 60 * 12;

    # set cache for new field ID if it isn't set yet.
    # note: it's possible there is no database record for a given field ID. in this case, an empty
    # but defined value has to be stored. Otherwise there would be a database query the next time.
    if ( !ref $Cache || ref $Cache ne 'HASH' ) {
        $Cache = {
            $CacheSearchKey => undef,
        };
    }

    if (@Values) {
        for my $Value (@Values) {
            push @{ $Cache->{$CacheSearchKey} }, $Value;
        }
    }
    else {
        $Cache->{$CacheSearchKey} = undef;
    }

    $CacheObject->Set(
        Type  => $CacheType,
        Key   => $CacheKey,
        Value => $Cache,
        TTL   => $CacheTTL,
    );

    return \@Values;
}
</pre></div>

<p>Searches/fetches dynamic field value.</p>

<pre>    my $Value = $DynamicFieldValueObject-&gt;ValueSearch(
        FieldID            =&gt; 142,             # ID of dynamic field to search
        Search             =&gt; 'test',
        SearchSQL          =&gt; &quot;dynamic_field_value.value_text = 'test'&quot;,
    );
    Returns [
        {
            ID            =&gt; 437,
            FieldID       =&gt; 123,
            ObjectID      =&gt; 133,
            ValueText     =&gt; 'some text',
            ValueDateTime =&gt; '1977-12-12 12:00:00',
            ValueInt      =&gt; 123,
        },
        # ...
    ];
</pre><h1 id="TERMS_AND_CONDITIONS">TERMS AND CONDITIONS <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>This software is part of the OTRS project (<a href="http://otrs.org/">http://otrs.org/</a>).</p>

<p>This software comes with ABSOLUTELY NO WARRANTY. For details, see
the enclosed file COPYING for license information (AGPL). If you
did not receive this file, see <a href="http://www.gnu.org/licenses/agpl.txt">http://www.gnu.org/licenses/agpl.txt</a>.</p>


</div><div class="footer">generated by <a href="http://search.cpan.org/perldoc?Pod%3A%3AProjectDocs">Pod::ProjectDocs</a></div></body>
</html>
