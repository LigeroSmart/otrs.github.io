<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.1//EN" "http://www.w3.org/TR/xhtml11/DTD/xhtml11.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en">
<head>
<!-- otrs.github.io -->
<link href="https://netdna.bootstrapcdn.com/font-awesome/4.0.3/css/font-awesome.css" rel="stylesheet">
<link href="https://fonts.googleapis.com/css?family=Roboto" rel="stylesheet" type="text/css">
<link rel="stylesheet" href="../../../../../../documentation.css">
<script src="https://code.jquery.com/jquery-1.11.0.min.js"></script>
<script type="text/javascript" src="../../../../../../documentation.js"></script>
<link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/9.7.0/styles/default.min.css">
<script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/9.7.0/highlight.min.js"></script>
<script>
$(document).ready(function() {
    // programlistings in manuals
    $('pre.programlisting').each(function(i, block) {
        hljs.highlightBlock(block);
    });
    // code snippets in Perl API docs
    $('.pod pre').addClass('perl').each(function(i, block) {
        hljs.highlightBlock(block);
    });
});</script>
<!-- otrs.github.io -->

	<title>Kernel::System::Main</title>
<meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
  <script type="text/javascript">

  function toggleCode( id ) {
    if ( document.getElementById )
      elem = document.getElementById( id );
    else if ( document.all )
      elem = eval( "document.all." + id );
    else
      return false;

    elemStyle = elem.style;

    if ( elemStyle.display != "block" ) {
      elemStyle.display = "block"
    } else {
      elemStyle.display = "none"
    }

    return true;
  }
  document.writeln( "<style type=\"text/css\">div.method-source-code { display: none }</style>" )
  </script></head>
<body>
<div class="box">
  <h1 class="t1">OTRS 6.0 API Reference Perl</h1>
  <table>
    <tr>
      <td class="label">Description</td>
      <td class="cell">manuals and libraries</td>
    </tr>
  </table>
</div>
<div class="path">
  <a href="../../index.html">OTRS 6.0 API Reference Perl</a> &gt; Perl Modules &gt;
  Kernel::System::Main
</div>
<div>
<a href="../../src/Kernel/System/Main.pm">Source</a>
</div>
<div class="pod">
<!-- INDEX START -->
<h3 id="TOP">Index</h3>
<ul>
	<li><a href="#NAME">NAME</a></li>
	<li><a href="#DESCRIPTION">DESCRIPTION</a></li>
	<li><a href="#PUBLIC_INTERFACE">PUBLIC INTERFACE</a><br />
<ul>
	<li><a href="#new">new()</a></li>
	<li><a href="#Require">Require()</a></li>
	<li><a href="#RequireBaseClass">RequireBaseClass()</a></li>
	<li><a href="#Die">Die()</a></li>
	<li><a href="#FilenameCleanUp">FilenameCleanUp()</a></li>
	<li><a href="#FileRead">FileRead()</a></li>
	<li><a href="#FileWrite">FileWrite()</a></li>
	<li><a href="#FileDelete">FileDelete()</a></li>
	<li><a href="#FileGetMTime">FileGetMTime()</a></li>
	<li><a href="#MD5sum">MD5sum()</a></li>
	<li><a href="#Dump">Dump()</a></li>
	<li><a href="#DirectoryRead">DirectoryRead()</a></li>
	<li><a href="#GenerateRandomString">GenerateRandomString()</a></li>
</ul>
</li>
	<li><a href="#TERMS_AND_CONDITIONS">TERMS AND CONDITIONS</a></li>
</ul>
<hr />
<!-- INDEX END -->

<h1 id="NAME">NAME <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>Kernel::System::Main - main object</p>

<h1 id="DESCRIPTION">DESCRIPTION <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>All main functions to load modules, die, and handle files.</p>

<h1 id="PUBLIC_INTERFACE">PUBLIC INTERFACE <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<h2 id="new">new()</h2>

<p><a href="#" onclick="toggleCode('method_new');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_new">
                                        <pre>

sub new {
    my ( $Type, %Param ) = @_;

    # allocate new hash for object
    my $Self = {};
    bless( $Self, $Type );

    return $Self;
}
</pre></div>

<p>create new object. Do not use it directly, instead use:</p>

<pre>    my $MainObject = $Kernel::OM-&gt;Get('Kernel::System::Main');
</pre><h2 id="Require">Require()</h2>

<p><a href="#" onclick="toggleCode('method_Require');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_Require">
                                        <pre>

sub Require {
    my ( $Self, $Module, %Param ) = @_;

    if ( !$Module ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => 'Need module!',
        );
        return;
    }

    # prepare module
    $Module =~ s/::/\//g;
    $Module .= '.pm';

    # just return if it's already loaded
    return 1 if $INC{$Module};

    my $Result;
    my $File;

    # find full path of module
    PREFIX:
    for my $Prefix (@INC) {
        $File = $Prefix . '/' . $Module;

        next PREFIX if !-f $File;

        $Result = do $File;

        last PREFIX;
    }

    # if there was an error
    if ($@) {

        if ( !$Param{Silent} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Caller   => 1,
                Priority => 'error',
                Message  => "$@",
            );
        }

        return;
    }

    # check result value, should be true
    if ( !$Result ) {

        if ( !$Param{Silent} ) {
            my $Message = "Module $Module not found/could not be loaded";
            if ( !-f $File ) {
                $Message = "Module $Module not in \@INC (@INC)";
            }
            elsif ( !-r $File ) {
                $Message = "Module could not be loaded (no read permissions on $File)";
            }

            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Caller   => 1,
                Priority => 'error',
                Message  => $Message,
            );
        }

        return;
    }

    # add module
    $INC{$Module} = $File;

    return 1;
}
</pre></div>

<p>require/load a module</p>

<pre>    my $Loaded = $MainObject-&gt;Require(
        'Kernel::System::Example',
        Silent =&gt; 1,                # optional, no log entry if module was not found
    );
</pre><h2 id="RequireBaseClass">RequireBaseClass()</h2>

<p><a href="#" onclick="toggleCode('method_RequireBaseClass');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_RequireBaseClass">
                                        <pre>

sub RequireBaseClass {
    my ( $Self, $Module ) = @_;

    # Load the module, if not already loaded.
    return if !$Self->Require($Module);

    no strict 'refs';    ## no critic
    my $CallingClass = caller(0);

    # Check if the base class was already loaded.
    # This can happen in persistent environments as mod_perl (see bug#9686).
    if ( List::Util::first { $_ eq $Module } @{"${CallingClass}::ISA"} ) {
        return 1;    # nothing to do now
    }

    push @{"${CallingClass}::ISA"}, $Module;

    return 1;
}
</pre></div>

<p>require/load a module and add it as a base class to the
calling package, if not already present (this check is needed
for persistent environments).</p>

<pre>    my $Loaded = $MainObject-&gt;RequireBaseClass(
        'Kernel::System::Example',
    );
</pre><h2 id="Die">Die()</h2>

<p><a href="#" onclick="toggleCode('method_Die');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_Die">
                                        <pre>

sub Die {
    my ( $Self, $Message ) = @_;

    $Message = $Message || 'Died!';

    # log message
    $Kernel::OM->Get('Kernel::System::Log')->Log(
        Caller   => 1,
        Priority => 'error',
        Message  => $Message,
    );

    exit;
}
</pre></div>

<p>to die</p>

<pre>    $MainObject-&gt;Die('some message to die');
</pre><h2 id="FilenameCleanUp">FilenameCleanUp()</h2>

<p><a href="#" onclick="toggleCode('method_FilenameCleanUp');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_FilenameCleanUp">
                                        <pre>

sub FilenameCleanUp {
    my ( $Self, %Param ) = @_;

    if ( !$Param{Filename} ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => 'Need Filename!',
        );
        return;
    }

    my $Type = lc( $Param{Type} || 'local' );

    if ( $Type eq 'md5' ) {
        $Kernel::OM->Get('Kernel::System::Encode')->EncodeOutput( \$Param{Filename} );
        $Param{Filename} = md5_hex( $Param{Filename} );
    }

    # replace invalid token for attachment file names
    elsif ( $Type eq 'attachment' ) {

        # replace invalid token like < > ? " : ; | \ / or *
        $Param{Filename} =~ s/[ <>\?":\\\*\|\/;\[\]]/_/g;

        # replace utf8 and iso
        $Param{Filename} =~ s/(\x{00C3}\x{00A4}|\x{00A4})/ae/g;
        $Param{Filename} =~ s/(\x{00C3}\x{00B6}|\x{00B6})/oe/g;
        $Param{Filename} =~ s/(\x{00C3}\x{00BC}|\x{00FC})/ue/g;
        $Param{Filename} =~ s/(\x{00C3}\x{009F}|\x{00C4})/Ae/g;
        $Param{Filename} =~ s/(\x{00C3}\x{0096}|\x{0096})/Oe/g;
        $Param{Filename} =~ s/(\x{00C3}\x{009C}|\x{009C})/Ue/g;
        $Param{Filename} =~ s/(\x{00C3}\x{009F}|\x{00DF})/ss/g;
        $Param{Filename} =~ s/-+/-/g;

        # cut the string if too long
        if ( length( $Param{Filename} ) > 100 ) {
            my $Ext = '';
            if ( $Param{Filename} =~ /^.*(\.(...|....))$/ ) {
                $Ext = $1;
            }
            $Param{Filename} = substr( $Param{Filename}, 0, 95 ) . $Ext;
        }
    }
    else {

        # replace invalid token like [ ] * : ? " < > ; | \ /
        $Param{Filename} =~ s/[<>\?":\\\*\|\/;\[\]]/_/g;
    }

    return $Param{Filename};
}
</pre></div>

<p>to clean up filenames which can be used in any case (also quoting is done)</p>

<pre>    my $Filename = $MainObject-&gt;FilenameCleanUp(
        Filename =&gt; 'me_to/alal.xml',
        Type     =&gt; 'Local', # Local|Attachment|MD5
    );
    my $Filename = $MainObject-&gt;FilenameCleanUp(
        Filename =&gt; 'some:file.xml',
        Type     =&gt; 'MD5', # Local|Attachment|MD5
    );
</pre><h2 id="FileRead">FileRead()</h2>

<p><a href="#" onclick="toggleCode('method_FileRead');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_FileRead">
                                        <pre>

sub FileRead {
    my ( $Self, %Param ) = @_;

    my $FH;
    if ( $Param{Filename} && $Param{Directory} ) {

        # filename clean up
        $Param{Filename} = $Self->FilenameCleanUp(
            Filename => $Param{Filename},
            Type     => $Param{Type} || 'Local',    # Local|Attachment|MD5
        );
        $Param{Location} = "$Param{Directory}/$Param{Filename}";
    }
    elsif ( $Param{Location} ) {

        # filename clean up
        $Param{Location} =~ s{//}{/}xmsg;
    }
    else {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => 'Need Filename and Directory or Location!',
        );

    }

    # check if file exists
    if ( !-e $Param{Location} ) {
        if ( !$Param{DisableWarnings} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "File '$Param{Location}' doesn't exist!"
            );
        }
        return;
    }

    # set open mode
    my $Mode = '<';
    if ( $Param{Mode} && $Param{Mode} =~ m{ \A utf-?8 \z }xmsi ) {
        $Mode = '<:utf8';
    }

    # return if file can not open
    if ( !open $FH, $Mode, $Param{Location} ) {    ## no critic
        if ( !$Param{DisableWarnings} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Can't open '$Param{Location}': $!",
            );
        }
        return;
    }

    # lock file (Shared Lock)
    if ( !flock $FH, LOCK_SH ) {
        if ( !$Param{DisableWarnings} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Can't lock '$Param{Location}': $!",
            );
        }
    }

    # enable binmode
    if ( !$Param{Mode} || $Param{Mode} =~ m{ \A binmode }xmsi ) {
        binmode $FH;
    }

    # read file as array
    if ( $Param{Result} && $Param{Result} eq 'ARRAY' ) {

        # read file content at once
        my @Array = <$FH>;
        close $FH;

        return \@Array;
    }

    # read file as string
    my $String = do { local $/; <$FH> };
    close $FH;

    return \$String;
}
</pre></div>

<p>to read files from file system</p>

<pre>    my $ContentSCALARRef = $MainObject-&gt;FileRead(
        Directory =&gt; 'c:\some\location',
        Filename  =&gt; 'file2read.txt',
        # or Location
        Location  =&gt; 'c:\some\location\file2read.txt',
    );
    my $ContentARRAYRef = $MainObject-&gt;FileRead(
        Directory =&gt; 'c:\some\location',
        Filename  =&gt; 'file2read.txt',
        # or Location
        Location  =&gt; 'c:\some\location\file2read.txt',
        Result    =&gt; 'ARRAY', # optional - SCALAR|ARRAY
    );
    my $ContentSCALARRef = $MainObject-&gt;FileRead(
        Directory       =&gt; 'c:\some\location',
        Filename        =&gt; 'file2read.txt',
        # or Location
        Location        =&gt; 'c:\some\location\file2read.txt',
        Mode            =&gt; 'binmode', # optional - binmode|utf8
        Type            =&gt; 'Local',   # optional - Local|Attachment|MD5
        Result          =&gt; 'SCALAR',  # optional - SCALAR|ARRAY
        DisableWarnings =&gt; 1,         # optional
    );
</pre><h2 id="FileWrite">FileWrite()</h2>

<p><a href="#" onclick="toggleCode('method_FileWrite');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_FileWrite">
                                        <pre>

sub FileWrite {
    my ( $Self, %Param ) = @_;

    if ( $Param{Filename} && $Param{Directory} ) {

        # filename clean up
        $Param{Filename} = $Self->FilenameCleanUp(
            Filename => $Param{Filename},
            Type     => $Param{Type} || 'Local',    # Local|Attachment|MD5
        );
        $Param{Location} = "$Param{Directory}/$Param{Filename}";
    }
    elsif ( $Param{Location} ) {

        # filename clean up
        $Param{Location} =~ s/\/\//\//g;
    }
    else {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => 'Need Filename and Directory or Location!',
        );
    }

    # set open mode (if file exists, lock it on open, done by '+<')
    my $Exists;
    if ( -f $Param{Location} ) {
        $Exists = 1;
    }
    my $Mode = '>';
    if ($Exists) {
        $Mode = '+<';
    }
    if ( $Param{Mode} && $Param{Mode} =~ /^(utf8|utf\-8)/i ) {
        $Mode = '>:utf8';
        if ($Exists) {
            $Mode = '+<:utf8';
        }
    }

    # return if file can not open
    my $FH;
    if ( !open $FH, $Mode, $Param{Location} ) {    ## no critic
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "Can't write '$Param{Location}': $!",
        );
        return;
    }

    # lock file (Exclusive Lock)
    if ( !flock $FH, LOCK_EX ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "Can't lock '$Param{Location}': $!",
        );
    }

    # empty file first (needed if file is open by '+<')
    truncate $FH, 0;

    # enable binmode
    if ( !$Param{Mode} || lc $Param{Mode} eq 'binmode' ) {

        # make sure, that no utf8 stamp exists (otherway perl will do auto convert to iso)
        $Kernel::OM->Get('Kernel::System::Encode')->EncodeOutput( $Param{Content} );

        # set file handle to binmode
        binmode $FH;
    }

    # write file if content is not undef
    if ( defined ${ $Param{Content} } ) {
        print $FH ${ $Param{Content} };
    }

    # write empty file if content is undef
    else {
        print $FH '';
    }

    # close the filehandle
    close $FH;

    # set permission
    if ( $Param{Permission} ) {
        if ( length $Param{Permission} == 3 ) {
            $Param{Permission} = "0$Param{Permission}";
        }
        chmod( oct( $Param{Permission} ), $Param{Location} );
    }

    return $Param{Filename} if $Param{Filename};
    return $Param{Location};
}
</pre></div>

<p>to write data to file system</p>

<pre>    my $FileLocation = $MainObject-&gt;FileWrite(
        Directory =&gt; 'c:\some\location',
        Filename  =&gt; 'file2write.txt',
        # or Location
        Location  =&gt; 'c:\some\location\file2write.txt',
        Content   =&gt; \$Content,
    );
    my $FileLocation = $MainObject-&gt;FileWrite(
        Directory  =&gt; 'c:\some\location',
        Filename   =&gt; 'file2write.txt',
        # or Location
        Location   =&gt; 'c:\some\location\file2write.txt',
        Content    =&gt; \$Content,
        Mode       =&gt; 'binmode', # binmode|utf8
        Type       =&gt; 'Local',   # optional - Local|Attachment|MD5
        Permission =&gt; '644',     # optional - unix file permissions
    );
</pre><p>Platform note: MacOS (HFS+) stores filenames as Unicode NFD internally,
and DirectoryRead() will also report them as NFD.</p>

<h2 id="FileDelete">FileDelete()</h2>

<p><a href="#" onclick="toggleCode('method_FileDelete');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_FileDelete">
                                        <pre>

sub FileDelete {
    my ( $Self, %Param ) = @_;

    if ( $Param{Filename} && $Param{Directory} ) {

        # filename clean up
        $Param{Filename} = $Self->FilenameCleanUp(
            Filename => $Param{Filename},
            Type     => $Param{Type} || 'Local',    # Local|Attachment|MD5
        );
        $Param{Location} = "$Param{Directory}/$Param{Filename}";
    }
    elsif ( $Param{Location} ) {

        # filename clean up
        $Param{Location} =~ s/\/\//\//g;
    }
    else {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => 'Need Filename and Directory or Location!',
        );
    }

    # check if file exists
    if ( !-e $Param{Location} ) {
        if ( !$Param{DisableWarnings} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "File '$Param{Location}' doesn't exist!"
            );
        }
        return;
    }

    # delete file
    if ( !unlink( $Param{Location} ) ) {
        if ( !$Param{DisableWarnings} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Can't delete '$Param{Location}': $!",
            );
        }
        return;
    }

    return 1;
}
</pre></div>

<p>to delete a file from file system</p>

<pre>    my $Success = $MainObject-&gt;FileDelete(
        Directory       =&gt; 'c:\some\location',
        Filename        =&gt; 'me_to/alal.xml',
        # or Location
        Location        =&gt; 'c:\some\location\me_to\alal.xml'
        Type            =&gt; 'Local',   # optional - Local|Attachment|MD5
        DisableWarnings =&gt; 1, # optional
    );
</pre><h2 id="FileGetMTime">FileGetMTime()</h2>

<p><a href="#" onclick="toggleCode('method_FileGetMTime');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_FileGetMTime">
                                        <pre>

sub FileGetMTime {
    my ( $Self, %Param ) = @_;

    my $FH;
    if ( $Param{Filename} && $Param{Directory} ) {

        # filename clean up
        $Param{Filename} = $Self->FilenameCleanUp(
            Filename => $Param{Filename},
            Type     => $Param{Type} || 'Local',    # Local|Attachment|MD5
        );
        $Param{Location} = "$Param{Directory}/$Param{Filename}";
    }
    elsif ( $Param{Location} ) {

        # filename clean up
        $Param{Location} =~ s{//}{/}xmsg;
    }
    else {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => 'Need Filename and Directory or Location!',
        );

    }

    # check if file exists
    if ( !-e $Param{Location} ) {
        if ( !$Param{DisableWarnings} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "File '$Param{Location}' doesn't exist!"
            );
        }
        return;
    }

    # get file metadata
    my $Stat = stat( $Param{Location} );

    if ( !$Stat ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "Cannot stat file '$Param{Location}': $!"
        );
        return;
    }

    return $Stat->mtime();
}
</pre></div>

<p>get timestamp of file change time</p>

<pre>    my $FileMTime = $MainObject-&gt;FileGetMTime(
        Directory =&gt; 'c:\some\location',
        Filename  =&gt; 'me_to/alal.xml',
        # or Location
        Location  =&gt; 'c:\some\location\me_to\alal.xml'
    );
</pre><h2 id="MD5sum">MD5sum()</h2>

<p><a href="#" onclick="toggleCode('method_MD5sum');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_MD5sum">
                                        <pre>

sub MD5sum {
    my ( $Self, %Param ) = @_;

    if ( !$Param{Filename} && !$Param{String} ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => 'Need Filename or String!',
        );
        return;
    }

    # check if file exists
    if ( $Param{Filename} && !-e $Param{Filename} ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "File '$Param{Filename}' doesn't exist!",
        );
        return;
    }

    # md5sum file
    if ( $Param{Filename} ) {

        # open file
        my $FH;
        if ( !open $FH, '<', $Param{Filename} ) {    ## no critic
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Can't read '$Param{Filename}': $!",
            );
            return;
        }

        binmode $FH;
        my $MD5sum = Digest::MD5->new()->addfile($FH)->hexdigest();
        close $FH;

        return $MD5sum;
    }

    # get encode object
    my $EncodeObject = $Kernel::OM->Get('Kernel::System::Encode');

    # md5sum string
    if ( !ref $Param{String} ) {
        $EncodeObject->EncodeOutput( \$Param{String} );
        return md5_hex( $Param{String} );
    }

    # md5sum scalar reference
    if ( ref $Param{String} eq 'SCALAR' ) {
        $EncodeObject->EncodeOutput( $Param{String} );
        return md5_hex( ${ $Param{String} } );
    }

    $Kernel::OM->Get('Kernel::System::Log')->Log(
        Priority => 'error',
        Message  => "Need a SCALAR reference like 'String => \$Content' in String param.",
    );

    return;
}
</pre></div>

<p>get a md5 sum of a file or a string</p>

<pre>    my $MD5Sum = $MainObject-&gt;MD5sum(
        Filename =&gt; '/path/to/me_to_alal.xml',
    );
    my $MD5Sum = $MainObject-&gt;MD5sum(
        String =&gt; \$SomeString,
    );
    # note: needs more memory!
    my $MD5Sum = $MainObject-&gt;MD5sum(
        String =&gt; $SomeString,
    );
</pre><h2 id="Dump">Dump()</h2>

<p><a href="#" onclick="toggleCode('method_Dump');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_Dump">
                                        <pre>

sub Dump {
    my ( $Self, $Data, $Type ) = @_;

    # check needed data
    if ( !defined $Data ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "Need \$String in Dump()!"
        );
        return;
    }

    # check type
    if ( !$Type ) {
        $Type = 'binary';
    }
    if ( $Type ne 'ascii' && $Type ne 'binary' ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "Invalid Type '$Type'!"
        );
        return;
    }

    # mild pretty print
    $Data::Dumper::Indent = 1;

    # sort hash keys
    $Data::Dumper::Sortkeys = 1;

    # This Dump() is using Data::Dumper with a utf8 workarounds to handle
    # the bug [rt.cpan.org #28607] Data::Dumper::Dumper is dumping utf8
    # strings as latin1/8bit instead of utf8. Use Storable module used for
    # workaround.
    # -> http://rt.cpan.org/Ticket/Display.html?id=28607
    if ( $Type eq 'binary' ) {

        # Clone the data because we need to disable the utf8 flag in all
        # reference variables and do not to want to do this in the orig.
        # variables because they will still used in the system.
        my $DataNew = Storable::dclone( \$Data );

        # Disable utf8 flag.
        $Self->_Dump($DataNew);

        # Dump it as binary strings.
        my $String = Data::Dumper::Dumper( ${$DataNew} );    ## no critic

        # Enable utf8 flag.
        Encode::_utf8_on($String);

        return $String;
    }

    # fallback if Storable can not be loaded
    return Data::Dumper::Dumper($Data);                      ## no critic

}
</pre></div>

<p>dump variable to an string</p>

<pre>    my $Dump = $MainObject-&gt;Dump(
        $SomeVariable,
    );
    my $Dump = $MainObject-&gt;Dump(
        {
            Key1 =&gt; $SomeVariable,
        },
    );
    dump only in ascii characters (&gt; 128 will be marked as \x{..})
    my $Dump = $MainObject-&gt;Dump(
        $SomeVariable,
        'ascii', # ascii|binary - default is binary
    );
</pre><h2 id="DirectoryRead">DirectoryRead()</h2>

<p><a href="#" onclick="toggleCode('method_DirectoryRead');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_DirectoryRead">
                                        <pre>

sub DirectoryRead {
    my ( $Self, %Param ) = @_;

    # check needed params
    for my $Needed (qw(Directory Filter)) {
        if ( !$Param{$Needed} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Message  => "Needed $Needed: $!",
                Priority => 'error',
            );
            return;
        }
    }

    # if directory doesn't exists stop
    if ( !-d $Param{Directory} && !$Param{Silent} ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Message  => "Directory doesn't exist: $Param{Directory}: $!",
            Priority => 'error',
        );
        return;
    }

    # check Filter param
    if ( ref $Param{Filter} ne '' && ref $Param{Filter} ne 'ARRAY' ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Message  => 'Filter param need to be scalar or array ref!',
            Priority => 'error',
        );
        return;
    }

    # prepare non array filter
    if ( ref $Param{Filter} ne 'ARRAY' ) {
        $Param{Filter} = [ $Param{Filter} ];
    }

    # executes glob for every filter
    my @GlobResults;
    my %Seen;

    for my $Filter ( @{ $Param{Filter} } ) {
        my @Glob = glob "$Param{Directory}/$Filter";

        # look for repeated values
        NAME:
        for my $GlobName (@Glob) {

            next NAME if !-e $GlobName;
            if ( !$Seen{$GlobName} ) {
                push @GlobResults, $GlobName;
                $Seen{$GlobName} = 1;
            }
        }
    }

    if ( $Param{Recursive} ) {

        # loop protection to prevent symlinks causing lockups
        $Param{LoopProtection}++;
        return if $Param{LoopProtection} > 100;

        # check all files in current directory
        my @Directories = glob "$Param{Directory}/*";

        DIRECTORY:
        for my $Directory (@Directories) {

            # return if file is not a directory
            next DIRECTORY if !-d $Directory;

            # repeat same glob for directory
            my @SubResult = $Self->DirectoryRead(
                %Param,
                Directory => $Directory,
            );

            # add result to hash
            for my $Result (@SubResult) {
                if ( !$Seen{$Result} ) {
                    push @GlobResults, $Result;
                    $Seen{$Result} = 1;
                }
            }
        }
    }

    # if no results
    return if !@GlobResults;

    # get encode object
    my $EncodeObject = $Kernel::OM->Get('Kernel::System::Encode');

    # compose normalize every name in the file list
    my @Results;
    for my $Filename (@GlobResults) {

        # first convert filename to utf-8 if utf-8 is used internally
        $Filename = $EncodeObject->Convert2CharsetInternal(
            Text => $Filename,
            From => 'utf-8',
        );

        push @Results, $Filename;
    }

    # always sort the result
    @Results = sort @Results;

    return @Results;
}
</pre></div>

<p>reads a directory and returns an array with results.</p>

<pre>    my @FilesInDirectory = $MainObject-&gt;DirectoryRead(
        Directory =&gt; '/tmp',
        Filter    =&gt; 'Filenam*',
    );
    my @FilesInDirectory = $MainObject-&gt;DirectoryRead(
        Directory =&gt; $Path,
        Filter    =&gt; '*',
    );
</pre><p>read all files in subdirectories as well (recursive):</p>

<pre>    my @FilesInDirectory = $MainObject-&gt;DirectoryRead(
        Directory =&gt; $Path,
        Filter    =&gt; '*',
        Recursive =&gt; 1,
    );
</pre><p>You can pass several additional filters at once:</p>

<pre>    my @FilesInDirectory = $MainObject-&gt;DirectoryRead(
        Directory =&gt; '/tmp',
        Filter    =&gt; \@MyFilters,
    );
</pre><p>The result strings are absolute paths, and they are converted to utf8.</p>

<p>Use the 'Silent' parameter to suppress log messages when a directory
does not have to exist:</p>

<pre>    my @FilesInDirectory = $MainObject-&gt;DirectoryRead(
        Directory =&gt; '/special/optional/directory/',
        Filter    =&gt; '*',
        Silent    =&gt; 1,     # will not log errors if the directory does not exist
    );
</pre><p>Platform note: MacOS (HFS+) stores filenames as Unicode NFD internally,
and DirectoryRead() will also report them as NFD.</p>

<h2 id="GenerateRandomString">GenerateRandomString()</h2>

<p><a href="#" onclick="toggleCode('method_GenerateRandomString');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_GenerateRandomString">
                                        <pre>

sub GenerateRandomString {
    my ( $Self, %Param ) = @_;

    my $Length = $Param{Length} || 16;

    # The standard list of characters in the dictionary. Don't use special chars here.
    my @DictionaryChars = ( 0 .. 9, 'A' .. 'Z', 'a' .. 'z' );

    # override dictionary with custom list if given
    if ( $Param{Dictionary} && ref $Param{Dictionary} eq 'ARRAY' ) {
        @DictionaryChars = @{ $Param{Dictionary} };
    }

    my $DictionaryLength = scalar @DictionaryChars;

    # generate the string
    my $String;

    for ( 1 .. $Length ) {

        my $Key = int rand $DictionaryLength;

        $String .= $DictionaryChars[$Key];
    }

    return $String;
}
</pre></div>

<p>generate a random string of defined length, and of a defined alphabet.
defaults to a length of 16 and alphanumerics ( 0..9, A-Z and a-z).</p>

<pre>    my $String = $MainObject-&gt;GenerateRandomString();
    returns
    $String = 'mHLOx7psWjMe5Pj7';
    with specific length:
    my $String = $MainObject-&gt;GenerateRandomString(
        Length =&gt; 32,
    );
    returns
    $String = 'azzHab72wIlAXDrxHexsI5aENsESxAO7';
    with specific length and alphabet:
    my $String = $MainObject-&gt;GenerateRandomString(
        Length     =&gt; 32,
        Dictionary =&gt; [ 0..9, 'a'..'f' ], # hexadecimal
        );
    returns
    $String = '9fec63d37078fe72f5798d2084fea8ad';


</pre><h1 id="TERMS_AND_CONDITIONS">TERMS AND CONDITIONS <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>This software is part of the OTRS project (<a href="http://otrs.org/">http://otrs.org/</a>).</p>

<p>This software comes with ABSOLUTELY NO WARRANTY. For details, see
the enclosed file COPYING for license information (AGPL). If you
did not receive this file, see <a href="http://www.gnu.org/licenses/agpl.txt">http://www.gnu.org/licenses/agpl.txt</a>.</p>


</div><div class="footer">generated by <a href="http://search.cpan.org/perldoc?Pod%3A%3AProjectDocs">Pod::ProjectDocs</a></div></body>
</html>
