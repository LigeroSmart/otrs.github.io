<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.1//EN" "http://www.w3.org/TR/xhtml11/DTD/xhtml11.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en">
<head>
<!-- otrs.github.io -->
<link href="https://netdna.bootstrapcdn.com/font-awesome/4.0.3/css/font-awesome.css" rel="stylesheet">
<link href="https://fonts.googleapis.com/css?family=Roboto" rel="stylesheet" type="text/css">
<link rel="stylesheet" href="../../../../../../documentation.css">
<script src="https://code.jquery.com/jquery-1.11.0.min.js"></script>
<script type="text/javascript" src="../../../../../../documentation.js"></script>
<link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/9.9.0/styles/default.min.css">
<script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/9.9.0/highlight.min.js"></script>
<script>
$(document).ready(function() {

    // Hint languages to prevent false matches (php for example).
    hljs.configure({
        languages: ['perl', 'javascript', 'xml', 'html', 'css', 'json', 'yaml']
    });

    // programlistings in manuals
    $('pre.programlisting').each(function(i, block) {
        hljs.highlightBlock(block);
    });
    // code snippets in Perl API docs
    $('.pod pre').addClass('perl').each(function(i, block) {
        hljs.highlightBlock(block);
    });
});</script>
<!-- otrs.github.io -->

	<title>Kernel::System::CustomerUser</title>
<meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
  <script type="text/javascript">

  function toggleCode( id ) {
    if ( document.getElementById )
      elem = document.getElementById( id );
    else if ( document.all )
      elem = eval( "document.all." + id );
    else
      return false;

    elemStyle = elem.style;

    if ( elemStyle.display != "block" ) {
      elemStyle.display = "block"
    } else {
      elemStyle.display = "none"
    }

    return true;
  }
  document.writeln( "<style type=\"text/css\">div.method-source-code { display: none }</style>" )
  </script></head>
<body>
<div class="box">
  <h1 class="t1">OTRS 6.0 API Reference Perl</h1>
  <table>
    <tr>
      <td class="label">Description</td>
      <td class="cell">manuals and libraries</td>
    </tr>
  </table>
</div>
<div class="path">
  <a href="../../index.html">OTRS 6.0 API Reference Perl</a> &gt; Perl Modules &gt;
  Kernel::System::CustomerUser
</div>
<div>
<a href="../../src/Kernel/System/CustomerUser.pm">Source</a>
</div>
<div class="pod">
<!-- INDEX START -->
<h3 id="TOP">Index</h3>
<ul>
	<li><a href="#NAME">NAME</a></li>
	<li><a href="#DESCRIPTION">DESCRIPTION</a></li>
	<li><a href="#PUBLIC_INTERFACE">PUBLIC INTERFACE</a><br />
<ul>
	<li><a href="#new">new()</a></li>
	<li><a href="#CustomerSourceList">CustomerSourceList()</a></li>
	<li><a href="#CustomerSearch">CustomerSearch()</a></li>
	<li><a href="#CustomerSearchDetail">CustomerSearchDetail()</a></li>
	<li><a href="#CustomerUserSearchFields">CustomerUserSearchFields()</a></li>
	<li><a href="#GetFieldConfig">GetFieldConfig()</a></li>
	<li><a href="#GetFieldSelections">GetFieldSelections()</a></li>
	<li><a href="#CustomerIDList">CustomerIDList()</a></li>
	<li><a href="#CustomerName">CustomerName()</a></li>
	<li><a href="#CustomerIDs">CustomerIDs()</a></li>
	<li><a href="#CustomerUserDataGet">CustomerUserDataGet()</a></li>
	<li><a href="#CustomerUserAdd">CustomerUserAdd()</a></li>
	<li><a href="#CustomerUserUpdate">CustomerUserUpdate()</a></li>
	<li><a href="#SetPassword">SetPassword()</a></li>
	<li><a href="#GenerateRandomPassword">GenerateRandomPassword()</a></li>
	<li><a href="#SetPreferences">SetPreferences()</a></li>
	<li><a href="#GetPreferences">GetPreferences()</a></li>
	<li><a href="#SearchPreferences">SearchPreferences()</a></li>
	<li><a href="#TokenGenerate">TokenGenerate()</a></li>
	<li><a href="#TokenCheck">TokenCheck()</a></li>
	<li><a href="#CustomerUserCacheClear">CustomerUserCacheClear()</a></li>
</ul>
</li>
	<li><a href="#TERMS_AND_CONDITIONS">TERMS AND CONDITIONS</a></li>
</ul>
<hr />
<!-- INDEX END -->

<h1 id="NAME">NAME <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>Kernel::System::CustomerUser - customer user lib</p>

<h1 id="DESCRIPTION">DESCRIPTION <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>All customer user functions. E. g. to add and update customer users.</p>

<h1 id="PUBLIC_INTERFACE">PUBLIC INTERFACE <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<h2 id="new">new()</h2>

<p><a href="#" onclick="toggleCode('method_new');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_new">
                                        <pre>

sub new {
    my ( $Type, %Param ) = @_;

    # allocate new hash for object
    my $Self = {};
    bless( $Self, $Type );

    # get config object
    my $ConfigObject = $Kernel::OM->Get('Kernel::Config');

    # load generator customer preferences module
    my $GeneratorModule = $ConfigObject->Get('CustomerPreferences')->{Module}
        || 'Kernel::System::CustomerUser::Preferences::DB';

    # get main object
    my $MainObject = $Kernel::OM->Get('Kernel::System::Main');

    if ( $MainObject->Require($GeneratorModule) ) {
        $Self->{PreferencesObject} = $GeneratorModule->new();
    }

    # load customer user backend module
    SOURCE:
    for my $Count ( '', 1 .. 10 ) {

        next SOURCE if !$ConfigObject->Get("CustomerUser$Count");

        my $GenericModule = $ConfigObject->Get("CustomerUser$Count")->{Module};
        if ( !$MainObject->Require($GenericModule) ) {
            $MainObject->Die("Can't load backend module $GenericModule! $@");
        }

        $Self->{"CustomerUser$Count"} = $GenericModule->new(
            Count             => $Count,
            PreferencesObject => $Self->{PreferencesObject},
            CustomerUserMap   => $ConfigObject->Get("CustomerUser$Count"),
        );
    }

    # init of event handler
    $Self->EventHandlerInit(
        Config => 'CustomerUser::EventModulePost',
    );

    return $Self;
}
</pre></div>

<p>Don't use the constructor directly, use the ObjectManager instead:</p>

<pre>    my $CustomerUserObject = $Kernel::OM-&gt;Get('Kernel::System::CustomerUser');
</pre><h2 id="CustomerSourceList">CustomerSourceList()</h2>

<p><a href="#" onclick="toggleCode('method_CustomerSourceList');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CustomerSourceList">
                                        <pre>

sub CustomerSourceList {
    my ( $Self, %Param ) = @_;

    # get config object
    my $ConfigObject = $Kernel::OM->Get('Kernel::Config');

    my %Data;
    SOURCE:
    for my $Count ( '', 1 .. 10 ) {

        next SOURCE if !$ConfigObject->Get("CustomerUser$Count");
        if ( defined $Param{ReadOnly} ) {
            my $CustomerBackendConfig = $ConfigObject->Get("CustomerUser$Count");
            if ( $Param{ReadOnly} ) {
                next SOURCE if !$CustomerBackendConfig->{ReadOnly};
            }
            else {
                next SOURCE if $CustomerBackendConfig->{ReadOnly};
            }
        }
        $Data{"CustomerUser$Count"} = $ConfigObject->Get("CustomerUser$Count")->{Name}
            || "No Name $Count";
    }
    return %Data;
}
</pre></div>

<p>return customer source list</p>

<pre>    my %List = $CustomerUserObject-&gt;CustomerSourceList(
        ReadOnly =&gt; 0 # optional, 1 returns only RO backends, 0 returns writable, if not passed returns all backends
    );
</pre><h2 id="CustomerSearch">CustomerSearch()</h2>

<p><a href="#" onclick="toggleCode('method_CustomerSearch');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CustomerSearch">
                                        <pre>

sub CustomerSearch {
    my ( $Self, %Param ) = @_;

    # remove leading and ending spaces
    if ( $Param{Search} ) {
        $Param{Search} =~ s/^\s+//;
        $Param{Search} =~ s/\s+$//;
    }

    # Get dynamic fiekd object.
    my $DynamicFieldObject = $Kernel::OM->Get('Kernel::System::DynamicField');

    my $DynamicFieldConfigs = $DynamicFieldObject->DynamicFieldListGet(
        ObjectType => 'CustomerUser',
        Valid      => 1,
    );

    my %DynamicFieldLookup = map { $_->{Name} => $_ } @{$DynamicFieldConfigs};

    # Get dynamic field backend object.
    my $DynamicFieldBackendObject = $Kernel::OM->Get('Kernel::System::DynamicField::Backend');

    my %Data;
    SOURCE:
    for my $Count ( '', 1 .. 10 ) {

        next SOURCE if !$Self->{"CustomerUser$Count"};

        # search dynamic field values, if configured
        my $Map = $Self->{"CustomerUser$Count"}->{CustomerUserMap}->{Map};
        if ( IsArrayRefWithData($Map) ) {

            # fetch dynamic field names that are configured in Map
            # only these will be considered for any other search config
            # [ 'DynamicField_Name_X', undef, 'Name_X', 0, 0, 'dynamic_field', undef, 0, undef, undef, ],
            my %DynamicFieldNames = map { $_->[2] => 1 } grep { $_->[5] eq 'dynamic_field' } @{$Map};

            if ( IsHashRefWithData( \%DynamicFieldNames ) ) {
                my $FoundDynamicFieldObjectIDs;
                my $SearchFields;
                my $SearchParam;

                # check which of the dynamic fields configured in Map are also
                # configured in SearchFields

                # param Search
                if ( defined $Param{Search} && length $Param{Search} ) {
                    $SearchFields = $Self->{"CustomerUser$Count"}->{CustomerUserMap}->{CustomerUserSearchFields};
                    $SearchParam  = $Param{Search};
                }

                # param PostMasterSearch
                elsif ( defined $Param{PostMasterSearch} && length $Param{PostMasterSearch} ) {
                    $SearchFields
                        = $Self->{"CustomerUser$Count"}->{CustomerUserMap}->{CustomerUserPostMasterSearchFields};
                    $SearchParam = $Param{PostMasterSearch};
                }

                # search dynamic field values
                if ( IsArrayRefWithData($SearchFields) ) {
                    my @SearchDynamicFieldNames = grep { exists $DynamicFieldNames{$_} } @{$SearchFields};
                    my @SearchDynamicFieldIDs;

                    my %FoundDynamicFieldObjectIDs;
                    FIELDNAME:
                    for my $FieldName (@SearchDynamicFieldNames) {

                        my $DynamicFieldConfig = $DynamicFieldLookup{$FieldName};

                        next FIELDNAME if !IsHashRefWithData($DynamicFieldConfig);

                        my $DynamicFieldValues = $DynamicFieldBackendObject->ValueSearch(
                            DynamicFieldConfig => $DynamicFieldConfig,
                            Search             => $SearchParam,
                        );

                        if ( IsArrayRefWithData($DynamicFieldValues) ) {
                            for my $DynamicFieldValue ( @{$DynamicFieldValues} ) {
                                $FoundDynamicFieldObjectIDs{ $DynamicFieldValue->{ObjectID} } = 1;
                            }
                        }
                    }

                    $FoundDynamicFieldObjectIDs = [ keys %FoundDynamicFieldObjectIDs ];
                }

                # execute backend search for found object IDs
                # this data is being merged with the following CustomerSearch call
                if ( IsArrayRefWithData($FoundDynamicFieldObjectIDs) ) {

                    my $ObjectNames = $DynamicFieldObject->ObjectMappingGet(
                        ObjectID   => $FoundDynamicFieldObjectIDs,
                        ObjectType => 'CustomerUser',
                    );

                    OBJECTNAME:
                    for my $ObjectName ( values %{$ObjectNames} ) {
                        next OBJECTNAME if exists $Data{$ObjectName};

                        my %SearchParam = %Param;
                        delete $SearchParam{Search};
                        delete $SearchParam{PostMasterSearch};

                        $SearchParam{UserLogin} = $ObjectName;

                        my %SubData = $Self->{"CustomerUser$Count"}->CustomerSearch(%SearchParam);

                        # UserLogin search does a wild-card search, but in this case only the
                        # exact matching user login is relevant
                        if ( IsHashRefWithData( \%SubData ) && exists $SubData{$ObjectName} ) {
                            %Data = (
                                $ObjectName => $SubData{$ObjectName},
                                %Data
                            );
                        }
                    }
                }
            }
        }

        # get customer search result of backend and merge it
        my %SubData = $Self->{"CustomerUser$Count"}->CustomerSearch(%Param);
        %Data = ( %SubData, %Data );
    }
    return %Data;
}
</pre></div>

<p>to search users</p>

<pre>    # text search
    my %List = $CustomerUserObject-&gt;CustomerSearch(
        Search =&gt; '*some*', # also 'hans+huber' possible
        Valid  =&gt; 1,        # (optional) default 1
        Limit  =&gt; 100,      # (optional) overrides limit of the config
    );

    # username search
    my %List = $CustomerUserObject-&gt;CustomerSearch(
        UserLogin =&gt; '*some*',
        Valid     =&gt; 1,         # (optional) default 1
    );

    # email search
    my %List = $CustomerUserObject-&gt;CustomerSearch(
        PostMasterSearch =&gt; 'email@example.com',
        Valid            =&gt; 1,                    # (optional) default 1
    );

    # search by CustomerID
    my %List = $CustomerUserObject-&gt;CustomerSearch(
        CustomerID       =&gt; 'CustomerID123',
        Valid            =&gt; 1,                # (optional) default 1
    );
</pre><h2 id="CustomerSearchDetail">CustomerSearchDetail()</h2>

<p><a href="#" onclick="toggleCode('method_CustomerSearchDetail');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CustomerSearchDetail">
                                        <pre>

sub CustomerSearchDetail {
    my ( $Self, %Param ) = @_;

    # get all general search fields (without a restriction to a source)
    my @AllSearchFields = $Self->CustomerUserSearchFields();

    # generate a hash with the customer user sources which must be searched
    my %SearchCustomerUserSources;

    SOURCE:
    for my $Count ( '', 1 .. 10 ) {
        next SOURCE if !$Self->{"CustomerUser$Count"};

        # get the search fields for the current source
        my @SourceSearchFields = $Self->CustomerUserSearchFields(
            Source => "CustomerUser$Count",
        );
        my %LookupSourceSearchFields = map { $_->{Name} => 1 } @SourceSearchFields;

        # check if all search param exists in the search fields from the current source
        SEARCHFIELD:
        for my $SearchField (@AllSearchFields) {

            next SEARCHFIELD if !$Param{ $SearchField->{Name} };

            next SOURCE if !$LookupSourceSearchFields{ $SearchField->{Name} };
        }
        $SearchCustomerUserSources{"CustomerUser$Count"} = \@SourceSearchFields;
    }

    # set the default behaviour for the return type
    $Param{Result} ||= 'ARRAY';

    if ( $Param{Result} eq 'COUNT' ) {

        my $IDsCount = 0;

        SOURCE:
        for my $Source ( sort keys %SearchCustomerUserSources ) {
            next SOURCE if !$Self->{$Source};

            my $SubIDsCount = $Self->{$Source}->CustomerSearchDetail(
                %Param,
                SearchFields => $SearchCustomerUserSources{$Source},
            );

            return if !defined $SubIDsCount;

            $IDsCount += $SubIDsCount || 0;
        }
        return $IDsCount;
    }
    else {

        my @IDs;

        my $ResultCount = 0;

        SOURCE:
        for my $Source ( sort keys %SearchCustomerUserSources ) {
            next SOURCE if !$Self->{$Source};

            my $SubIDs = $Self->{$Source}->CustomerSearchDetail(
                %Param,
                SearchFields => $SearchCustomerUserSources{$Source},
            );

            return if !defined $SubIDs;

            next SOURCE if !IsArrayRefWithData($SubIDs);

            push @IDs, @{$SubIDs};

            $ResultCount++;
        }

        # if we have more then one search results from diffrent sources, we need a resorting
        # because of the merged single results
        if ( $ResultCount > 1 ) {

            my @UserDataList;

            for my $ID (@IDs) {

                my %UserData = $Self->CustomerUserDataGet(
                    User => $ID,
                );
                push @UserDataList, \%UserData;
            }

            my $OrderBy = 'UserLogin';
            if ( IsArrayRefWithData($Param{OrderBy} ) ) {
                $OrderBy = $Param{OrderBy}->[0];
            }

            if ( IsArrayRefWithData($Param{OrderByDirection}) && $Param{OrderByDirection}->[0] eq 'Up' ) {
                @UserDataList = sort { lc( $a->{$OrderBy} ) cmp lc( $b->{$OrderBy} ) } @UserDataList;
            }
            else {
                @UserDataList = sort { lc( $b->{$OrderBy} ) cmp lc( $a->{$OrderBy} ) } @UserDataList;
            }

            if ( $Param{Limit} && scalar @UserDataList > $Param{Limit} ) {
                splice @UserDataList, $Param{Limit};
            }

            @IDs = map { $_->{UserLogin} } @UserDataList;
        }

        return \@IDs;
    }
}
</pre></div>

<p>To find customer user in the system.</p>

<p>The search criteria are logically AND connected.
When a list is passed as criteria, the individual members are OR connected.
When an undef or a reference to an empty array is passed, then the search criteria
is ignored.</p>

<p>Returns either a list, as an arrayref, or a count of found customer user ids.
The count of results is returned when the parameter <code>Result = 'COUNT'</code> is passed.</p>

<pre>    my $CustomerUserIDsRef = $CustomerUserObject-&gt;CustomerSearchDetail(

        # all search fields possible which are defined in CustomerUser::EnhancedSearchFields
        UserLogin     =&gt; 'example*',                                    # (optional)
        UserFirstname =&gt; 'Firstn*',                                     # (optional)

        # special parameters
        CustomerCompanySearchCustomerIDs =&gt; [ 'example.com' ],          # (optional)
        ExcludeUserLogins                =&gt; [ 'example', 'doejohn' ],   # (optional)

        # array parameters are used with logical OR operator (all values are possible which
        are defined in the config selection hash for the field)
        UserCountry              =&gt; [ 'Austria', 'Germany', ],          # (optional)

        # DynamicFields
        #   At least one operator must be specified. Operators will be connected with AND,
        #       values in an operator with OR.
        #   You can also pass more than one argument to an operator: ['value1', 'value2']
        DynamicField_FieldNameX =&gt; {
            Equals            =&gt; 123,
            Like              =&gt; 'value*',                # &quot;equals&quot; operator with wildcard support
            GreaterThan       =&gt; '2001-01-01 01:01:01',
            GreaterThanEquals =&gt; '2001-01-01 01:01:01',
            SmallerThan       =&gt; '2002-02-02 02:02:02',
            SmallerThanEquals =&gt; '2002-02-02 02:02:02',
        }

        OrderBy =&gt; [ 'UserLogin', 'UserCustomerID' ],                   # (optional)
        # ignored if the result type is 'COUNT'
        # default: [ 'UserLogin' ]
        # (all search fields possible which are defined in
        CustomerUser::EnhancedSearchFields)

        # Additional information for OrderBy:
        # The OrderByDirection can be specified for each OrderBy attribute.
        # The pairing is made by the array indices.

        OrderByDirection =&gt; [ 'Down', 'Up' ],                          # (optional)
        # ignored if the result type is 'COUNT'
        # (Down | Up) Default: [ 'Down' ]

        Result =&gt; 'ARRAY' || 'COUNT',                                  # (optional)
        # default: ARRAY, returns an array of change ids
        # COUNT returns a scalar with the number of found changes

        Limit =&gt; 100,                                                  # (optional)
        # ignored if the result type is 'COUNT'
    );
</pre><p>Returns:</p>

<p>Result: 'ARRAY'</p>

<pre>    @CustomerUserIDs = ( 1, 2, 3 );
</pre><p>Result: 'COUNT'</p>

<pre>    $CustomerUserIDs = 10;
</pre><h2 id="CustomerUserSearchFields">CustomerUserSearchFields()</h2>

<p><a href="#" onclick="toggleCode('method_CustomerUserSearchFields');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CustomerUserSearchFields">
                                        <pre>

sub CustomerUserSearchFields {
    my ( $Self, %Param ) = @_;

    # Get the search fields from all customer user maps (merge from all maps together).
    my @SearchFields;

    my %SearchFieldsExists;

    SOURCE:
    for my $Count ( '', 1 .. 10 ) {
        next SOURCE if !$Self->{"CustomerUser$Count"};
        next SOURCE if $Param{Source} && $Param{Source} ne "CustomerUser$Count";

        ENTRY:
        for my $Entry ( @{ $Self->{"CustomerUser$Count"}->{CustomerUserMap}->{Map} } ) {

            my $SearchFieldName = $Entry->[0];

            next ENTRY if $SearchFieldsExists{$SearchFieldName};
            next ENTRY if $SearchFieldName =~ m{(Password|Pw)\d*$}smxi;

            # Remeber the already collected search field name.
            $SearchFieldsExists{$SearchFieldName} = 1;

            my %FieldConfig = $Self->GetFieldConfig(
                FieldName => $SearchFieldName,
                Source    => $Param{Source},     # to get the right database field for the given source
            );

            next SEARCHFIELDNAME if !%FieldConfig;

            my %SearchFieldData = (
                %FieldConfig,
                Name => $SearchFieldName,
            );

            my %SelectionsData = $Self->GetFieldSelections(
                FieldName => $SearchFieldName,
            );

            if ( $SearchFieldData{StorageType} eq 'dynamic_field' ) {
                $SearchFieldData{Type} = 'DynamicField';
            }
            elsif (%SelectionsData) {
                $SearchFieldData{Type}           = 'Selection';
                $SearchFieldData{SelectionsData} = \%SelectionsData;
            }
            else {
                $SearchFieldData{Type} = 'Input';
            }

            push @SearchFields, \%SearchFieldData;
        }
    }

    return @SearchFields;
}
</pre></div>

<p>Get a list of the defined search fields (optional only the relevant fields for the given source).</p>

<pre>    my @SeachFields = $CustomerUserObject-&gt;CustomerUserSearchFields(
        Source =&gt; 'CustomerUser', # optional, but important in the CustomerSearchDetail to get the right database fields
    );
</pre><p>Returns an array of hash references.</p>

<pre>    @SeachFields = (
        {
            Name          =&gt; 'UserEmail',
            Label         =&gt; 'Email',
            Type          =&gt; 'Input',
            DatabaseField =&gt; 'mail',
        },
        {
            Name           =&gt; 'UserCountry',
            Label          =&gt; 'Country',
            Type           =&gt; 'Selection',
            SelectionsData =&gt; {
                'Germany'        =&gt; 'Germany',
                'United Kingdom' =&gt; 'United Kingdom',
                'United States'  =&gt; 'United States',
                ...
            },
            DatabaseField =&gt; 'country',
        },
        {
            Name          =&gt; 'DynamicField_SkypeAccountName',
            Label         =&gt; '',
            Type          =&gt; 'DynamicField',
            DatabaseField =&gt; 'SkypeAccountName',
        },
    );
</pre><h2 id="GetFieldConfig">GetFieldConfig()</h2>

<p><a href="#" onclick="toggleCode('method_GetFieldConfig');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_GetFieldConfig">
                                        <pre>

sub GetFieldConfig {
    my ( $Self, %Param ) = @_;

    if ( !$Param{FieldName} ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "Need FieldName!"
        );
        return;
    }

    SOURCE:
    for my $Count ( '', 1 .. 10 ) {
        next SOURCE if !$Self->{"CustomerUser$Count"};
        next SOURCE if $Param{Source} && $Param{Source} ne "CustomerUser$Count";

        # Search the right field and return some config information from the field.
        ENTRY:
        for my $Entry ( @{ $Self->{"CustomerUser$Count"}->{CustomerUserMap}->{Map} } ) {
            next ENTRY if $Param{FieldName} ne $Entry->[0];

            my %FieldConfig = (
                Label         => $Entry->[1],
                DatabaseField => $Entry->[2],
                StorageType   => $Entry->[5],
            );

            return %FieldConfig;
        }
    }

    return;
}
</pre></div>

<p>This function collect some field config information from the customer user map.</p>

<pre>    my %FieldConfig = $CustomerUserObject-&gt;GetFieldConfig(
        FieldName =&gt; 'UserEmail',
        Source    =&gt; 'CustomerUser', # optional
    );
</pre><p>Returns some field config information:</p>

<pre>    my %FieldConfig = (
        Label         =&gt; 'Email',
        DatabaseField =&gt; 'email',
        StorageType   =&gt; 'var',
    );
</pre><h2 id="GetFieldSelections">GetFieldSelections()</h2>

<p><a href="#" onclick="toggleCode('method_GetFieldSelections');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_GetFieldSelections">
                                        <pre>

sub GetFieldSelections {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    if ( !$Param{FieldName} ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "Need FieldName!"
        );
        return;
    }

    my %SelectionsData;

    SOURCE:
    for my $Count ( '', 1 .. 10 ) {
        next SOURCE if !$Self->{"CustomerUser$Count"};
        next SOURCE if !$Self->{"CustomerUser$Count"}->{CustomerUserMap}->{Selections}->{ $Param{FieldName} };

        %SelectionsData = (
            %SelectionsData, %{ $Self->{"CustomerUser$Count"}->{CustomerUserMap}->{Selections}->{ $Param{FieldName} } }
        );
    }

    # Make sure the encoding stamp is set.
    for my $Key ( sort keys %SelectionsData ) {
        $SelectionsData{$Key} = $Kernel::OM->Get('Kernel::System::Encode')->EncodeInput( $SelectionsData{$Key} );
    }

    # Default handling for field 'ValidID'.
    if ( !%SelectionsData && $Param{FieldName} =~ /^ValidID/i ) {
        %SelectionsData = $Kernel::OM->Get('Kernel::System::Valid')->ValidList();
    }

    return %SelectionsData;
}
</pre></div>

<p>This function collect the selections for the given field name, if the field has some selections.</p>

<pre>    my %SelectionsData = $CustomerUserObject-&gt;GetFieldSelections(
        FieldName =&gt; 'UserTitle',
    );
</pre><p>Returns the selections for the given field name (merged from all sources) or a empty hash:</p>

<pre>    my %SelectionData = (
        'Mr.'  =&gt; 'Mr.',
        'Mrs.' =&gt; 'Mrs.',
    );
</pre><h2 id="CustomerIDList">CustomerIDList()</h2>

<p><a href="#" onclick="toggleCode('method_CustomerIDList');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CustomerIDList">
                                        <pre>

sub CustomerIDList {
    my ( $Self, %Param ) = @_;

    my @Data;
    SOURCE:
    for my $Count ( '', 1 .. 10 ) {

        next SOURCE if !$Self->{"CustomerUser$Count"};

        # get customer list result of backend and merge it
        push @Data, $Self->{"CustomerUser$Count"}->CustomerIDList(%Param);
    }

    # make entries unique
    my %Tmp;
    @Tmp{@Data} = undef;
    @Data = sort { lc $a cmp lc $b } keys %Tmp;

    return @Data;
}
</pre></div>

<p>return a list of with all known unique CustomerIDs of the registered customers users (no SearchTerm),
or a filtered list where the CustomerIDs must contain a search term.</p>

<pre>    my @CustomerIDs = $CustomerUserObject-&gt;CustomerIDList(
        SearchTerm  =&gt; 'somecustomer',    # optional
        Valid       =&gt; 1,                 # optional
    );
</pre><h2 id="CustomerName">CustomerName()</h2>

<p><a href="#" onclick="toggleCode('method_CustomerName');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CustomerName">
                                        <pre>

sub CustomerName {
    my ( $Self, %Param ) = @_;

    SOURCE:
    for my $Count ( '', 1 .. 10 ) {

        next SOURCE if !$Self->{"CustomerUser$Count"};

        # get customer name and return it
        my $Name = $Self->{"CustomerUser$Count"}->CustomerName(%Param);
        if ($Name) {
            return $Name;
        }
    }
    return;
}
</pre></div>

<p>get customer user name</p>

<pre>    my $Name = $CustomerUserObject-&gt;CustomerName(
        UserLogin =&gt; 'some-login',
    );
</pre><h2 id="CustomerIDs">CustomerIDs()</h2>

<p><a href="#" onclick="toggleCode('method_CustomerIDs');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CustomerIDs">
                                        <pre>

sub CustomerIDs {
    my ( $Self, %Param ) = @_;

    SOURCE:
    for my $Count ( '', 1 .. 10 ) {

        next SOURCE if !$Self->{"CustomerUser$Count"};

        # get customer id's and return it
        my @CustomerIDs = $Self->{"CustomerUser$Count"}->CustomerIDs(%Param);
        if (@CustomerIDs) {
            return @CustomerIDs;
        }
    }
    return;
}
</pre></div>

<p>get customer user customer ids</p>

<pre>    my @CustomerIDs = $CustomerUserObject-&gt;CustomerIDs(
        User =&gt; 'some-login',
    );
</pre><h2 id="CustomerUserDataGet">CustomerUserDataGet()</h2>

<p><a href="#" onclick="toggleCode('method_CustomerUserDataGet');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CustomerUserDataGet">
                                        <pre>

sub CustomerUserDataGet {
    my ( $Self, %Param ) = @_;

    return if !$Param{User};

    # fetch dynamic field configurations for CustomerUser.
    my $DynamicFieldConfigs = $Kernel::OM->Get('Kernel::System::DynamicField')->DynamicFieldListGet(
        ObjectType => 'CustomerUser',
        Valid      => 1,
    );

    my %DynamicFieldLookup = map { $_->{Name} => $_ } @{$DynamicFieldConfigs};

    # Get needed objects.
    my $ConfigObject              = $Kernel::OM->Get('Kernel::Config');
    my $CustomerCompanyObject     = $Kernel::OM->Get('Kernel::System::CustomerCompany');
    my $DynamicFieldBackendObject = $Kernel::OM->Get('Kernel::System::DynamicField::Backend');

    SOURCE:
    for my $Count ( '', 1 .. 10 ) {

        next SOURCE if !$Self->{"CustomerUser$Count"};

        my %Customer = $Self->{"CustomerUser$Count"}->CustomerUserDataGet( %Param, );
        next SOURCE if !%Customer;

        # add preferences defaults
        my $Config = $ConfigObject->Get('CustomerPreferencesGroups');
        if ($Config) {
            KEY:
            for my $Key ( sort keys %{$Config} ) {

                next KEY if !defined $Config->{$Key}->{DataSelected};
                next KEY if defined $Customer{ $Config->{$Key}->{PrefKey} };

                # set default data
                $Customer{ $Config->{$Key}->{PrefKey} } = $Config->{$Key}->{DataSelected};
            }
        }

        # check if customer company support is enabled and get company data
        my %Company;
        if (
            $ConfigObject->Get("CustomerCompany")
            && $ConfigObject->Get("CustomerUser$Count")->{CustomerCompanySupport}
            )
        {
            %Company = $CustomerCompanyObject->CustomerCompanyGet(
                CustomerID => $Customer{UserCustomerID},
            );

            $Company{CustomerCompanyValidID} = $Company{ValidID};
        }

        # fetch dynamic field values
        if ( IsArrayRefWithData( $Self->{"CustomerUser$Count"}->{CustomerUserMap}->{Map} ) ) {
            CUSTOMERUSERFIELD:
            for my $CustomerUserField ( @{ $Self->{"CustomerUser$Count"}->{CustomerUserMap}->{Map} } ) {
                next CUSTOMERUSERFIELD if $CustomerUserField->[5] ne 'dynamic_field';
                next CUSTOMERUSERFIELD if !$DynamicFieldLookup{ $CustomerUserField->[2] };

                my $Value = $DynamicFieldBackendObject->ValueGet(
                    DynamicFieldConfig => $DynamicFieldLookup{ $CustomerUserField->[2] },
                    ObjectName         => $Customer{UserID},
                );

                $Customer{ $CustomerUserField->[0] } = $Value;
            }
        }

        # return customer data
        return (
            %Company,
            %Customer,
            Source        => "CustomerUser$Count",
            Config        => $ConfigObject->Get("CustomerUser$Count"),
            CompanyConfig => $ConfigObject->Get( $Company{Source} // 'CustomerCompany' ),
        );
    }

    return;
}
</pre></div>

<p>get user data (UserLogin, UserFirstname, UserLastname, UserEmail, ...)</p>

<pre>    my %User = $CustomerUserObject-&gt;CustomerUserDataGet(
        User =&gt; 'franz',
    );
</pre><h2 id="CustomerUserAdd">CustomerUserAdd()</h2>

<p><a href="#" onclick="toggleCode('method_CustomerUserAdd');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CustomerUserAdd">
                                        <pre>

sub CustomerUserAdd {
    my ( $Self, %Param ) = @_;

    # check data source
    if ( !$Param{Source} ) {
        $Param{Source} = 'CustomerUser';
    }

    # check if user exists
    if ( $Param{UserLogin} ) {
        my %User = $Self->CustomerUserDataGet( User => $Param{UserLogin} );
        if (%User) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "User already exists '$Param{UserLogin}'!",
            );
            return;
        }
    }

    # store customer user data
    my $Result = $Self->{ $Param{Source} }->CustomerUserAdd(%Param);
    return if !$Result;

    # trigger event
    $Self->EventHandler(
        Event => 'CustomerUserAdd',
        Data  => {
            UserLogin => $Param{UserLogin},
            NewData   => \%Param,
        },
        UserID => $Param{UserID},
    );

    return $Result;

}
</pre></div>

<p>to add new customer users</p>

<pre>    my $UserLogin = $CustomerUserObject-&gt;CustomerUserAdd(
        Source         =&gt; 'CustomerUser', # CustomerUser source config
        UserFirstname  =&gt; 'Huber',
        UserLastname   =&gt; 'Manfred',
        UserCustomerID =&gt; 'A124',
        UserLogin      =&gt; 'mhuber',
        UserPassword   =&gt; 'some-pass', # not required
        UserEmail      =&gt; 'email@example.com',
        ValidID        =&gt; 1,
        UserID         =&gt; 123,
    );
</pre><h2 id="CustomerUserUpdate">CustomerUserUpdate()</h2>

<p><a href="#" onclick="toggleCode('method_CustomerUserUpdate');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CustomerUserUpdate">
                                        <pre>

sub CustomerUserUpdate {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    if ( !$Param{UserLogin} ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "Need UserLogin!"
        );
        return;
    }

    # check for UserLogin-renaming and if new UserLogin already exists...
    if ( $Param{ID} && ( lc $Param{UserLogin} ne lc $Param{ID} ) ) {
        my %User = $Self->CustomerUserDataGet( User => $Param{UserLogin} );
        if (%User) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "User already exists '$Param{UserLogin}'!",
            );
            return;
        }
    }

    # check if user exists
    my %User = $Self->CustomerUserDataGet( User => $Param{ID} || $Param{UserLogin} );
    if ( !%User ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "No such user '$Param{UserLogin}'!",
        );
        return;
    }

    my $Result = $Self->{ $User{Source} }->CustomerUserUpdate(%Param);
    return if !$Result;

    # trigger event
    $Self->EventHandler(
        Event => 'CustomerUserUpdate',
        Data  => {
            UserLogin => $Param{ID} || $Param{UserLogin},
            NewData   => \%Param,
            OldData   => \%User,
        },
        UserID => $Param{UserID},
    );

    return $Result;
}
</pre></div>

<p>to update customer users</p>

<pre>    $CustomerUserObject-&gt;CustomerUserUpdate(
        Source        =&gt; 'CustomerUser', # CustomerUser source config
        ID            =&gt; 'mh'            # current user login
        UserLogin     =&gt; 'mhuber',       # new user login
        UserFirstname =&gt; 'Huber',
        UserLastname  =&gt; 'Manfred',
        UserPassword  =&gt; 'some-pass',    # not required
        UserEmail     =&gt; 'email@example.com',
        ValidID       =&gt; 1,
        UserID        =&gt; 123,
    );
</pre><h2 id="SetPassword">SetPassword()</h2>

<p><a href="#" onclick="toggleCode('method_SetPassword');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_SetPassword">
                                        <pre>

sub SetPassword {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    if ( !$Param{UserLogin} ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => 'User UserLogin!'
        );
        return;
    }

    # check if user exists
    my %User = $Self->CustomerUserDataGet( User => $Param{UserLogin} );
    if ( !%User ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "No such user '$Param{UserLogin}'!",
        );
        return;
    }
    return $Self->{ $User{Source} }->SetPassword(%Param);
}
</pre></div>

<p>to set customer users passwords</p>

<pre>    $CustomerUserObject-&gt;SetPassword(
        UserLogin =&gt; 'some-login',
        PW        =&gt; 'some-new-password'
    );
</pre><h2 id="GenerateRandomPassword">GenerateRandomPassword()</h2>

<p><a href="#" onclick="toggleCode('method_GenerateRandomPassword');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_GenerateRandomPassword">
                                        <pre>

sub GenerateRandomPassword {
    my ( $Self, %Param ) = @_;

    return $Self->{CustomerUser}->GenerateRandomPassword(%Param);
}
</pre></div>

<p>generate a random password</p>

<pre>    my $Password = $CustomerUserObject-&gt;GenerateRandomPassword();

    or

    my $Password = $CustomerUserObject-&gt;GenerateRandomPassword(
        Size =&gt; 16,
    );
</pre><h2 id="SetPreferences">SetPreferences()</h2>

<p><a href="#" onclick="toggleCode('method_SetPreferences');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_SetPreferences">
                                        <pre>

sub SetPreferences {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    if ( !$Param{UserID} ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => 'Need UserID!'
        );
        return;
    }

    # check if user exists
    my %User = $Self->CustomerUserDataGet( User => $Param{UserID} );
    if ( !%User ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "No such user '$Param{UserID}'!",
        );
        return;
    }

    # call new api (2.4.8 and higher)
    if ( $Self->{ $User{Source} }->can('SetPreferences') ) {
        return $Self->{ $User{Source} }->SetPreferences(%Param);
    }

    # call old api
    return $Self->{PreferencesObject}->SetPreferences(%Param);
}
</pre></div>

<p>set customer user preferences</p>

<pre>    $CustomerUserObject-&gt;SetPreferences(
        Key    =&gt; 'UserComment',
        Value  =&gt; 'some comment',
        UserID =&gt; 'some-login',
    );
</pre><h2 id="GetPreferences">GetPreferences()</h2>

<p><a href="#" onclick="toggleCode('method_GetPreferences');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_GetPreferences">
                                        <pre>

sub GetPreferences {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    if ( !$Param{UserID} ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => 'Need UserID!'
        );
        return;
    }

    # check if user exists
    my %User = $Self->CustomerUserDataGet( User => $Param{UserID} );
    if ( !%User ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "No such user '$Param{UserID}'!",
        );
        return;
    }

    # call new api (2.4.8 and higher)
    if ( $Self->{ $User{Source} }->can('GetPreferences') ) {
        return $Self->{ $User{Source} }->GetPreferences(%Param);
    }

    # call old api
    return $Self->{PreferencesObject}->GetPreferences(%Param);
}
</pre></div>

<p>get customer user preferences</p>

<pre>    my %Preferences = $CustomerUserObject-&gt;GetPreferences(
        UserID =&gt; 'some-login',
    );
</pre><h2 id="SearchPreferences">SearchPreferences()</h2>

<p><a href="#" onclick="toggleCode('method_SearchPreferences');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_SearchPreferences">
                                        <pre>

sub SearchPreferences {
    my ( $Self, %Param ) = @_;

    my %Data;
    SOURCE:
    for my $Count ( '', 1 .. 10 ) {

        next SOURCE if !$Self->{"CustomerUser$Count"};

        # get customer search result of backend and merge it
        # call new api (2.4.8 and higher)
        my %SubData;
        if ( $Self->{"CustomerUser$Count"}->can('SearchPreferences') ) {
            %SubData = $Self->{"CustomerUser$Count"}->SearchPreferences(%Param);
        }

        # call old api
        else {
            %SubData = $Self->{PreferencesObject}->SearchPreferences(%Param);
        }
        %Data = ( %SubData, %Data );
    }

    return %Data;
}
</pre></div>

<p>search in user preferences</p>

<pre>    my %UserList = $CustomerUserObject-&gt;SearchPreferences(
        Key   =&gt; 'UserSomeKey',
        Value =&gt; 'SomeValue',   # optional, limit to a certain value/pattern
    );
</pre><h2 id="TokenGenerate">TokenGenerate()</h2>

<p><a href="#" onclick="toggleCode('method_TokenGenerate');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_TokenGenerate">
                                        <pre>

sub TokenGenerate {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    if ( !$Param{UserID} ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "Need UserID!"
        );
        return;
    }

    my $Token = $Kernel::OM->Get('Kernel::System::Main')->GenerateRandomString(
        Length => 14,
    );

    # save token in preferences
    $Self->SetPreferences(
        Key    => 'UserToken',
        Value  => $Token,
        UserID => $Param{UserID},
    );

    return $Token;
}
</pre></div>

<p>generate a random token</p>

<pre>    my $Token = $UserObject-&gt;TokenGenerate(
        UserID =&gt; 123,
    );
</pre><h2 id="TokenCheck">TokenCheck()</h2>

<p><a href="#" onclick="toggleCode('method_TokenCheck');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_TokenCheck">
                                        <pre>

sub TokenCheck {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    if ( !$Param{Token} || !$Param{UserID} ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "Need Token and UserID!"
        );
        return;
    }

    # get preferences token
    my %Preferences = $Self->GetPreferences(
        UserID => $Param{UserID},
    );

    # check requested vs. stored token
    return if !$Preferences{UserToken};
    return if $Preferences{UserToken} ne $Param{Token};

    # reset password token
    $Self->SetPreferences(
        Key    => 'UserToken',
        Value  => '',
        UserID => $Param{UserID},
    );

    return 1;
}
</pre></div>

<p>check password token</p>

<pre>    my $Valid = $UserObject-&gt;TokenCheck(
        Token  =&gt; $Token,
        UserID =&gt; 123,
    );
</pre><h2 id="CustomerUserCacheClear">CustomerUserCacheClear()</h2>

<p><a href="#" onclick="toggleCode('method_CustomerUserCacheClear');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CustomerUserCacheClear">
                                        <pre>

sub CustomerUserCacheClear {
    my ( $Self, %Param ) = @_;

    SOURCE:
    for my $Count ( '', 1 .. 10 ) {

        next SOURCE if !$Self->{"CustomerUser$Count"};
        $Self->{"CustomerUser$Count"}->_CustomerUserCacheClear(
            UserLogin => $Param{UserLogin},
        );
    }

    return 1;
}
</pre></div>

<p>clear cache of customer user data</p>

<pre>    $CustomerUserObject-&gt;CustomerUserCacheClear(
        UserLogin =&gt; 'mhuber',
    );
</pre><h1 id="TERMS_AND_CONDITIONS">TERMS AND CONDITIONS <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>This software is part of the OTRS project (<a href="http://otrs.org/">http://otrs.org/</a>).</p>

<p>This software comes with ABSOLUTELY NO WARRANTY. For details, see
the enclosed file COPYING for license information (AGPL). If you
did not receive this file, see <a href="http://www.gnu.org/licenses/agpl.txt">http://www.gnu.org/licenses/agpl.txt</a>.</p>


</div><div class="footer">generated by <a href="http://search.cpan.org/perldoc?Pod%3A%3AProjectDocs">Pod::ProjectDocs</a></div></body>
</html>
