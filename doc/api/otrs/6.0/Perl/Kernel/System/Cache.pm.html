<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.1//EN" "http://www.w3.org/TR/xhtml11/DTD/xhtml11.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en">
<head>
<!-- otrs.github.io -->
<link href="https://netdna.bootstrapcdn.com/font-awesome/4.0.3/css/font-awesome.css" rel="stylesheet">
<link href="https://fonts.googleapis.com/css?family=Roboto" rel="stylesheet" type="text/css">
<link rel="stylesheet" href="../../../../../../documentation.css">
<script src="https://code.jquery.com/jquery-1.11.0.min.js"></script>
<script type="text/javascript" src="../../../../../../documentation.js"></script>
<link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/9.7.0/styles/default.min.css">
<script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/9.7.0/highlight.min.js"></script>
<script>
$(document).ready(function() {
    // programlistings in manuals
    $('pre.programlisting').each(function(i, block) {
        hljs.highlightBlock(block);
    });
    // code snippets in Perl API docs
    $('.pod pre').addClass('perl').each(function(i, block) {
        hljs.highlightBlock(block);
    });
});</script>
<!-- otrs.github.io -->

	<title>Kernel::System::Cache</title>
<meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
  <script type="text/javascript">

  function toggleCode( id ) {
    if ( document.getElementById )
      elem = document.getElementById( id );
    else if ( document.all )
      elem = eval( "document.all." + id );
    else
      return false;

    elemStyle = elem.style;

    if ( elemStyle.display != "block" ) {
      elemStyle.display = "block"
    } else {
      elemStyle.display = "none"
    }

    return true;
  }
  document.writeln( "<style type=\"text/css\">div.method-source-code { display: none }</style>" )
  </script></head>
<body>
<div class="box">
  <h1 class="t1">OTRS 6.0 API Reference Perl</h1>
  <table>
    <tr>
      <td class="label">Description</td>
      <td class="cell">manuals and libraries</td>
    </tr>
  </table>
</div>
<div class="path">
  <a href="../../index.html">OTRS 6.0 API Reference Perl</a> &gt; Perl Modules &gt;
  Kernel::System::Cache
</div>
<div>
<a href="../../src/Kernel/System/Cache.pm">Source</a>
</div>
<div class="pod">
<!-- INDEX START -->
<h3 id="TOP">Index</h3>
<ul>
	<li><a href="#NAME">NAME</a></li>
	<li><a href="#DESCRIPTION">DESCRIPTION</a></li>
	<li><a href="#PUBLIC_INTERFACE">PUBLIC INTERFACE</a><br />
<ul>
	<li><a href="#new">new()</a></li>
	<li><a href="#Configure">Configure()</a></li>
	<li><a href="#Set">Set()</a></li>
	<li><a href="#Get">Get()</a></li>
	<li><a href="#Delete">Delete()</a></li>
	<li><a href="#CleanUp">CleanUp()</a></li>
</ul>
</li>
	<li><a href="#TERMS_AND_CONDITIONS">TERMS AND CONDITIONS</a></li>
</ul>
<hr />
<!-- INDEX END -->

<h1 id="NAME">NAME <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>Kernel::System::Cache - Key/value based data cache for OTRS</p>

<h1 id="DESCRIPTION">DESCRIPTION <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>This is a simple data cache. It can store key/value data both
in memory and in a configured cache backend for persistent caching.</p>

<p>This can be controlled via the config settings <code>Cache::InMemory</code> and
<code>Cache::InBackend</code>. The backend can also be selected with the config setting
<code>Cache::Module</code> and defaults to file system based storage for permanent caching.</p>

<h1 id="PUBLIC_INTERFACE">PUBLIC INTERFACE <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<h2 id="new">new()</h2>

<p><a href="#" onclick="toggleCode('method_new');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_new">
                                        <pre>

sub new {
    my ( $Type, %Param ) = @_;

    # allocate new hash for object
    my $Self = {};
    bless( $Self, $Type );

    # 0=off; 1=set+get_cache; 2=+delete+get_request;
    $Self->{Debug} = $Param{Debug} || 0;

    # cache backend
    my $CacheModule = $Kernel::OM->Get('Kernel::Config')->Get('Cache::Module')
        || 'Kernel::System::Cache::FileStorable';

    # Store backend in $Self for fastest access.
    $Self->{CacheObject}    = $Kernel::OM->Get($CacheModule);
    $Self->{CacheInMemory}  = $Kernel::OM->Get('Kernel::Config')->Get('Cache::InMemory') // 1;
    $Self->{CacheInBackend} = $Kernel::OM->Get('Kernel::Config')->Get('Cache::InBackend') // 1;

    return $Self;
}
</pre></div>

<p>Don't use the constructor directly, use the ObjectManager instead:</p>

<pre>    my $CacheObject = $Kernel::OM-&gt;Get('Kernel::System::Cache');
</pre><h2 id="Configure">Configure()</h2>

<p><a href="#" onclick="toggleCode('method_Configure');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_Configure">
                                        <pre>

sub Configure {
    my ( $Self, %Param ) = @_;

    SETTING:
    for my $Setting (qw(CacheInMemory CacheInBackend)) {
        next SETTING if !exists $Param{$Setting};
        $Self->{$Setting} = $Param{$Setting} ? 1 : 0;
    }

    return;
}
</pre></div>

<p>change cache configuration settings at runtime. You can use this to disable the cache in
environments where it is not desired, such as in long running scripts.</p>

<p>please, to turn CacheInMemory off in persistent environments.</p>

<pre>    $CacheObject-&gt;Configure(
        CacheInMemory  =&gt; 1,    # optional
        CacheInBackend =&gt; 1,    # optional
    );
</pre><h2 id="Set">Set()</h2>

<p><a href="#" onclick="toggleCode('method_Set');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_Set">
                                        <pre>

sub Set {
    my ( $Self, %Param ) = @_;

    for my $Needed (qw(Type Key Value)) {
        if ( !defined $Param{$Needed} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Need $Needed!",
            );
            return;
        }
    }

    # set default TTL to 20 days
    $Param{TTL} //= 60 * 60 * 24 * 20;

    # Enforce cache type restriction to make sure it works properly on all file systems.
    if ( $Param{Type} !~ m{ \A [a-zA-Z0-9_]+ \z}smx ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message =>
                "Cache Type '$Param{Type}' contains invalid characters, use [a-zA-Z0-9_] only!",
        );
        return;
    }

    # debug
    if ( $Self->{Debug} > 0 ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'notice',
            Message  => "Set Key:$Param{Key} TTL:$Param{TTL}!",
        );
    }

    # Set in-memory cache.
    if ( $Self->{CacheInMemory} && ( $Param{CacheInMemory} // 1 ) ) {
        $Self->{Cache}->{ $Param{Type} }->{ $Param{Key} } = $Param{Value};
    }

    # If in-memory caching is not active, make sure the in-memory
    #   cache is not in an inconsistent state.
    else {
        delete $Self->{Cache}->{ $Param{Type} }->{ $Param{Key} };
    }

    # Set persistent cache.
    if ( $Self->{CacheInBackend} && ( $Param{CacheInBackend} // 1 ) ) {
        return $Self->{CacheObject}->Set(%Param);
    }

    # If persistent caching is not active, make sure the persistent
    #   cache is not in an inconsistent state.
    else {
        return $Self->{CacheObject}->Delete(%Param);
    }

    return 1;
}
</pre></div>

<p>store a value in the cache.</p>

<pre>    $CacheObject-&gt;Set(
        Type  =&gt; 'ObjectName',      # only [a-zA-Z0-9_] chars usable
        Key   =&gt; 'SomeKey',
        Value =&gt; 'Some Value',
        TTL   =&gt; 60 * 60 * 24 * 20, # seconds, this means 20 days
    );
</pre><p>The Type here refers to the group of entries that should be cached and cleaned up together,
usually this will represent the OTRS object that is supposed to be cached, like 'Ticket'.</p>

<p>The Key identifies the entry (together with the type) for retrieval and deletion of this value.</p>

<p>The TTL controls when the cache will expire. Please note that the in-memory cache is not persistent
and thus has no TTL/expiry mechanism.</p>

<p>Please note that if you store complex data, you have to make sure that the data is not modified
in other parts of the code as the in-memory cache only refers to it. Otherwise also the cache would
contain the modifications. If you cannot avoid this, you can disable the in-memory cache for this
value:</p>

<pre>    $CacheObject-&gt;Set(
        Type  =&gt; 'ObjectName',
        Key   =&gt; 'SomeKey',
        Value =&gt; { ... complex data ... },

        TTL            =&gt; 60 * 60 * 24 * 1,  # optional, default 20 days
        CacheInMemory  =&gt; 0,                 # optional, defaults to 1
        CacheInBackend =&gt; 1,                 # optional, defaults to 1
    );
</pre><h2 id="Get">Get()</h2>

<p><a href="#" onclick="toggleCode('method_Get');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_Get">
                                        <pre>

sub Get {
    my ( $Self, %Param ) = @_;

    for my $Needed (qw(Type Key)) {
        if ( !$Param{$Needed} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Need $Needed!",
            );
            return;
        }
    }

    # check in-memory cache
    if ( $Self->{CacheInMemory} && ( $Param{CacheInMemory} // 1 ) ) {
        if ( exists $Self->{Cache}->{ $Param{Type} }->{ $Param{Key} } ) {
            return $Self->{Cache}->{ $Param{Type} }->{ $Param{Key} };
        }
    }

    return if ( !$Self->{CacheInBackend} || !( $Param{CacheInBackend} // 1 ) );

    # check persistent cache
    my $Value = $Self->{CacheObject}->Get(%Param);

    # set in-memory cache
    if ( defined $Value ) {
        if ( $Self->{CacheInMemory} && ( $Param{CacheInMemory} // 1 ) ) {
            $Self->{Cache}->{ $Param{Type} }->{ $Param{Key} } = $Value;
        }
    }

    return $Value;
}
</pre></div>

<p>fetch a value from the cache.</p>

<pre>    my $Value = $CacheObject-&gt;Get(
        Type =&gt; 'ObjectName',       # only [a-zA-Z0-9_] chars usable
        Key  =&gt; 'SomeKey',
    );
</pre><p>Please note that if you store complex data, you have to make sure that the data is not modified
in other parts of the code as the in-memory cache only refers to it. Otherwise also the cache would
contain the modifications. If you cannot avoid this, you can disable the in-memory cache for this
value:</p>

<pre>    my $Value = $CacheObject-&gt;Get(
        Type =&gt; 'ObjectName',
        Key  =&gt; 'SomeKey',

        CacheInMemory =&gt; 0,     # optional, defaults to 1
        CacheInBackend =&gt; 1,    # optional, defaults to 1
    );



</pre><h2 id="Delete">Delete()</h2>

<p><a href="#" onclick="toggleCode('method_Delete');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_Delete">
                                        <pre>

sub Delete {
    my ( $Self, %Param ) = @_;

    for my $Needed (qw(Type Key)) {
        if ( !$Param{$Needed} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Need $Needed!",
            );
            return;
        }
    }

    # Delete and cleanup operations should also be done if the cache is disabled
    #   to avoid inconsistent states.

    # delete from in-memory cache
    delete $Self->{Cache}->{ $Param{Type} }->{ $Param{Key} };

    # delete from persistent cache
    return $Self->{CacheObject}->Delete(%Param);
}
</pre></div>

<p>deletes a single value from the cache.</p>

<pre>    $CacheObject-&gt;Delete(
        Type =&gt; 'ObjectName',       # only [a-zA-Z0-9_] chars usable
        Key  =&gt; 'SomeKey',
    );
</pre><p>Please note that despite the cache configuration, Delete and CleanUp will always
be executed both in memory and in the backend to avoid inconsistent cache states.</p>

<h2 id="CleanUp">CleanUp()</h2>

<p><a href="#" onclick="toggleCode('method_CleanUp');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CleanUp">
                                        <pre>

sub CleanUp {
    my ( $Self, %Param ) = @_;

    # cleanup in-memory cache
    # We don't have TTL/expiry information here, so just always delete to be sure.
    if ( $Param{Type} ) {
        delete $Self->{Cache}->{ $Param{Type} };
    }
    elsif ( $Param{KeepTypes} ) {
        my %KeepTypeLookup;
        @KeepTypeLookup{ @{ $Param{KeepTypes} } } = undef;
        TYPE:
        for my $Type ( sort keys %{ $Self->{Cache} || {} } ) {
            next TYPE if exists $KeepTypeLookup{$Type};
            delete $Self->{Cache}->{$Type};
        }
    }
    else {
        delete $Self->{Cache};
    }

    # cleanup persistent cache
    return $Self->{CacheObject}->CleanUp(%Param);
}
</pre></div>

<p>delete parts of the cache or the full cache data.</p>

<p>To delete the whole cache:</p>

<pre>    $CacheObject-&gt;CleanUp();
</pre><p>To delete the data of only one cache type:</p>

<pre>    $CacheObject-&gt;CleanUp(
        Type =&gt; 'ObjectName',   # only [a-zA-Z0-9_] chars usable
    );
</pre><p>To delete all data except of some types:</p>

<pre>    $CacheObject-&gt;CleanUp(
        KeepTypes =&gt; ['Object1', 'Object2'],
    );
</pre><p>To delete only expired cache data:</p>

<pre>    $CacheObject-&gt;CleanUp(
        Expired =&gt; 1,   # optional, defaults to 0
    );
</pre><p>Type/KeepTypes and Expired can be combined to only delete expired data of a single type
or of all types except the types to keep.</p>

<p>Please note that despite the cache configuration, Delete and CleanUp will always
be executed both in memory and in the backend to avoid inconsistent cache states.</p>

<h1 id="TERMS_AND_CONDITIONS">TERMS AND CONDITIONS <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>This software is part of the OTRS project (<a href="http://otrs.org/">http://otrs.org/</a>).</p>

<p>This software comes with ABSOLUTELY NO WARRANTY. For details, see
the enclosed file COPYING for license information (AGPL). If you
did not receive this file, see <a href="http://www.gnu.org/licenses/agpl.txt">http://www.gnu.org/licenses/agpl.txt</a>.</p>


</div><div class="footer">generated by <a href="http://search.cpan.org/perldoc?Pod%3A%3AProjectDocs">Pod::ProjectDocs</a></div></body>
</html>
