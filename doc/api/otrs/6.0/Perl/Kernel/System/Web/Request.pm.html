<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.1//EN" "http://www.w3.org/TR/xhtml11/DTD/xhtml11.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en">
<head>
<!-- otrs.github.io -->
<link href="https://netdna.bootstrapcdn.com/font-awesome/4.0.3/css/font-awesome.css" rel="stylesheet">
<link href="https://fonts.googleapis.com/css?family=Roboto" rel="stylesheet" type="text/css">
<link rel="stylesheet" href="../../../../../../../documentation.css">
<script src="https://code.jquery.com/jquery-1.11.0.min.js"></script>
<script type="text/javascript" src="../../../../../../../documentation.js"></script>
<link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/9.9.0/styles/default.min.css">
<script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/9.9.0/highlight.min.js"></script>
<script>
$(document).ready(function() {

    // Hint languages to prevent false matches (php for example).
    hljs.configure({
        languages: ['perl', 'javascript', 'xml', 'html', 'css', 'json', 'yaml', 'bash']
    });

    // programlistings in manuals
    $('pre.programlisting').each(function(i, block) {
        hljs.highlightBlock(block);
    });
    // code snippets in Perl API docs
    $('.pod pre').addClass('perl').each(function(i, block) {
        hljs.highlightBlock(block);
    });
});</script>
<!-- otrs.github.io -->

	<title>Kernel::System::Web::Request</title>
<meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
  <script type="text/javascript">

  function toggleCode( id ) {
    if ( document.getElementById )
      elem = document.getElementById( id );
    else if ( document.all )
      elem = eval( "document.all." + id );
    else
      return false;

    elemStyle = elem.style;

    if ( elemStyle.display != "block" ) {
      elemStyle.display = "block"
    } else {
      elemStyle.display = "none"
    }

    return true;
  }
  document.writeln( "<style type=\"text/css\">div.method-source-code { display: none }</style>" )
  </script></head>
<body>
<div class="box">
  <h1 class="t1">OTRS 6.0 API Reference Perl</h1>
  <table>
    <tr>
      <td class="label">Description</td>
      <td class="cell">manuals and libraries</td>
    </tr>
  </table>
</div>
<div class="path">
  <a href="../../../index.html">OTRS 6.0 API Reference Perl</a> &gt; Perl Modules &gt;
  Kernel::System::Web::Request
</div>
<div>
<a href="../../../src/Kernel/System/Web/Request.pm">Source</a>
</div>
<div class="pod">
<!-- INDEX START -->
<h3 id="TOP">Index</h3>
<ul>
	<li><a href="#NAME">NAME</a></li>
	<li><a href="#DESCRIPTION">DESCRIPTION</a></li>
	<li><a href="#PUBLIC_INTERFACE">PUBLIC INTERFACE</a><br />
<ul>
	<li><a href="#new">new()</a></li>
	<li><a href="#Error">Error()</a></li>
	<li><a href="#GetParam">GetParam()</a></li>
	<li><a href="#GetParamNames">GetParamNames()</a></li>
	<li><a href="#GetArray">GetArray()</a></li>
	<li><a href="#GetUploadAll">GetUploadAll()</a></li>
	<li><a href="#SetCookie">SetCookie()</a></li>
	<li><a href="#GetCookie">GetCookie()</a></li>
	<li><a href="#IsAJAXRequest">IsAJAXRequest()</a></li>
</ul>
</li>
	<li><a href="#TERMS_AND_CONDITIONS">TERMS AND CONDITIONS</a></li>
</ul>
<hr />
<!-- INDEX END -->

<h1 id="NAME">NAME <a href="#TOP" class="toplink"><img alt="^" src="../../../up.gif" /></a></h1>

<p>Kernel::System::Web::Request - global CGI interface</p>

<h1 id="DESCRIPTION">DESCRIPTION <a href="#TOP" class="toplink"><img alt="^" src="../../../up.gif" /></a></h1>

<p>All cgi param functions.</p>

<h1 id="PUBLIC_INTERFACE">PUBLIC INTERFACE <a href="#TOP" class="toplink"><img alt="^" src="../../../up.gif" /></a></h1>

<h2 id="new">new()</h2>

<p><a href="#" onclick="toggleCode('method_new');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_new">
                                        <pre>

sub new {
    my ( $Type, %Param ) = @_;

    # allocate new hash for object
    my $Self = {};
    bless( $Self, $Type );

    # get config object
    my $ConfigObject = $Kernel::OM->Get('Kernel::Config');

    # max 5 MB posts
    $CGI::POST_MAX = $ConfigObject->Get('WebMaxFileUpload') || 1024 * 1024 * 5;    ## no critic

    # query object (in case use already existing WebRequest, e. g. fast cgi)
    $Self->{Query} = $Param{WebRequest} || CGI->new();

    return $Self;
}
</pre></div>

<p>create param object. Do not use it directly, instead use:</p>

<pre>    use Kernel::System::ObjectManager;
    local $Kernel::OM = Kernel::System::ObjectManager-&gt;new(
        'Kernel::System::Web::Request' =&gt; {
            WebRequest   =&gt; CGI::Fast-&gt;new(), # optional, e. g. if fast cgi is used
        }
    );
    my $ParamObject = $Kernel::OM-&gt;Get('Kernel::System::Web::Request');
</pre><p>If Kernel::System::Web::Request is instantiated several times, they will share the
same CGI data (this can be helpful in filters which do not have access to the
ParamObject, for example.</p>

<p>If you need to reset the CGI data before creating a new instance, use</p>

<pre>    CGI::initialize_globals();
</pre><p>before calling Kernel::System::Web::Request-&gt;new();</p>

<h2 id="Error">Error()</h2>

<p><a href="#" onclick="toggleCode('method_Error');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_Error">
                                        <pre>

sub Error {
    my ( $Self, %Param ) = @_;

    # Workaround, do not check cgi_error() with perlex, CGI module is not
    # working with perlex.
    if ( $ENV{'GATEWAY_INTERFACE'} && $ENV{'GATEWAY_INTERFACE'} =~ /^CGI-PerlEx/ ) {
        return;
    }

    return if !$Self->{Query}->cgi_error();
    ## no critic
    return $Self->{Query}->cgi_error() . ' - POST_MAX=' . ( $CGI::POST_MAX / 1024 ) . 'KB';
    ## use critic
}
</pre></div>

<p>to get the error back</p>

<pre>    if ( $ParamObject-&gt;Error() ) {
        print STDERR $ParamObject-&gt;Error() . &quot;\n&quot;;
    }
</pre><h2 id="GetParam">GetParam()</h2>

<p><a href="#" onclick="toggleCode('method_GetParam');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_GetParam">
                                        <pre>

sub GetParam {
    my ( $Self, %Param ) = @_;

    my $Value = $Self->{Query}->param( $Param{Param} );

    # Fallback to query string for mixed requests.
    my $RequestMethod = $Self->{Query}->request_method() // '';
    if ( $RequestMethod eq 'POST' && !defined $Value ) {
        $Value = $Self->{Query}->url_param( $Param{Param} );
    }

    $Kernel::OM->Get('Kernel::System::Encode')->EncodeInput( \$Value );

    my $Raw = defined $Param{Raw} ? $Param{Raw} : 0;

    if ( !$Raw ) {

        # If it is a plain string, perform trimming
        if ( ref \$Value eq 'SCALAR' ) {
            $Kernel::OM->Get('Kernel::System::CheckItem')->StringClean(
                StringRef => \$Value,
                TrimLeft  => 1,
                TrimRight => 1,
            );
        }
    }

    return $Value;
}
</pre></div>

<p>to get single request parameters. By default, trimming is performed on the data.</p>

<pre>    my $Param = $ParamObject-&gt;GetParam(
        Param =&gt; 'ID',
        Raw   =&gt; 1,       # optional, input data is not changed
    );
</pre><h2 id="GetParamNames">GetParamNames()</h2>

<p><a href="#" onclick="toggleCode('method_GetParamNames');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_GetParamNames">
                                        <pre>

sub GetParamNames {
    my $Self = shift;

    # fetch all names
    my @ParamNames = $Self->{Query}->param();

    # Fallback to query string for mixed requests.
    my $RequestMethod = $Self->{Query}->request_method() // '';
    if ( $RequestMethod eq 'POST' ) {
        my %POSTNames;
        @POSTNames{@ParamNames} = @ParamNames;
        my @GetNames = $Self->{Query}->url_param();
        GETNAME:
        for my $GetName (@GetNames) {
            next GETNAME if !defined $GetName;
            push @ParamNames, $GetName if !exists $POSTNames{$GetName};
        }
    }

    for my $Name (@ParamNames) {
        $Kernel::OM->Get('Kernel::System::Encode')->EncodeInput( \$Name );
    }

    return @ParamNames;
}
</pre></div>

<p>to get names of all parameters passed to the script.</p>

<pre>    my @ParamNames = $ParamObject-&gt;GetParamNames();
</pre><p>Example:</p>

<p>Called URL: index.pl?Action=AdminSysConfig;Subaction=Save;Name=Config::Option::Valid</p>

<pre>    my @ParamNames = $ParamObject-&gt;GetParamNames();
    print join &quot; :: &quot;, @ParamNames;
    #prints Action :: Subaction :: Name
</pre><h2 id="GetArray">GetArray()</h2>

<p><a href="#" onclick="toggleCode('method_GetArray');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_GetArray">
                                        <pre>

sub GetArray {
    my ( $Self, %Param ) = @_;

    my @Values = $Self->{Query}->multi_param( $Param{Param} );

    # Fallback to query string for mixed requests.
    my $RequestMethod = $Self->{Query}->request_method() // '';
    if ( $RequestMethod eq 'POST' && !@Values ) {
        @Values = $Self->{Query}->url_param( $Param{Param} );
    }

    $Kernel::OM->Get('Kernel::System::Encode')->EncodeInput( \@Values );

    my $Raw = defined $Param{Raw} ? $Param{Raw} : 0;

    if ( !$Raw ) {

        # get check item object
        my $CheckItemObject = $Kernel::OM->Get('Kernel::System::CheckItem');

        VALUE:
        for my $Value (@Values) {

            # don't validate CGI::File::Temp objects from file uploads
            next VALUE if !$Value || ref \$Value ne 'SCALAR';

            $CheckItemObject->StringClean(
                StringRef => \$Value,
                TrimLeft  => 1,
                TrimRight => 1,
            );
        }
    }

    return @Values;
}
</pre></div>

<p>to get array request parameters.
By default, trimming is performed on the data.</p>

<pre>    my @Param = $ParamObject-&gt;GetArray(
        Param =&gt; 'ID',
        Raw   =&gt; 1,     # optional, input data is not changed
    );
</pre><h2 id="GetUploadAll">GetUploadAll()</h2>

<p><a href="#" onclick="toggleCode('method_GetUploadAll');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_GetUploadAll">
                                        <pre>

sub GetUploadAll {
    my ( $Self, %Param ) = @_;

    # get upload
    my $Upload = $Self->{Query}->upload( $Param{Param} );
    return if !$Upload;

    # get real file name
    my $UploadFilenameOrig = $Self->GetParam( Param => $Param{Param} ) || 'unknown';

    my $NewFileName = "$UploadFilenameOrig";    # use "" to get filename of anony. object
    $Kernel::OM->Get('Kernel::System::Encode')->EncodeInput( \$NewFileName );

    # replace all devices like c: or d: and dirs for IE!
    $NewFileName =~ s/.:\\(.*)/$1/g;
    $NewFileName =~ s/.*\\(.+?)/$1/g;

    # return a string
    my $Content;
    while (<$Upload>) {
        $Content .= $_;
    }
    close $Upload;

    # Check if content is there, IE is always sending file uploads without content.
    return if !$Content;

    my $ContentType = $Self->_GetUploadInfo(
        Filename => $UploadFilenameOrig,
        Header   => 'Content-Type',
    );

    return (
        Filename    => $NewFileName,
        Content     => $Content,
        ContentType => $ContentType,
    );
}
</pre></div>

<p>gets file upload data.</p>

<pre>    my %File = $ParamObject-&gt;GetUploadAll(
        Param  =&gt; 'FileParam',  # the name of the request parameter containing the file data
    );

    returns (
        Filename    =&gt; 'abc.txt',
        ContentType =&gt; 'text/plain',
        Content     =&gt; 'Some text',
    );
</pre><h2 id="SetCookie">SetCookie()</h2>

<p><a href="#" onclick="toggleCode('method_SetCookie');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_SetCookie">
                                        <pre>

sub SetCookie {
    my ( $Self, %Param ) = @_;

    $Param{Path} ||= '';

    return $Self->{Query}->cookie(
        -name     => $Param{Key},
        -value    => $Param{Value},
        -expires  => $Param{Expires},
        -secure   => $Param{Secure} || '',
        -httponly => $Param{HTTPOnly} || '',
        -path     => '/' . $Param{Path},
    );
}
</pre></div>

<p>set a cookie</p>

<pre>    $ParamObject-&gt;SetCookie(
        Key     =&gt; ID,
        Value   =&gt; 123456,
        Expires =&gt; '+3660s',
        Path    =&gt; 'otrs/',     # optional, only allow cookie for given path
        Secure  =&gt; 1,           # optional, set secure attribute to disable cookie on HTTP (HTTPS only)
        HTTPOnly =&gt; 1,          # optional, sets HttpOnly attribute of cookie to prevent access via JavaScript
    );
</pre><h2 id="GetCookie">GetCookie()</h2>

<p><a href="#" onclick="toggleCode('method_GetCookie');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_GetCookie">
                                        <pre>

sub GetCookie {
    my ( $Self, %Param ) = @_;

    return $Self->{Query}->cookie( $Param{Key} );
}
</pre></div>

<p>get a cookie</p>

<pre>    my $String = $ParamObject-&gt;GetCookie(
        Key =&gt; ID,
    );
</pre><h2 id="IsAJAXRequest">IsAJAXRequest()</h2>

<p><a href="#" onclick="toggleCode('method_IsAJAXRequest');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_IsAJAXRequest">
                                        <pre>

sub IsAJAXRequest {
    my ( $Self, %Param ) = @_;

    return ( $Self->{Query}->http('X-Requested-With') // '' ) eq 'XMLHttpRequest' ? 1 : 0;
}
</pre></div>

<p>checks if the current request was sent by AJAX</p>

<pre>    my $IsAJAXRequest = $ParamObject-&gt;IsAJAXRequest();
</pre><h1 id="TERMS_AND_CONDITIONS">TERMS AND CONDITIONS <a href="#TOP" class="toplink"><img alt="^" src="../../../up.gif" /></a></h1>

<p>This software is part of the OTRS project (<a href="http://otrs.org/">http://otrs.org/</a>).</p>

<p>This software comes with ABSOLUTELY NO WARRANTY. For details, see
the enclosed file COPYING for license information (AGPL). If you
did not receive this file, see <a href="http://www.gnu.org/licenses/agpl.txt">http://www.gnu.org/licenses/agpl.txt</a>.</p>


</div><div class="footer">generated by <a href="http://search.cpan.org/perldoc?Pod%3A%3AProjectDocs">Pod::ProjectDocs</a></div></body>
</html>
