<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.1//EN" "http://www.w3.org/TR/xhtml11/DTD/xhtml11.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en">
<head>
<!-- otrs.github.io -->
<link href="https://netdna.bootstrapcdn.com/font-awesome/4.0.3/css/font-awesome.css" rel="stylesheet">
<link href="https://fonts.googleapis.com/css?family=Roboto" rel="stylesheet" type="text/css">
<link rel="stylesheet" href="../../../../../../documentation.css">
<script src="https://code.jquery.com/jquery-1.11.0.min.js"></script>
<script type="text/javascript" src="../../../../../../documentation.js"></script>
<link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/9.9.0/styles/default.min.css">
<script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/9.9.0/highlight.min.js"></script>
<script>
$(document).ready(function() {

    // Hint languages to prevent false matches (php for example).
    hljs.configure({
        languages: ['perl', 'javascript', 'xml', 'html', 'css', 'json', 'yaml']
    });

    // programlistings in manuals
    $('pre.programlisting').each(function(i, block) {
        hljs.highlightBlock(block);
    });
    // code snippets in Perl API docs
    $('.pod pre').addClass('perl').each(function(i, block) {
        hljs.highlightBlock(block);
    });
});</script>
<!-- otrs.github.io -->

	<title>Kernel::System::CustomerCompany</title>
<meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
  <script type="text/javascript">

  function toggleCode( id ) {
    if ( document.getElementById )
      elem = document.getElementById( id );
    else if ( document.all )
      elem = eval( "document.all." + id );
    else
      return false;

    elemStyle = elem.style;

    if ( elemStyle.display != "block" ) {
      elemStyle.display = "block"
    } else {
      elemStyle.display = "none"
    }

    return true;
  }
  document.writeln( "<style type=\"text/css\">div.method-source-code { display: none }</style>" )
  </script></head>
<body>
<div class="box">
  <h1 class="t1">OTRS 6.0 API Reference Perl</h1>
  <table>
    <tr>
      <td class="label">Description</td>
      <td class="cell">manuals and libraries</td>
    </tr>
  </table>
</div>
<div class="path">
  <a href="../../index.html">OTRS 6.0 API Reference Perl</a> &gt; Perl Modules &gt;
  Kernel::System::CustomerCompany
</div>
<div>
<a href="../../src/Kernel/System/CustomerCompany.pm">Source</a>
</div>
<div class="pod">
<!-- INDEX START -->
<h3 id="TOP">Index</h3>
<ul>
	<li><a href="#NAME">NAME</a></li>
	<li><a href="#DESCRIPTION">DESCRIPTION</a></li>
	<li><a href="#PUBLIC_INTERFACE">PUBLIC INTERFACE</a><br />
<ul>
	<li><a href="#new">new()</a></li>
	<li><a href="#CustomerCompanyAdd">CustomerCompanyAdd()</a></li>
	<li><a href="#CustomerCompanyGet">CustomerCompanyGet()</a></li>
	<li><a href="#CustomerCompanyUpdate">CustomerCompanyUpdate()</a></li>
	<li><a href="#CustomerCompanySourceList">CustomerCompanySourceList()</a></li>
	<li><a href="#CustomerCompanyList">CustomerCompanyList()</a></li>
	<li><a href="#CustomerCompanySearchDetail">CustomerCompanySearchDetail()</a></li>
	<li><a href="#CustomerCompanySearchFields">CustomerCompanySearchFields()</a></li>
	<li><a href="#GetFieldConfig">GetFieldConfig()</a></li>
	<li><a href="#GetFieldSelections">GetFieldSelections()</a></li>
</ul>
</li>
	<li><a href="#TERMS_AND_CONDITIONS">TERMS AND CONDITIONS</a></li>
</ul>
<hr />
<!-- INDEX END -->

<h1 id="NAME">NAME <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>Kernel::System::CustomerCompany - customer company lib</p>

<h1 id="DESCRIPTION">DESCRIPTION <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>All Customer functions. E.g. to add and update customer companies.</p>

<h1 id="PUBLIC_INTERFACE">PUBLIC INTERFACE <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<h2 id="new">new()</h2>

<p><a href="#" onclick="toggleCode('method_new');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_new">
                                        <pre>

sub new {
    my ( $Type, %Param ) = @_;

    # allocate new hash for object
    my $Self = {};
    bless( $Self, $Type );

    # get needed objects
    my $ConfigObject = $Kernel::OM->Get('Kernel::Config');
    my $MainObject   = $Kernel::OM->Get('Kernel::System::Main');

    # load customer company backend modules
    SOURCE:
    for my $Count ( '', 1 .. 10 ) {

        next SOURCE if !$ConfigObject->Get("CustomerCompany$Count");

        my $GenericModule = $ConfigObject->Get("CustomerCompany$Count")->{Module}
            || 'Kernel::System::CustomerCompany::DB';
        if ( !$MainObject->Require($GenericModule) ) {
            $MainObject->Die("Can't load backend module $GenericModule! $@");
        }
        $Self->{"CustomerCompany$Count"} = $GenericModule->new(
            Count              => $Count,
            CustomerCompanyMap => $ConfigObject->Get("CustomerCompany$Count"),
        );
    }

    # init of event handler
    $Self->EventHandlerInit(
        Config => 'CustomerCompany::EventModulePost',
    );

    return $Self;
}
</pre></div>

<p>Don't use the constructor directly, use the ObjectManager instead:</p>

<pre>    my $CustomerCompanyObject = $Kernel::OM-&gt;Get('Kernel::System::CustomerCompany');
</pre><h2 id="CustomerCompanyAdd">CustomerCompanyAdd()</h2>

<p><a href="#" onclick="toggleCode('method_CustomerCompanyAdd');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CustomerCompanyAdd">
                                        <pre>

sub CustomerCompanyAdd {
    my ( $Self, %Param ) = @_;

    # check data source
    if ( !$Param{Source} ) {
        $Param{Source} = 'CustomerCompany';
    }

    # check needed stuff
    for (qw(CustomerID UserID)) {
        if ( !$Param{$_} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Need $_!"
            );
            return;
        }
    }

    # store customer company data
    my $Result = $Self->{ $Param{Source} }->CustomerCompanyAdd(%Param);
    return if !$Result;

    # trigger event
    $Self->EventHandler(
        Event => 'CustomerCompanyAdd',
        Data  => {
            CustomerID => $Param{CustomerID},
            NewData    => \%Param,
        },
        UserID => $Param{UserID},
    );

    return $Result;
}
</pre></div>

<p>add a new customer company</p>

<pre>    my $ID = $CustomerCompanyObject-&gt;CustomerCompanyAdd(
        CustomerID              =&gt; 'example.com',
        CustomerCompanyName     =&gt; 'New Customer Inc.',
        CustomerCompanyStreet   =&gt; '5201 Blue Lagoon Drive',
        CustomerCompanyZIP      =&gt; '33126',
        CustomerCompanyCity     =&gt; 'Miami',
        CustomerCompanyCountry  =&gt; 'USA',
        CustomerCompanyURL      =&gt; 'http://www.example.org',
        CustomerCompanyComment  =&gt; 'some comment',
        ValidID                 =&gt; 1,
        UserID                  =&gt; 123,
    );
</pre><p>NOTE: Actual fields accepted by this API call may differ based on
CustomerCompany mapping in your system configuration.</p>

<h2 id="CustomerCompanyGet">CustomerCompanyGet()</h2>

<p><a href="#" onclick="toggleCode('method_CustomerCompanyGet');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CustomerCompanyGet">
                                        <pre>

sub CustomerCompanyGet {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    if ( !$Param{CustomerID} ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "Need CustomerID!"
        );
        return;
    }

    # Fetch dynamic field configurations for CustomerCompany.
    my $DynamicFieldConfigs = $Kernel::OM->Get('Kernel::System::DynamicField')->DynamicFieldListGet(
        ObjectType => 'CustomerCompany',
        Valid      => 1,
    );

    my %DynamicFieldLookup = map { $_->{Name} => $_ } @{$DynamicFieldConfigs};

    # get needed objects
    my $ConfigObject              = $Kernel::OM->Get('Kernel::Config');
    my $DynamicFieldBackendObject = $Kernel::OM->Get('Kernel::System::DynamicField::Backend');

    SOURCE:
    for my $Count ( '', 1 .. 10 ) {

        next SOURCE if !$Self->{"CustomerCompany$Count"};

        my %Company = $Self->{"CustomerCompany$Count"}->CustomerCompanyGet( %Param, );
        next SOURCE if !%Company;

        # fetch dynamic field values
        if ( IsArrayRefWithData( $Self->{"CustomerCompany$Count"}->{CustomerCompanyMap}->{Map} ) ) {
            CUSTOMERCOMPANYFIELD:
            for my $CustomerCompanyField ( @{ $Self->{"CustomerCompany$Count"}->{CustomerCompanyMap}->{Map} } ) {
                next CUSTOMERCOMPANYFIELD if $CustomerCompanyField->[5] ne 'dynamic_field';
                next CUSTOMERCOMPANYFIELD if !$DynamicFieldLookup{ $CustomerCompanyField->[2] };

                my $Value = $DynamicFieldBackendObject->ValueGet(
                    DynamicFieldConfig => $DynamicFieldLookup{ $CustomerCompanyField->[2] },
                    ObjectName         => $Company{CustomerID},
                );

                $Company{ $CustomerCompanyField->[0] } = $Value;
            }
        }

        # return company data
        return (
            %Company,
            Source => "CustomerCompany$Count",
            Config => $ConfigObject->Get("CustomerCompany$Count"),
        );
    }

    return;
}
</pre></div>

<p>get customer company attributes</p>

<pre>    my %CustomerCompany = $CustomerCompanyObject-&gt;CustomerCompanyGet(
        CustomerID =&gt; 123,
    );
</pre><p>Returns:</p>

<pre>    %CustomerCompany = (
        'CustomerCompanyName'    =&gt; 'Customer Inc.',
        'CustomerID'             =&gt; 'example.com',
        'CustomerCompanyStreet'  =&gt; '5201 Blue Lagoon Drive',
        'CustomerCompanyZIP'     =&gt; '33126',
        'CustomerCompanyCity'    =&gt; 'Miami',
        'CustomerCompanyCountry' =&gt; 'United States',
        'CustomerCompanyURL'     =&gt; 'http://example.com',
        'CustomerCompanyComment' =&gt; 'Some Comments',
        'ValidID'                =&gt; '1',
        'CreateTime'             =&gt; '2010-10-04 16:35:49',
        'ChangeTime'             =&gt; '2010-10-04 16:36:12',
    );
</pre><p>NOTE: Actual fields returned by this API call may differ based on
CustomerCompany mapping in your system configuration.</p>

<h2 id="CustomerCompanyUpdate">CustomerCompanyUpdate()</h2>

<p><a href="#" onclick="toggleCode('method_CustomerCompanyUpdate');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CustomerCompanyUpdate">
                                        <pre>

sub CustomerCompanyUpdate {
    my ( $Self, %Param ) = @_;

    $Param{CustomerCompanyID} ||= $Param{CustomerID};

    # check needed stuff
    if ( !$Param{CustomerCompanyID} ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "Need CustomerCompanyID or CustomerID!"
        );
        return;
    }

    # check if company exists
    my %Company = $Self->CustomerCompanyGet( CustomerID => $Param{CustomerCompanyID} );
    if ( !%Company ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "No such company '$Param{CustomerCompanyID}'!",
        );
        return;
    }

    my $Result = $Self->{ $Company{Source} }->CustomerCompanyUpdate(%Param);
    return if !$Result;

    # trigger event
    $Self->EventHandler(
        Event => 'CustomerCompanyUpdate',
        Data  => {
            CustomerID    => $Param{CustomerID},
            OldCustomerID => $Param{CustomerCompanyID},
            NewData       => \%Param,
            OldData       => \%Company,
        },
        UserID => $Param{UserID},
    );
    return $Result;
}
</pre></div>

<p>update customer company attributes</p>

<pre>    $CustomerCompanyObject-&gt;CustomerCompanyUpdate(
        CustomerCompanyID       =&gt; 'oldexample.com', # required for CustomerCompanyID-update
        CustomerID              =&gt; 'example.com',
        CustomerCompanyName     =&gt; 'New Customer Inc.',
        CustomerCompanyStreet   =&gt; '5201 Blue Lagoon Drive',
        CustomerCompanyZIP      =&gt; '33126',
        CustomerCompanyLocation =&gt; 'Miami',
        CustomerCompanyCountry  =&gt; 'USA',
        CustomerCompanyURL      =&gt; 'http://example.com',
        CustomerCompanyComment  =&gt; 'some comment',
        ValidID                 =&gt; 1,
        UserID                  =&gt; 123,
    );
</pre><h2 id="CustomerCompanySourceList">CustomerCompanySourceList()</h2>

<p><a href="#" onclick="toggleCode('method_CustomerCompanySourceList');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CustomerCompanySourceList">
                                        <pre>

sub CustomerCompanySourceList {
    my ( $Self, %Param ) = @_;

    # get config object
    my $ConfigObject = $Kernel::OM->Get('Kernel::Config');

    my %Data;
    SOURCE:
    for my $Count ( '', 1 .. 10 ) {

        next SOURCE if !$ConfigObject->Get("CustomerCompany$Count");

        if ( defined $Param{ReadOnly} ) {
            my $BackendConfig = $ConfigObject->Get("CustomerCompany$Count");
            if ( $Param{ReadOnly} ) {
                next SOURCE if !$BackendConfig->{ReadOnly};
            }
            else {
                next SOURCE if $BackendConfig->{ReadOnly};
            }
        }

        $Data{"CustomerCompany$Count"} = $ConfigObject->Get("CustomerCompany$Count")->{Name}
            || "No Name $Count";
    }

    return %Data;
}
</pre></div>

<p>return customer company source list</p>

<pre>    my %List = $CustomerCompanyObject-&gt;CustomerCompanySourceList(
        ReadOnly =&gt; 0 # optional, 1 returns only RO backends, 0 returns writable, if not passed returns all backends
    );
</pre><h2 id="CustomerCompanyList">CustomerCompanyList()</h2>

<p><a href="#" onclick="toggleCode('method_CustomerCompanyList');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CustomerCompanyList">
                                        <pre>

sub CustomerCompanyList {
    my ( $Self, %Param ) = @_;

    # Get dynamic field object.
    my $DynamicFieldObject = $Kernel::OM->Get('Kernel::System::DynamicField');

    my $DynamicFieldConfigs = $DynamicFieldObject->DynamicFieldListGet(
        ObjectType => 'CustomerCompany',
        Valid      => 1,
    );

    my %DynamicFieldLookup = map { $_->{Name} => $_ } @{$DynamicFieldConfigs};

    # Get dynamic field backend object.
    my $DynamicFieldBackendObject = $Kernel::OM->Get('Kernel::System::DynamicField::Backend');

    my %Data;
    SOURCE:
    for my $Count ( '', 1 .. 10 ) {

        next SOURCE if !$Self->{"CustomerCompany$Count"};

        # search dynamic field values, if configured
        my $Map = $Self->{"CustomerCompany$Count"}->{CustomerCompanyMap}->{Map};
        if ( IsArrayRefWithData($Map) ) {

            # fetch dynamic field names that are configured in Map
            # only these will be considered for any other search config
            # [ 'DynamicField_Name_Y', undef, 'Name_Y', 0, 0, 'dynamic_field', undef, 0,],
            my %DynamicFieldNames = map { $_->[2] => 1 } grep { $_->[5] eq 'dynamic_field' } @{$Map};

            if (%DynamicFieldNames) {
                my $FoundDynamicFieldObjectIDs;
                my $SearchFields;
                my $SearchParam;

                # check which of the dynamic fields configured in Map are also
                # configured in SearchFields

                # param Search
                if ( defined $Param{Search} && length $Param{Search} ) {
                    $SearchFields
                        = $Self->{"CustomerCompany$Count"}->{CustomerCompanyMap}->{CustomerCompanySearchFields};
                    $SearchParam = $Param{Search};
                }

                # search dynamic field values
                if ( IsArrayRefWithData($SearchFields) ) {
                    my @SearchDynamicFieldNames = grep { exists $DynamicFieldNames{$_} } @{$SearchFields};

                    my %FoundDynamicFieldObjectIDs;
                    FIELDNAME:
                    for my $FieldName (@SearchDynamicFieldNames) {

                        my $DynamicFieldConfig = $DynamicFieldLookup{$FieldName};

                        next FIELDNAME if !IsHashRefWithData($DynamicFieldConfig);

                        my $DynamicFieldValues = $DynamicFieldBackendObject->ValueSearch(
                            DynamicFieldConfig => $DynamicFieldConfig,
                            Search             => $SearchParam,
                        );

                        if ( IsArrayRefWithData($DynamicFieldValues) ) {
                            for my $DynamicFieldValue ( @{$DynamicFieldValues} ) {
                                $FoundDynamicFieldObjectIDs{ $DynamicFieldValue->{ObjectID} } = 1;
                            }
                        }
                    }

                    $FoundDynamicFieldObjectIDs = [ keys %FoundDynamicFieldObjectIDs ];
                }

                # execute backend search for found object IDs
                # this data is being merged with the following CustomerCompanyList call
                if ( IsArrayRefWithData($FoundDynamicFieldObjectIDs) ) {

                    my $ObjectNames = $DynamicFieldObject->ObjectMappingGet(
                        ObjectID   => $FoundDynamicFieldObjectIDs,
                        ObjectType => 'CustomerCompany',
                    );

                    my %SearchParam = %Param;
                    delete $SearchParam{Search};
                    my %CompanyList = $Self->{"CustomerCompany$Count"}->CustomerCompanyList(%SearchParam);

                    OBJECTNAME:
                    for my $ObjectName ( values %{$ObjectNames} ) {
                        next OBJECTNAME if exists $Data{$ObjectName};

                        if ( IsHashRefWithData( \%CompanyList ) && exists $CompanyList{$ObjectName} ) {
                            %Data = (
                                $ObjectName => $CompanyList{$ObjectName},
                                %Data
                            );
                        }
                    }
                }
            }
        }

        # get company list result of backend and merge it
        my %SubData = $Self->{"CustomerCompany$Count"}->CustomerCompanyList(%Param);
        %Data = ( %Data, %SubData );
    }
    return %Data;
}
</pre></div>

<p>get list of customer companies.</p>

<pre>    my %List = $CustomerCompanyObject-&gt;CustomerCompanyList();

    my %List = $CustomerCompanyObject-&gt;CustomerCompanyList(
        Valid =&gt; 0,
        Limit =&gt; 0,     # optional, override configured search result limit (0 means unlimited)
    );

    my %List = $CustomerCompanyObject-&gt;CustomerCompanyList(
        Search =&gt; 'somecompany',
    );
</pre><p>Returns:</p>

<pre>    %List = {
        'example.com' =&gt; 'example.com Customer Inc.',
        'acme.com'    =&gt; 'acme.com Acme, Inc.'
    };
</pre><h2 id="CustomerCompanySearchDetail">CustomerCompanySearchDetail()</h2>

<p><a href="#" onclick="toggleCode('method_CustomerCompanySearchDetail');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CustomerCompanySearchDetail">
                                        <pre>

sub CustomerCompanySearchDetail {
    my ( $Self, %Param ) = @_;

     # Get all general search fields (without a restriction to a source).
    my @AllSearchFields = $Self->CustomerCompanySearchFields();

    # Generate a hash with the customer company sources which must be searched.
    my %SearchCustomerCompanySources;

    SOURCE:
    for my $Count ( '', 1 .. 10 ) {
        next SOURCE if !$Self->{"CustomerCompany$Count"};

        # Get the search fields for the current source.
        my @SourceSearchFields = $Self->CustomerCompanySearchFields(
            Source => "CustomerCompany$Count",
        );
        my %LookupSourceSearchFields = map { $_->{Name} => 1 } @SourceSearchFields;

        # Check if all search param exists in the search fields from the current source.
        SEARCHFIELD:
        for my $SearchField (@AllSearchFields) {

            next SEARCHFIELD if !$Param{ $SearchField->{Name} };

            next SOURCE if !$LookupSourceSearchFields{ $SearchField->{Name} };
        }
        $SearchCustomerCompanySources{"CustomerCompany$Count"} = \@SourceSearchFields;
    }

    # Set the default behaviour for the return type.
    $Param{Result} ||= 'ARRAY';

    if ( $Param{Result} eq 'COUNT' ) {

        my $IDsCount = 0;

        SOURCE:
        for my $Source ( sort keys %SearchCustomerCompanySources ) {
            next SOURCE if !$Self->{$Source};

            my $SubIDsCount = $Self->{$Source}->CustomerCompanySearchDetail(
                %Param,
                SearchFields => $SearchCustomerCompanySources{$Source},
            );

            return if !defined $SubIDsCount;

            $IDsCount += $SubIDsCount || 0;
        }
        return $IDsCount;
    }
    else {

        my @IDs;

        my $ResultCount = 0;

        SOURCE:
        for my $Source ( sort keys %SearchCustomerCompanySources ) {
            next SOURCE if !$Self->{$Source};

            my $SubIDs = $Self->{$Source}->CustomerCompanySearchDetail(
                %Param,
                SearchFields => $SearchCustomerCompanySources{$Source},
            );

            return if !defined $SubIDs;

            next SOURCE if !IsArrayRefWithData($SubIDs);

            push @IDs, @{$SubIDs};

            $ResultCount++;
        }

        # If we have more then one search results from diffrent sources, we need a resorting
        #   and splice (for the limit) because of the merged single results.
        if ( $ResultCount > 1 ) {

            my @CustomerCompanyataList;

            for my $ID (@IDs) {

                my %CustomerCompanyData = $Self->CustomerCompanyGet(
                    CustomerID => $ID,
                );
                push @CustomerCompanyataList, \%CustomerCompanyData;
            }

            my $OrderBy = 'CustomerID';
            if ( IsArrayRefWithData($Param{OrderBy} ) ) {
                $OrderBy = $Param{OrderBy}->[0];
            }

            if ( IsArrayRefWithData($Param{OrderByDirection}) && $Param{OrderByDirection}->[0] eq 'Up' ) {
                @CustomerCompanyataList = sort { lc( $a->{$OrderBy} ) cmp lc( $b->{$OrderBy} ) } @CustomerCompanyataList;
            }
            else {
                @CustomerCompanyataList = sort { lc( $b->{$OrderBy} ) cmp lc( $a->{$OrderBy} ) } @CustomerCompanyataList;
            }

            if ( $Param{Limit} && scalar @CustomerCompanyataList > $Param{Limit} ) {
                splice @CustomerCompanyataList, $Param{Limit};
            }

            @IDs = map { $_->{CustomerID} } @CustomerCompanyataList;
        }

        return \@IDs;
    }
}
</pre></div>

<p>To find customer companies in the system.</p>

<p>The search criteria are logically AND connected.
When a list is passed as criteria, the individual members are OR connected.
When an undef or a reference to an empty array is passed, then the search criteria
is ignored.</p>

<p>Returns either a list, as an arrayref, or a count of found customer company ids.
The count of results is returned when the parameter <code>Result = 'COUNT'</code> is passed.</p>

<pre>    my $CustomerCompanyIDsRef = $CustomerCompanyObject-&gt;CustomerCompanySearchDetail(

        # all search fields possible which are defined in CustomerCompany::EnhancedSearchFields
        CustomerID          =&gt; 'example*',                                  # (optional)
        CustomerCompanyName =&gt; 'Name*',                                     # (optional)

        # array parameters are used with logical OR operator (all values are possible which
        are defined in the config selection hash for the field)
        CustomerCompanyCountry =&gt; [ 'Austria', 'Germany', ],                # (optional)

        # DynamicFields
        #   At least one operator must be specified. Operators will be connected with AND,
        #       values in an operator with OR.
        #   You can also pass more than one argument to an operator: ['value1', 'value2']
        DynamicField_FieldNameX =&gt; {
            Equals            =&gt; 123,
            Like              =&gt; 'value*',                # &quot;equals&quot; operator with wildcard support
            GreaterThan       =&gt; '2001-01-01 01:01:01',
            GreaterThanEquals =&gt; '2001-01-01 01:01:01',
            SmallerThan       =&gt; '2002-02-02 02:02:02',
            SmallerThanEquals =&gt; '2002-02-02 02:02:02',
        }

        OrderBy =&gt; [ 'CustomerID', 'CustomerCompanyCountry' ],              # (optional)
        # ignored if the result type is 'COUNT'
        # default: [ 'CustomerID' ]
        # (all search fields possible which are defined in
        CustomerCompany::EnhancedSearchFields)

        # Additional information for OrderBy:
        # The OrderByDirection can be specified for each OrderBy attribute.
        # The pairing is made by the array indices.

        OrderByDirection =&gt; [ 'Down', 'Up' ],                               # (optional)
        # ignored if the result type is 'COUNT'
        # (Down | Up) Default: [ 'Down' ]

        Result =&gt; 'ARRAY' || 'COUNT',                                       # (optional)
        # default: ARRAY, returns an array of change ids
        # COUNT returns a scalar with the number of found changes

        Limit =&gt; 100,                                                       # (optional)
        # ignored if the result type is 'COUNT'
    );
</pre><p>Returns:</p>

<p>Result: 'ARRAY'</p>

<pre>    @CustomerIDs = ( 1, 2, 3 );
</pre><p>Result: 'COUNT'</p>

<pre>    $CustomerIDs = 10;
</pre><h2 id="CustomerCompanySearchFields">CustomerCompanySearchFields()</h2>

<p><a href="#" onclick="toggleCode('method_CustomerCompanySearchFields');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CustomerCompanySearchFields">
                                        <pre>

sub CustomerCompanySearchFields {
    my ( $Self, %Param ) = @_;

    # Get the search fields from all customer company maps (merge from all maps together).
    my @SearchFields;

    my %SearchFieldsExists;

    SOURCE:
    for my $Count ( '', 1 .. 10 ) {
        next SOURCE if !$Self->{"CustomerCompany$Count"};
        next SOURCE if $Param{Source} && $Param{Source} ne "CustomerCompany$Count";

        ENTRY:
        for my $Entry ( @{ $Self->{"CustomerCompany$Count"}->{CustomerCompanyMap}->{Map} } ) {

            my $SearchFieldName = $Entry->[0];

            next ENTRY if $SearchFieldsExists{$SearchFieldName};

            # Remeber the already collected search field name.
            $SearchFieldsExists{$SearchFieldName} = 1;

            my %FieldConfig = $Self->GetFieldConfig(
                FieldName => $SearchFieldName,
                Source    => $Param{Source},     # to get the right database field for the given source
            );

            next SEARCHFIELDNAME if !%FieldConfig;

            my %SearchFieldData = (
                %FieldConfig,
                Name => $SearchFieldName,
            );

            my %SelectionsData = $Self->GetFieldSelections(
                FieldName => $SearchFieldName,
            );

            if ( $SearchFieldData{StorageType} eq 'dynamic_field' ) {
                $SearchFieldData{Type} = 'DynamicField';
            }
            elsif (%SelectionsData) {
                $SearchFieldData{Type}           = 'Selection';
                $SearchFieldData{SelectionsData} = \%SelectionsData;
            }
            else {
                $SearchFieldData{Type} = 'Input';
            }

            push @SearchFields, \%SearchFieldData;
        }
    }

    return @SearchFields;
}
</pre></div>

<p>Get a list of defined search fields (optional only the relevant fields for the given source).</p>

<pre>    my @SeachFields = $CustomerCompanyObject-&gt;CustomerCompanySearchFields(
        Source =&gt; 'CustomerCompany', # optional, but important in the CustomerCompanySearchDetail to get the right database fields
    );
</pre><p>Returns an array of hash references.</p>

<pre>    @SeachFields = (
        {
            Name  =&gt; 'CustomerID',
            Label =&gt; 'CustomerID',
            Type  =&gt; 'Input',
        },
        {
            Name           =&gt; 'CustomerCompanyCountry',
            Label          =&gt; 'Country',
            Type           =&gt; 'Selection',
            SelectionsData =&gt; {
                'Germany'        =&gt; 'Germany',
                'United Kingdom' =&gt; 'United Kingdom',
                'United States'  =&gt; 'United States',
                ...
            },
        },
        {
            Name          =&gt; 'DynamicField_Branch',
            Label         =&gt; '',
            Type          =&gt; 'DynamicField',
            DatabaseField =&gt; 'Branch',
        },
    );
</pre><h2 id="GetFieldConfig">GetFieldConfig()</h2>

<p><a href="#" onclick="toggleCode('method_GetFieldConfig');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_GetFieldConfig">
                                        <pre>

sub GetFieldConfig {
    my ( $Self, %Param ) = @_;

    if ( !$Param{FieldName} ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "Need FieldName!"
        );
        return;
    }

    SOURCE:
    for my $Count ( '', 1 .. 10 ) {
        next SOURCE if !$Self->{"CustomerCompany$Count"};
        next SOURCE if $Param{Source} && $Param{Source} ne "CustomerCompany$Count";

        # Search the right field and return the label.
        ENTRY:
        for my $Entry ( @{ $Self->{"CustomerCompany$Count"}->{CustomerCompanyMap}->{Map} } ) {
            next ENTRY if $Param{FieldName} ne $Entry->[0];

            my %FieldConfig = (
                Label         => $Entry->[1],
                DatabaseField => $Entry->[2],
                StorageType   => $Entry->[5],
            );

            return %FieldConfig;
        }
    }

    return;
}
</pre></div>

<p>This function collect some field config information from the customer user map.</p>

<pre>    my %FieldConfig = $CustomerCompanyObject-&gt;GetFieldConfig(
        FieldName =&gt; 'CustomerCompanyName',
        Source    =&gt; 'CustomerCompany', # optional
    );
</pre><p>Returns some field config information:</p>

<pre>    my %FieldConfig = (
        Label         =&gt; 'Name',
        DatabaseField =&gt; 'name',
        StorageType   =&gt; 'var',
    );
</pre><h2 id="GetFieldSelections">GetFieldSelections()</h2>

<p><a href="#" onclick="toggleCode('method_GetFieldSelections');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_GetFieldSelections">
                                        <pre>

sub GetFieldSelections {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    if ( !$Param{FieldName} ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "Need FieldName!"
        );
        return;
    }

    my %SelectionsData;

    SOURCE:
    for my $Count ( '', 1 .. 10 ) {
        next SOURCE if !$Self->{"CustomerCompany$Count"};
        next SOURCE if !$Self->{"CustomerCompany$Count"}->{CustomerCompanyMap}->{Selections}->{ $Param{FieldName} };

        %SelectionsData = (
            %SelectionsData,
            %{ $Self->{"CustomerCompany$Count"}->{CustomerCompanyMap}->{Selections}->{ $Param{FieldName} } }
        );
    }

    # Make sure the encoding stamp is set.
    for my $Key ( sort keys %SelectionsData ) {
        $SelectionsData{$Key} = $Kernel::OM->Get('Kernel::System::Encode')->EncodeInput( $SelectionsData{$Key} );
    }

    # Default handling for field 'CustomerCompanyCountry'.
    if ( !%SelectionsData && $Param{FieldName} =~ /^CustomerCompanyCountry/i ) {
        %SelectionsData = %{ $Kernel::OM->Get('Kernel::System::ReferenceData')->CountryList() };
    }

    # Default handling for field 'ValidID'.
    elsif ( !%SelectionsData && $Param{FieldName} =~ /^ValidID/i ) {
        %SelectionsData = $Kernel::OM->Get('Kernel::System::Valid')->ValidList();
    }

    return %SelectionsData;
}
</pre></div>

<p>This function collect the selections for the given field name, if the field has some selections.</p>

<pre>    my %SelectionsData = $CustomerCompanyObject-&gt;GetFieldSelections(
        FieldName =&gt; 'CustomerCompanyCountry',
    );
</pre><p>Returns the selections for the given field name (merged from all sources) or a empty hash:</p>

<pre>    my %SelectionData = (
        'Germany'        =&gt; 'Germany',
        'United Kingdom' =&gt; 'United Kingdom',
        'United States'  =&gt; 'United States',
    );
</pre><h1 id="TERMS_AND_CONDITIONS">TERMS AND CONDITIONS <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>This software is part of the OTRS project (<a href="http://otrs.org/">http://otrs.org/</a>).</p>

<p>This software comes with ABSOLUTELY NO WARRANTY. For details, see
the enclosed file COPYING for license information (AGPL). If you
did not receive this file, see <a href="http://www.gnu.org/licenses/agpl.txt">http://www.gnu.org/licenses/agpl.txt</a>.</p>


</div><div class="footer">generated by <a href="http://search.cpan.org/perldoc?Pod%3A%3AProjectDocs">Pod::ProjectDocs</a></div></body>
</html>
