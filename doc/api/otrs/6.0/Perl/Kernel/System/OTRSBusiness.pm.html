<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.1//EN" "http://www.w3.org/TR/xhtml11/DTD/xhtml11.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en">
<head>
<!-- otrs.github.io -->
<link href="https://netdna.bootstrapcdn.com/font-awesome/4.0.3/css/font-awesome.css" rel="stylesheet">
<link href="https://fonts.googleapis.com/css?family=Roboto" rel="stylesheet" type="text/css">
<link rel="stylesheet" href="../../../../../../documentation.css">
<script src="https://code.jquery.com/jquery-1.11.0.min.js"></script>
<script type="text/javascript" src="../../../../../../documentation.js"></script>
<link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/9.7.0/styles/default.min.css">
<script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/9.7.0/highlight.min.js"></script>
<script>
$(document).ready(function() {
    // programlistings in manuals
    $('pre.programlisting').each(function(i, block) {
        hljs.highlightBlock(block);
    });
    // code snippets in Perl API docs
    $('.pod pre').addClass('perl').each(function(i, block) {
        hljs.highlightBlock(block);
    });
});</script>
<!-- otrs.github.io -->

	<title>Kernel::System::OTRSBusiness</title>
<meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
  <script type="text/javascript">

  function toggleCode( id ) {
    if ( document.getElementById )
      elem = document.getElementById( id );
    else if ( document.all )
      elem = eval( "document.all." + id );
    else
      return false;

    elemStyle = elem.style;

    if ( elemStyle.display != "block" ) {
      elemStyle.display = "block"
    } else {
      elemStyle.display = "none"
    }

    return true;
  }
  document.writeln( "<style type=\"text/css\">div.method-source-code { display: none }</style>" )
  </script></head>
<body>
<div class="box">
  <h1 class="t1">OTRS 6.0 API Reference Perl</h1>
  <table>
    <tr>
      <td class="label">Description</td>
      <td class="cell">manuals and libraries</td>
    </tr>
  </table>
</div>
<div class="path">
  <a href="../../index.html">OTRS 6.0 API Reference Perl</a> &gt; Perl Modules &gt;
  Kernel::System::OTRSBusiness
</div>
<div>
<a href="../../src/Kernel/System/OTRSBusiness.pm">Source</a>
</div>


<div class="pod">
<!-- INDEX START -->
<h3 id="TOP">Index</h3>
<ul>
	<li><a href="#NAME">NAME</a></li>
	<li><a href="#PUBLIC_INTERFACE">PUBLIC INTERFACE</a><br />
<ul>
	<li><a href="#new">new()</a></li>
	<li><a href="#OTRSBusinessIsInstalled">OTRSBusinessIsInstalled()</a></li>
	<li><a href="#OTRSBusinessIsAvailable">OTRSBusinessIsAvailable()</a></li>
	<li><a href="#OTRSBusinessIsAvailableOffline">OTRSBusinessIsAvailableOffline()</a></li>
	<li><a href="#OTRSBusinessIsCorrectlyDeployed">OTRSBusinessIsCorrectlyDeployed()</a></li>
	<li><a href="#OTRSBusinessIsReinstallable">OTRSBusinessIsReinstallable()</a></li>
	<li><a href="#OTRSBusinessIsUpdateable">OTRSBusinessIsUpdateable()</a></li>
	<li><a href="#OTRSBusinessVersionCheckOffline">OTRSBusinessVersionCheckOffline()</a></li>
	<li><a href="#OTRSBusinessGetDependencies">OTRSBusinessGetDependencies()</a></li>
	<li><a href="#OTRSBusinessEntitlementCheck">OTRSBusinessEntitlementCheck()</a></li>
	<li><a href="#OTRSBusinessEntitlementStatus">OTRSBusinessEntitlementStatus()</a></li>
	<li><a href="#OTRSBusinessContractExpiryDateCheck">OTRSBusinessContractExpiryDateCheck()</a></li>
	<li><a href="#OTRSBusinessInstall">OTRSBusinessInstall()</a></li>
	<li><a href="#OTRSBusinessReinstall">OTRSBusinessReinstall()</a></li>
	<li><a href="#OTRSBusinessUpdate">OTRSBusinessUpdate()</a></li>
	<li><a href="#OTRSBusinessUninstall">OTRSBusinessUninstall()</a></li>
	<li><a href="#OTRSBusinessCommandNextUpdateTimeSet">OTRSBusinessCommandNextUpdateTimeSet()</a></li>
</ul>
</li>
	<li><a href="#TERMS_AND_CONDITIONS">TERMS AND CONDITIONS</a></li>
</ul>
<hr />
<!-- INDEX END -->

<h1 id="NAME">NAME <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>Kernel::System::OTRSBusiness - OTRSBusiness deployment backend</p>

<h1 id="PUBLIC_INTERFACE">PUBLIC INTERFACE <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<h2 id="new">new()</h2>

<p><a href="#" onclick="toggleCode('method_new');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_new">
                                        <pre>

sub new {
    my ( $Type, %Param ) = @_;

    # allocate new hash for object
    my $Self = {};
    bless( $Self, $Type );

    #$Self->{APIVersion} = 1;

    # Get config object
    my $ConfigObject = $Kernel::OM->Get('Kernel::Config');

    # Get OTRSBusiness::ReleaseChannel from SysConfig (Stable = 1, Development = 0)
    $Self->{OnlyStable} = $ConfigObject->Get('OTRSBusiness::ReleaseChannel') // 1;

    # Set cache params
    $Self->{CacheType} = 'OTRSBusiness';
    $Self->{CacheTTL}  = 60 * 60 * 24 * 30;    # 30 days

    # Check if cloud services are disabled
    $Self->{CloudServicesDisabled} = $ConfigObject->Get('CloudServices::Disabled') || 0;

    return $Self;
}
</pre></div>

<p>create an object. Do not use it directly, instead use:</p>

<pre>    use Kernel::System::ObjectManager;
    local $Kernel::OM = Kernel::System::ObjectManager-&gt;new();
    my $RegistrationObject = $Kernel::OM-&gt;Get('Kernel::System::OTRSBusiness');




</pre><h2 id="OTRSBusinessIsInstalled">OTRSBusinessIsInstalled()</h2>

<p><a href="#" onclick="toggleCode('method_OTRSBusinessIsInstalled');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_OTRSBusinessIsInstalled">
                                        <pre>

sub OTRSBusinessIsInstalled {
    my ( $Self, %Param ) = @_;

    my $CacheObject = $Kernel::OM->Get('Kernel::System::Cache');

    # as the check for installed packages can be
    # very expensive, we want to use caching here
    my $Cache = $CacheObject->Get(
        Type => $Self->{CacheType},
        TTL  => $Self->{CacheTTL},
        Key  => 'OTRSBusinessIsInstalled',
    );

    return $Cache if defined $Cache;

    my $IsInstalled = $Self->_GetOTRSBusinessPackageFromRepository() ? 1 : 0;

    # set cache
    $CacheObject->Set(
        Type  => $Self->{CacheType},
        TTL   => $Self->{CacheTTL},
        Key   => 'OTRSBusinessIsInstalled',
        Value => $IsInstalled,
    );

    return $IsInstalled;
}
</pre></div>

<p>checks if OTRSBusiness is installed in the current system.
That does not necessarily mean that it is also active, for
example if the package is only on the database but not on
the file system.</p>

<h2 id="OTRSBusinessIsAvailable">OTRSBusinessIsAvailable()</h2>

<p><a href="#" onclick="toggleCode('method_OTRSBusinessIsAvailable');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_OTRSBusinessIsAvailable">
                                        <pre>

sub OTRSBusinessIsAvailable {
    my ( $Self, %Param ) = @_;

    return if $Self->{CloudServicesDisabled};

    my $CloudServiceObject = $Kernel::OM->Get('Kernel::System::CloudService::Backend::Run');
    my $RequestResult      = $CloudServiceObject->Request(
        RequestData => {
            OTRSBusiness => [
                {
                    Operation => 'BusinessVersionCheck',
                    Data      => {
                        OnlyStable => $Self->{OnlyStable},
                    },
                },
            ],
        },
    );

    my $OperationResult;
    if ( IsHashRefWithData($RequestResult) ) {
        $OperationResult = $CloudServiceObject->OperationResultGet(
            RequestResult => $RequestResult,
            CloudService  => 'OTRSBusiness',
            Operation     => 'BusinessVersionCheck',
        );

        if ( $OperationResult->{Success} ) {
            $Self->HandleBusinessVersionCheckCloudServiceResult(
                OperationResult => $OperationResult,
            );

            if ( $OperationResult->{Data}->{LatestVersionForCurrentFramework} ) {
                return 1;
            }
        }
    }
    return;
}
</pre></div>

<p>checks with cloud.otrs.com if OTRSBusiness is available for the current framework.</p>

<h2 id="OTRSBusinessIsAvailableOffline">OTRSBusinessIsAvailableOffline()</h2>

<p><a href="#" onclick="toggleCode('method_OTRSBusinessIsAvailableOffline');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_OTRSBusinessIsAvailableOffline">
                                        <pre>

sub OTRSBusinessIsAvailableOffline {
    my ( $Self, %Param ) = @_;

    my %BusinessVersionCheck = $Kernel::OM->Get('Kernel::System::SystemData')->SystemDataGroupGet(
        Group => 'OTRSBusiness',
    );

    return $BusinessVersionCheck{LatestVersionForCurrentFramework} ? 1 : 0;
}
</pre></div>

<p>retrieves the latest result of the BusinessVersionCheck cloud service
that was stored in the system_data table.</p>

<p>returns 1 if available.</p>

<h2 id="OTRSBusinessIsCorrectlyDeployed">OTRSBusinessIsCorrectlyDeployed()</h2>

<p><a href="#" onclick="toggleCode('method_OTRSBusinessIsCorrectlyDeployed');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_OTRSBusinessIsCorrectlyDeployed">
                                        <pre>

sub OTRSBusinessIsCorrectlyDeployed {
    my ( $Self, %Param ) = @_;

    my $Package = $Self->_GetOTRSBusinessPackageFromRepository();

    # Package not found -> return failure
    return if !$Package;

    # first check the regular way if the files are present and the package
    # itself is installed correctly
    return if !$Kernel::OM->Get('Kernel::System::Package')->DeployCheck(
        Name    => $Package->{Name}->{Content},
        Version => $Package->{Version}->{Content},
    );

    # check if all tables have been created correctly
    # we can't rely on any .opm file here, so we just check
    # the list of tables manually
    my $DBObject = $Kernel::OM->Get('Kernel::System::DB');

    TABLES:
    for my $Table (qw(chat chat_participant chat_message)) {

        # if a table does not exist, $TablePresent will be 'undef' for this table
        my $TablePresent = $DBObject->Do(
            SQL   => "SELECT * FROM $Table",
            Limit => 1,
        );

        return if !$TablePresent;
    }

    return 1;
}
</pre></div>

<p>checks if the OTRSBusiness package is correctly
deployed or if it needs to be reinstalled.</p>

<h2 id="OTRSBusinessIsReinstallable">OTRSBusinessIsReinstallable()</h2>

<p><a href="#" onclick="toggleCode('method_OTRSBusinessIsReinstallable');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_OTRSBusinessIsReinstallable">
                                        <pre>

sub OTRSBusinessIsReinstallable {
    my ( $Self, %Param ) = @_;

    my $Package = $Self->_GetOTRSBusinessPackageFromRepository();

    # Package not found -> return failure
    return if !$Package;

    return $Kernel::OM->Get('Kernel::System::Package')->_CheckFramework(
        Framework => $Package->{Framework},
    );
}
</pre></div>

<p>checks if the OTRSBusiness package can be reinstalled
(if it supports the current framework version). If not,
an update might be needed.</p>

<h2 id="OTRSBusinessIsUpdateable">OTRSBusinessIsUpdateable()</h2>

<p><a href="#" onclick="toggleCode('method_OTRSBusinessIsUpdateable');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_OTRSBusinessIsUpdateable">
                                        <pre>

sub OTRSBusinessIsUpdateable {
    my ( $Self, %Param ) = @_;

    return 0 if $Self->{CloudServicesDisabled};

    my $Package = $Self->_GetOTRSBusinessPackageFromRepository();
    return if !$Package;

    my $CloudServiceObject = $Kernel::OM->Get('Kernel::System::CloudService::Backend::Run');
    my $RequestResult      = $CloudServiceObject->Request(
        RequestData => {
            OTRSBusiness => [
                {
                    Operation => 'BusinessVersionCheck',
                    Data      => {
                        OnlyStable => $Self->{OnlyStable},
                    },
                },
            ],
        },
    );

    my $OperationResult;
    if ( IsHashRefWithData($RequestResult) ) {
        $OperationResult = $CloudServiceObject->OperationResultGet(
            RequestResult => $RequestResult,
            CloudService  => 'OTRSBusiness',
            Operation     => 'BusinessVersionCheck',
        );

        if ( $OperationResult && $OperationResult->{Success} ) {

            $Self->HandleBusinessVersionCheckCloudServiceResult( OperationResult => $OperationResult );

            if ( $OperationResult->{Data}->{LatestVersionForCurrentFramework} ) {
                return $Kernel::OM->Get('Kernel::System::Package')->_CheckVersion(
                    VersionNew       => $OperationResult->{Data}->{LatestVersionForCurrentFramework},
                    VersionInstalled => $Package->{Version}->{Content},
                    Type             => 'Max',
                );
            }
        }
    }

    return 0;
}
</pre></div>

<p>checks with cloud.otrs.com if the OTRSBusiness package is available in a newer version
than the one currently installed. The result of this check will be stored in the
system_data table for offline usage.</p>

<h2 id="OTRSBusinessVersionCheckOffline">OTRSBusinessVersionCheckOffline()</h2>

<p><a href="#" onclick="toggleCode('method_OTRSBusinessVersionCheckOffline');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_OTRSBusinessVersionCheckOffline">
                                        <pre>

sub OTRSBusinessVersionCheckOffline {
    my ( $Self, %Param ) = @_;

    my $Package = $Self->_GetOTRSBusinessPackageFromRepository();
    return if !$Package;

    my %EntitlementData = $Kernel::OM->Get('Kernel::System::SystemData')->SystemDataGroupGet(
        Group => 'OTRSBusiness',
    );

    my %Result = (
        FrameworkUpdateAvailable => $EntitlementData{FrameworkUpdateAvailable} // '',
    );

    if ( $EntitlementData{LatestVersionForCurrentFramework} ) {
        $Result{OTRSBusinessUpdateAvailable} = $Kernel::OM->Get('Kernel::System::Package')->_CheckVersion(
            VersionNew       => $EntitlementData{LatestVersionForCurrentFramework},
            VersionInstalled => $Package->{Version}->{Content},
            Type             => 'Max',
        );
    }

    return %Result;
}
</pre></div>

<p>retrieves the latest result of the BusinessVersionCheck cloud service
that was stored in the system_data table.</p>

<pre>    my %Result = $OTRSBusinessObject-&gt;OTRSBusinessVersionCheckOffline();

</pre><p>returns</p>

<pre>    $Result = (
        OTRSBusinessUpdateAvailable      =&gt; 1,  # if applicable
        FrameworkUpdateAvailable         =&gt; 1,  # if applicable
    );

</pre><h2 id="OTRSBusinessGetDependencies">OTRSBusinessGetDependencies()</h2>

<p><a href="#" onclick="toggleCode('method_OTRSBusinessGetDependencies');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_OTRSBusinessGetDependencies">
                                        <pre>

sub OTRSBusinessGetDependencies {
    my ( $Self, %Param ) = @_;

    my @Packages = $Kernel::OM->Get('Kernel::System::Package')->RepositoryList();

    my @DependentPackages;
    PACKAGE:
    for my $Package (@Packages) {

        next PACKAGE if !IsHashRefWithData($Package);
        next PACKAGE if !IsArrayRefWithData( $Package->{PackageRequired} );

        DEPENDENCY:
        for my $Dependency ( @{ $Package->{PackageRequired} } ) {

            next DEPENDENCY if !IsHashRefWithData($Dependency);
            next DEPENDENCY if !$Dependency->{Content};
            next DEPENDENCY if $Dependency->{Content} ne 'OTRSBusiness';

            push @DependentPackages, {
                Name        => $Package->{Name}->{Content},
                Vendor      => $Package->{Vendor}->{Content},
                Version     => $Package->{Version}->{Content},
                Description => $Package->{Description},
            };

            last DEPENDENCY;
        }
    }

    return \@DependentPackages;
}
</pre></div>

<p>checks if there are any active dependencies on OTRSBusiness.</p>

<h2 id="OTRSBusinessEntitlementCheck">OTRSBusinessEntitlementCheck()</h2>

<p><a href="#" onclick="toggleCode('method_OTRSBusinessEntitlementCheck');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_OTRSBusinessEntitlementCheck">
                                        <pre>

sub OTRSBusinessEntitlementCheck {
    my ( $Self, %Param ) = @_;

    return if $Self->{CloudServicesDisabled};

    my $CloudServiceObject = $Kernel::OM->Get('Kernel::System::CloudService::Backend::Run');
    my $RequestResult      = $CloudServiceObject->Request(
        RequestData => {
            OTRSBusiness => [
                {
                    Operation => 'BusinessPermission',
                    Data      => {},
                },
            ],
        },
    );

    my $OperationResult;
    if ( IsHashRefWithData($RequestResult) ) {
        $OperationResult = $CloudServiceObject->OperationResultGet(
            RequestResult => $RequestResult,
            CloudService  => 'OTRSBusiness',
            Operation     => 'BusinessPermission',
        );
    }

    # OK, so we got a successful cloud call result. Then we will use it and remember it.
    if ( IsHashRefWithData($OperationResult) && $OperationResult->{Success} ) {

        # Store it in the SystemData so that it is also available for the notification modules,
        #   even before the first run of RegistrationUpdate.
        $Self->HandleBusinessPermissionCloudServiceResult(
            OperationResult => $OperationResult,
        );
        return 1;
    }

    if ( !IsHashRefWithData($RequestResult) || !$RequestResult->{Success} ) {
        my $Message = "BusinessPermission - Can't contact cloud.otrs.com server";
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => $Message
        );
    }

    if ( !IsHashRefWithData($OperationResult) || !$OperationResult->{Success} ) {
        my $Message = "BusinessPermission - could not perform BusinessPermission check";
        if ( IsHashRefWithData($OperationResult) ) {
            $Message .= $OperationResult->{ErrorMessage};
        }
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => $Message
        );
    }

    return 0;
}
</pre></div>

<p>determines the OTRSBusiness entitlement status of this system as reported by cloud.otrs.com
and stores it in the system_data cache.</p>

<p>Returns 1 if the cloud call was successful.</p>

<h2 id="OTRSBusinessEntitlementStatus">OTRSBusinessEntitlementStatus()</h2>

<p><a href="#" onclick="toggleCode('method_OTRSBusinessEntitlementStatus');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_OTRSBusinessEntitlementStatus">
                                        <pre>

sub OTRSBusinessEntitlementStatus {
    my ( $Self, %Param ) = @_;

    # If the system is not registered, it cannot have an OB permission.
    #   Also, the BusinessPermissionChecks will not work any more, so the permission
    #   would expire after our waiting period. But in this case we can immediately deny
    #   the permission.
    my $RegistrationState = $Kernel::OM->Get('Kernel::System::SystemData')->SystemDataGet(
        Key => 'Registration::State',
    );
    if ( !$RegistrationState || $RegistrationState ne 'registered' ) {
        return 'forbidden';
    }

    if ( $Param{CallCloudService} ) {
        $Self->OTRSBusinessEntitlementCheck();
    }

    # OK. Let's look at the system_data cache now and use it if appropriate
    my %EntitlementData = $Kernel::OM->Get('Kernel::System::SystemData')->SystemDataGroupGet(
        Group => 'OTRSBusiness',
    );

    if ( !%EntitlementData || !$EntitlementData{BusinessPermission} ) {
        return 'forbidden';
    }

    # Check when the last successful BusinessPermission check was made.
    my $LastUpdateSystemTime = $Kernel::OM->Get('Kernel::System::Time')->TimeStamp2SystemTime(
        String => $EntitlementData{LastUpdateTime},
    );

    my $SecondsSinceLastUpdate = $Kernel::OM->Get('Kernel::System::Time')->SystemTime() - $LastUpdateSystemTime;

    if ( $SecondsSinceLastUpdate > $NoConnectBlockPeriod ) {
        return 'forbidden';
    }
    if ( $SecondsSinceLastUpdate > $NoConnectErrorPeriod ) {
        return 'warning-error';
    }
    if ( $SecondsSinceLastUpdate > $NoConnectWarningPeriod ) {
        return 'warning';
    }

    return 'entitled';
}
</pre></div>

<p>Returns the current entitlement status.</p>

<pre>    my $Status = $OTRSBusinessObject-&gt;OTRSBusinessEntitlementStatus(
        CallCloudService =&gt; 1,              # 0 or 1, call the cloud service before looking at the cache
    );

    $Status = 'entitled';      # everything is OK
    $Status = 'warning';       # last check was OK, and we are in the waiting period - show warning
    $Status = 'warning-error'; # last check was OK, and we are past waiting period - show error message
    $Status = 'forbidden';     # not entitled (either because the server said so or because the last check was too long ago)

</pre><h2 id="OTRSBusinessContractExpiryDateCheck">OTRSBusinessContractExpiryDateCheck()</h2>

<p><a href="#" onclick="toggleCode('method_OTRSBusinessContractExpiryDateCheck');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_OTRSBusinessContractExpiryDateCheck">
                                        <pre>

sub OTRSBusinessContractExpiryDateCheck {
    my ( $Self, %Param ) = @_;

    if ( $Param{CallCloudService} ) {
        $Self->OTRSBusinessEntitlementCheck();
    }

    # OK. Let's look at the system_data cache now and use it if appropriate
    my %EntitlementData = $Kernel::OM->Get('Kernel::System::SystemData')->SystemDataGroupGet(
        Group => 'OTRSBusiness',
    );

    if ( !%EntitlementData || !$EntitlementData{ExpiryDate} ) {
        return;
    }

    my $ExpiryDateSystemTime = $Kernel::OM->Get('Kernel::System::Time')->TimeStamp2SystemTime(
        String => $EntitlementData{ExpiryDate},
    );

    my $SecondsUntilExpiryDate = $ExpiryDateSystemTime - $Kernel::OM->Get('Kernel::System::Time')->SystemTime();

    if ( $SecondsUntilExpiryDate < $ContractExpiryWarningPeriod ) {
        return $EntitlementData{ExpiryDate};
    }

    return;
}
</pre></div>

<p>checks for the warning period before the contract expires</p>

<pre>    my $ExpiryDate = $OTRSBusinessObject-&gt;OTRSBusinessContractExpiryDateCheck();

</pre><p>returns the ExpiryDate if a warning should be displayed</p>

<pre>    $ExpiryDate = undef;                    # everything is OK, no warning
    $ExpiryDate = '2012-12-12 12:12:12'     # contract is about to expire, issue warning

</pre><h2 id="OTRSBusinessInstall">OTRSBusinessInstall()</h2>

<p><a href="#" onclick="toggleCode('method_OTRSBusinessInstall');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_OTRSBusinessInstall">
                                        <pre>

sub OTRSBusinessInstall {
    my ( $Self, %Param ) = @_;

    my $PackageString = $Self->_OTRSBusinessFileGet();
    return if !$PackageString;

    my $Install = $Kernel::OM->Get('Kernel::System::Package')->PackageInstall(
        String    => $PackageString,
        FromCloud => 1,
    );

    return $Install if !$Install;

    # now that we know that OTRSBusiness has been installed,
    # we can just preset the cache instead of just swiping it.
    $Kernel::OM->Get('Kernel::System::Cache')->Set(
        Type  => $Self->{CacheType},
        TTL   => $Self->{CacheTTL},
        Key   => 'OTRSBusinessIsInstalled',
        Value => 1,
    );

    return $Install;
}
</pre></div>

<p>downloads and installs OTRSBusiness.</p>

<h2 id="OTRSBusinessReinstall">OTRSBusinessReinstall()</h2>

<p><a href="#" onclick="toggleCode('method_OTRSBusinessReinstall');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_OTRSBusinessReinstall">
                                        <pre>

sub OTRSBusinessReinstall {
    my ( $Self, %Param ) = @_;

    my $Package = $Self->_GetOTRSBusinessPackageFromRepository();

    # Package not found -> return failure
    return if !$Package;

    my $PackageString = $Kernel::OM->Get('Kernel::System::Package')->RepositoryGet(
        Name    => $Package->{Name}->{Content},
        Version => $Package->{Version}->{Content},
    );

    return $Kernel::OM->Get('Kernel::System::Package')->PackageReinstall(
        String => $PackageString,
    );
}
</pre></div>

<p>reinstalls OTRSBusiness from local repository.</p>

<h2 id="OTRSBusinessUpdate">OTRSBusinessUpdate()</h2>

<p><a href="#" onclick="toggleCode('method_OTRSBusinessUpdate');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_OTRSBusinessUpdate">
                                        <pre>

sub OTRSBusinessUpdate {
    my ( $Self, %Param ) = @_;

    my $PackageString = $Self->_OTRSBusinessFileGet();
    return if !$PackageString;

    return $Kernel::OM->Get('Kernel::System::Package')->PackageUpgrade(
        String    => $PackageString,
        FromCloud => 1,
    );
}
</pre></div>

<p>downloads and updates OTRSBusiness.</p>

<h2 id="OTRSBusinessUninstall">OTRSBusinessUninstall()</h2>

<p><a href="#" onclick="toggleCode('method_OTRSBusinessUninstall');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_OTRSBusinessUninstall">
                                        <pre>

sub OTRSBusinessUninstall {
    my ( $Self, %Param ) = @_;

    my $Package = $Self->_GetOTRSBusinessPackageFromRepository();

    # Package not found -> return failure
    return if !$Package;

    # TODO: the following code is now Deprecated and should be removed in further versions of OTRS
    # get a list of all dynamic fields for ticket and article
    my $DynamicFieldObject = $Kernel::OM->Get('Kernel::System::DynamicField');
    my $DynamicFieldList   = $DynamicFieldObject->DynamicFieldListGet(
        Valid      => 0,
        ObjectType => [ 'Ticket', 'Article' ],
    );

    # filter only dynamic fields added by OTRSBusiness
    my %OTRSBusinessDynamicFieldTypes = (
        ContactWithData => 1,
        Database        => 1,
    );

    my $DynamicFieldBackendObject = $Kernel::OM->Get('Kernel::System::DynamicField::Backend');

    DYNAMICFIELD:
    for my $DynamicFieldConfig ( @{$DynamicFieldList} ) {
        next DYNAMICFIELD if !IsHashRefWithData($DynamicFieldConfig);
        next DYNAMICFIELD if !$OTRSBusinessDynamicFieldTypes{ $DynamicFieldConfig->{FieldType} };

        # remove data from the field
        my $ValuesDeleteSuccess = $DynamicFieldBackendObject->AllValuesDelete(
            DynamicFieldConfig => $DynamicFieldConfig,
            UserID             => 1,
        );

        if ( !$ValuesDeleteSuccess ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Values from dynamic field $DynamicFieldConfig->{Name} could not be deleted!",
            );
        }

        my $Success = $DynamicFieldObject->DynamicFieldDelete(
            ID      => $DynamicFieldConfig->{ID},
            UserID  => 1,
            Reorder => 1,
        );

        if ( !$Success ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Dynamic field $DynamicFieldConfig->{Name} could not be deleted!",
            );
        }
    }

    # TODO: end Deprecated

    my $PackageString = $Kernel::OM->Get('Kernel::System::Package')->RepositoryGet(
        Name    => $Package->{Name}->{Content},
        Version => $Package->{Version}->{Content},
    );

    my $Uninstall = $Kernel::OM->Get('Kernel::System::Package')->PackageUninstall(
        String => $PackageString,
    );

    return $Uninstall if !$Uninstall;

    # now that we know that OTRSBusiness has been uninstalled,
    # we can just preset the cache instead of just swiping it.
    $Kernel::OM->Get('Kernel::System::Cache')->Set(
        Type  => $Self->{CacheType},
        TTL   => $Self->{CacheTTL},
        Key   => 'OTRSBusinessIsInstalled',
        Value => 0,
    );

    return $Uninstall;
}
</pre></div>

<p>removes OTRSBusiness from the system.</p>

<h2 id="OTRSBusinessCommandNextUpdateTimeSet">OTRSBusinessCommandNextUpdateTimeSet()</h2>

<p><a href="#" onclick="toggleCode('method_OTRSBusinessCommandNextUpdateTimeSet');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_OTRSBusinessCommandNextUpdateTimeSet">
                                        <pre>

sub OTRSBusinessCommandNextUpdateTimeSet {
    my ( $Self, %Param ) = @_;

    return if !$Param{Command};

    my $Key = "OTRSBusiness::$Param{Command}::NextUpdateTime";

    my $SystemDataObject = $Kernel::OM->Get('Kernel::System::SystemData');

    my $NextUpdateTime = $SystemDataObject->SystemDataGet(
        Key => $Key,
    );

    # set the default next update seconds offset
    my $NextUpdateSecondsOffset = 60 * 60 * 24;

    # generate a random seconds offset, if no next update time exists
    if ( !$NextUpdateTime ) {

        # create the random numbers
        my $RandomHour   = int 20 + rand 23 - 20;
        my $RandomMinute = int rand 60;

        # create the random seconds offset
        $NextUpdateSecondsOffset = 60 * 60 * $RandomHour + ( 60 * $RandomMinute );
    }

    # get time object
    my $TimeObject = $Kernel::OM->Get('Kernel::System::Time');

    my $CalculatedNextUpdateTime = $TimeObject->SystemTime2TimeStamp(
        SystemTime => $TimeObject->SystemTime() + $NextUpdateSecondsOffset,
    );

    if ( defined $NextUpdateTime ) {
        $SystemDataObject->SystemDataUpdate(
            Key    => $Key,
            Value  => $CalculatedNextUpdateTime,
            UserID => 1,
        );
    }
    else {
        $SystemDataObject->SystemDataAdd(
            Key    => $Key,
            Value  => $CalculatedNextUpdateTime,
            UserID => 1,
        );
    }

    return 1;
}
</pre></div>

<p>Set the next update time for the given command in the system data table storage.</p>

<pre>    my $Success = $OTRSBusinessObject-&gt;OTRSBusinessCommandNextUpdateTimeSet(
        Command =&gt; 'AvailabilityCheck',
    );

</pre><p>Returns 1 if the next update time was set successfully.</p>

<h1 id="TERMS_AND_CONDITIONS">TERMS AND CONDITIONS <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>This software is part of the OTRS project (<a href="http://otrs.org/">http://otrs.org/</a>).</p>

<p>This software comes with ABSOLUTELY NO WARRANTY. For details, see
the enclosed file COPYING for license information (AGPL). If you
did not receive this file, see <a href="http://www.gnu.org/licenses/agpl.txt">http://www.gnu.org/licenses/agpl.txt</a>.</p>


</div><div class="footer">generated by <a href="http://search.cpan.org/perldoc?Pod%3A%3AProjectDocs">Pod::ProjectDocs</a></div></body>
</html>
