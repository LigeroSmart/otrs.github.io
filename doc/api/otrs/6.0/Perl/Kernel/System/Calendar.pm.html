<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.1//EN" "http://www.w3.org/TR/xhtml11/DTD/xhtml11.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en">
<head>
<!-- otrs.github.io -->
<link href="https://netdna.bootstrapcdn.com/font-awesome/4.0.3/css/font-awesome.css" rel="stylesheet">
<link href="https://fonts.googleapis.com/css?family=Roboto" rel="stylesheet" type="text/css">
<link rel="stylesheet" href="../../../../../../documentation.css">
<script src="https://code.jquery.com/jquery-1.11.0.min.js"></script>
<script type="text/javascript" src="../../../../../../documentation.js"></script>
<link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/9.9.0/styles/default.min.css">
<script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/9.9.0/highlight.min.js"></script>
<script>
$(document).ready(function() {

    // Hint languages to prevent false matches (php for example).
    hljs.configure({
        languages: ['perl', 'javascript', 'xml', 'html', 'css', 'json', 'yaml']
    });

    // programlistings in manuals
    $('pre.programlisting').each(function(i, block) {
        hljs.highlightBlock(block);
    });
    // code snippets in Perl API docs
    $('.pod pre').addClass('perl').each(function(i, block) {
        hljs.highlightBlock(block);
    });
});</script>
<!-- otrs.github.io -->

	<title>Kernel::System::Calendar</title>
<meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
  <script type="text/javascript">

  function toggleCode( id ) {
    if ( document.getElementById )
      elem = document.getElementById( id );
    else if ( document.all )
      elem = eval( "document.all." + id );
    else
      return false;

    elemStyle = elem.style;

    if ( elemStyle.display != "block" ) {
      elemStyle.display = "block"
    } else {
      elemStyle.display = "none"
    }

    return true;
  }
  document.writeln( "<style type=\"text/css\">div.method-source-code { display: none }</style>" )
  </script></head>
<body>
<div class="box">
  <h1 class="t1">OTRS 6.0 API Reference Perl</h1>
  <table>
    <tr>
      <td class="label">Description</td>
      <td class="cell">manuals and libraries</td>
    </tr>
  </table>
</div>
<div class="path">
  <a href="../../index.html">OTRS 6.0 API Reference Perl</a> &gt; Perl Modules &gt;
  Kernel::System::Calendar
</div>
<div>
<a href="../../src/Kernel/System/Calendar.pm">Source</a>
</div>
<div class="pod">
<!-- INDEX START -->
<h3 id="TOP">Index</h3>
<ul>
	<li><a href="#NAME">NAME</a></li>
	<li><a href="#DESCRIPTION">DESCRIPTION</a></li>
	<li><a href="#PUBLIC_INTERFACE">PUBLIC INTERFACE</a><br />
<ul>
	<li><a href="#new">new()</a></li>
	<li><a href="#CalendarCreate">CalendarCreate()</a></li>
	<li><a href="#CalendarGet">CalendarGet()</a></li>
	<li><a href="#CalendarList">CalendarList()</a></li>
	<li><a href="#CalendarUpdate">CalendarUpdate()</a></li>
	<li><a href="#CalendarImport">CalendarImport()</a></li>
	<li><a href="#CalendarExport">CalendarExport()</a></li>
	<li><a href="#CalendarPermissionGet">CalendarPermissionGet()</a></li>
	<li><a href="#TicketAppointmentProcessTicket">TicketAppointmentProcessTicket()</a></li>
	<li><a href="#TicketAppointmentProcessCalendar">TicketAppointmentProcessCalendar()</a></li>
	<li><a href="#TicketAppointmentProcessRule">TicketAppointmentProcessRule()</a></li>
	<li><a href="#TicketAppointmentUpdateTicket">TicketAppointmentUpdateTicket()</a></li>
	<li><a href="#TicketAppointmentTicketID">TicketAppointmentTicketID()</a></li>
	<li><a href="#TicketAppointmentRuleIDsGet">TicketAppointmentRuleIDsGet()</a></li>
	<li><a href="#TicketAppointmentRuleGet">TicketAppointmentRuleGet()</a></li>
	<li><a href="#TicketAppointmentTypesGet">TicketAppointmentTypesGet()</a></li>
	<li><a href="#TicketAppointmentDelete">TicketAppointmentDelete()</a></li>
	<li><a href="#GetAccessToken">GetAccessToken()</a></li>
	<li><a href="#GetTextColor">GetTextColor()</a></li>
</ul>
</li>
	<li><a href="#TERMS_AND_CONDITIONS">TERMS AND CONDITIONS</a></li>
</ul>
<hr />
<!-- INDEX END -->

<h1 id="NAME">NAME <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>Kernel::System::Calendar - calendar lib</p>

<h1 id="DESCRIPTION">DESCRIPTION <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>All calendar functions.</p>

<h1 id="PUBLIC_INTERFACE">PUBLIC INTERFACE <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<h2 id="new">new()</h2>

<p><a href="#" onclick="toggleCode('method_new');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_new">
                                        <pre>

sub new {
    my ( $Type, %Param ) = @_;

    # allocate new hash for object
    my $Self = {%Param};
    bless( $Self, $Type );

    @ISA = qw(
        Kernel::System::EventHandler
    );

    # init of event handler
    $Self->EventHandlerInit(
        Config => 'AppointmentCalendar::EventModulePost',
    );

    $Self->{CacheType} = 'Calendar';
    $Self->{CacheTTL}  = 60 * 60 * 24 * 20;

    return $Self;
}
</pre></div>

<p>create an object. Do not use it directly, instead use:</p>

<pre>    use Kernel::System::ObjectManager;
    local $Kernel::OM = Kernel::System::ObjectManager-&gt;new();
    my $CalendarObject = $Kernel::OM-&gt;Get('Kernel::System::Calendar');
</pre><h2 id="CalendarCreate">CalendarCreate()</h2>

<p><a href="#" onclick="toggleCode('method_CalendarCreate');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CalendarCreate">
                                        <pre>

sub CalendarCreate {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    for my $Needed (qw(CalendarName GroupID Color UserID)) {
        if ( !$Param{$Needed} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Need $Needed!",
            );
            return;
        }
    }

    # check color
    if ( !( $Param{Color} =~ /#[A-F0-9]{3,6}/i ) ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => 'Color must be in hexadecimal RGB notation, eg. #FFFFFF.',
        );
        return;
    }

    # reset ticket appointments
    if ( !( scalar @{ $Param{TicketAppointments} // [] } ) ) {
        $Param{TicketAppointments} = undef;
    }

    # make it uppercase for the sake of consistency
    $Param{Color} = uc $Param{Color};

    my $ValidID = defined $Param{ValidID} ? $Param{ValidID} : 1;

    my %Calendar = $Self->CalendarGet(
        CalendarName => $Param{CalendarName},
    );

    # return if calendar with same name already exists
    return if %Calendar;

    # create salt string
    my $SaltString = $Kernel::OM->Get('Kernel::System::Main')->GenerateRandomString(
        Length => 64,
    );

    # serialize and encode ticket appointment data
    my $TicketAppointments;
    if ( $Param{TicketAppointments} ) {
        $TicketAppointments = $Kernel::OM->Get('Kernel::System::Storable')->Serialize(
            Data => $Param{TicketAppointments},
        );
        $Kernel::OM->Get('Kernel::System::Encode')->EncodeOutput($TicketAppointments);
        $TicketAppointments = MIME::Base64::encode_base64($TicketAppointments);
    }

    my $SQL = '
        INSERT INTO calendar
            (group_id, name, salt_string, color, ticket_appointments, create_time, create_by,
            change_time, change_by, valid_id)
        VALUES (?, ?, ?, ?, ?, current_timestamp, ?, current_timestamp, ?, ?)
    ';

    # create db record
    return if !$Kernel::OM->Get('Kernel::System::DB')->Do(
        SQL  => $SQL,
        Bind => [
            \$Param{GroupID}, \$Param{CalendarName}, \$SaltString, \$Param{Color},
            \$TicketAppointments, \$Param{UserID}, \$Param{UserID}, \$ValidID
        ],
    );

    %Calendar = $Self->CalendarGet(
        CalendarName => $Param{CalendarName},
        UserID       => $Param{UserID},
    );
    return if !%Calendar;

    my $CacheObject = $Kernel::OM->Get('Kernel::System::Cache');

    # cache value
    $CacheObject->Set(
        Type  => $Self->{CacheType},
        Key   => $Calendar{CalendarID},
        Value => \%Calendar,
        TTL   => $Self->{CacheTTL},
    );

    # reset CalendarList
    $CacheObject->CleanUp(
        Type => 'CalendarList',
    );

    # fire event
    $Self->EventHandler(
        Event => 'CalendarCreate',
        Data  => {
            %Calendar,
        },
        UserID => $Param{UserID},
    );

    return %Calendar;
}
</pre></div>

<p>creates a new calendar for given user.</p>

<pre>    my %Calendar = $CalendarObject-&gt;CalendarCreate(
        CalendarName    =&gt; 'Meetings',          # (required) Personal calendar name
        GroupID         =&gt; 3,                   # (required) GroupID
        Color           =&gt; '#FF7700',           # (required) Color in hexadecimal RGB notation
        UserID          =&gt; 4,                   # (required) UserID

        TicketAppointments =&gt; [                 # (optional) Ticket appointments, array ref of hashes
            {
                StartDate =&gt; 'FirstResponse',
                EndDate   =&gt; 'Plus_5',
                QueueID   =&gt; [ 2 ],
                SearchParams =&gt; {
                    Title =&gt; 'This is a title',
                    Types =&gt; 'This is a type',
                },
            },
        ],

        ValidID =&gt; 1,                   # (optional) Default is 1.
    );
</pre><p>returns Calendar hash if successful:
    %Calendar = (
        CalendarID   =&gt; 2,
        GroupID      =&gt; 3,
        CalendarName =&gt; 'Meetings',
        CreateTime   =&gt; '2016-01-01 08:00:00',
        CreateBy     =&gt; 4,
        ChangeTime   =&gt; '2016-01-01 08:00:00',
        ChangeBy     =&gt; 4,
        ValidID      =&gt; 1,
    );</p>

<p>Events:
    CalendarCreate</p>

<h2 id="CalendarGet">CalendarGet()</h2>

<p><a href="#" onclick="toggleCode('method_CalendarGet');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CalendarGet">
                                        <pre>

sub CalendarGet {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    if ( !$Param{CalendarID} && !$Param{CalendarName} ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "Need CalendarID or CalendarName!",
        );
        return;
    }

    my %Calendar;

    if ( $Param{CalendarID} ) {

        # check if value is cached
        my $Data = $Kernel::OM->Get('Kernel::System::Cache')->Get(
            Type => $Self->{CacheType},
            Key  => $Param{CalendarID},
        );

        if ( IsHashRefWithData($Data) ) {
            %Calendar = %{$Data};
        }
    }

    if ( !%Calendar ) {

        # create db object
        my $DBObject = $Kernel::OM->Get('Kernel::System::DB');

        my $SQL = '
            SELECT id, group_id, name, color, ticket_appointments, create_time, create_by,
            change_time, change_by, valid_id
            FROM calendar
            WHERE
        ';

        my @Bind;
        if ( $Param{CalendarID} ) {
            $SQL .= '
                id=?
            ';
            push @Bind, \$Param{CalendarID};
        }
        else {
            $SQL .= '
                name=?
            ';
            push @Bind, \$Param{CalendarName};
        }

        # db query
        return if !$DBObject->Prepare(
            SQL   => $SQL,
            Bind  => \@Bind,
            Limit => 1,
        );

        while ( my @Row = $DBObject->FetchrowArray() ) {

            # decode and deserialize ticket appointment data
            my $TicketAppointments;
            if ( $Row[4] ) {
                my $DecodedData = MIME::Base64::decode_base64( $Row[4] );
                $TicketAppointments = $Kernel::OM->Get('Kernel::System::Storable')->Deserialize(
                    Data => $DecodedData,
                );
                $TicketAppointments = undef if ref $TicketAppointments ne 'ARRAY';
            }

            $Calendar{CalendarID}         = $Row[0];
            $Calendar{GroupID}            = $Row[1];
            $Calendar{CalendarName}       = $Row[2];
            $Calendar{Color}              = $Row[3];
            $Calendar{TicketAppointments} = $TicketAppointments;
            $Calendar{CreateTime}         = $Row[5];
            $Calendar{CreateBy}           = $Row[6];
            $Calendar{ChangeTime}         = $Row[7];
            $Calendar{ChangeBy}           = $Row[8];
            $Calendar{ValidID}            = $Row[9];
        }

        if ( $Param{CalendarID} ) {

            # cache
            $Kernel::OM->Get('Kernel::System::Cache')->Set(
                Type  => $Self->{CacheType},
                Key   => $Param{CalendarID},
                Value => \%Calendar,
                TTL   => $Self->{CacheTTL},
            );
        }

        if ( $Param{UserID} && $Calendar{GroupID} ) {

            # get user groups
            my %GroupList = $Kernel::OM->Get('Kernel::System::Group')->PermissionUserGet(
                UserID => $Param{UserID},
                Type   => 'ro',
            );

            if ( !grep { $Calendar{GroupID} == $_ } keys %GroupList ) {
                %Calendar = ();
            }
        }
    }

    return %Calendar;
}
</pre></div>

<p>Get calendar by name or id.</p>

<pre>    my %Calendar = $CalendarObject-&gt;CalendarGet(
        CalendarName =&gt; 'Meetings',          # (required) Calendar name
                                             # or
        CalendarID   =&gt; 4,                   # (required) CalendarID

        UserID       =&gt; 2,                   # (optional) UserID - System will check if user has access to calendar if provided
    );
</pre><p>Returns Calendar data:</p>

<pre>    %Calendar = (
        CalendarID         =&gt; 2,
        GroupID            =&gt; 3,
        CalendarName       =&gt; 'Meetings',
        Color              =&gt; '#FF7700',
        TicketAppointments =&gt; [
            {
                StartDate =&gt; 'FirstResponse',
                EndDate   =&gt; 'Plus_5',
                QueueID   =&gt; [ 2 ],
                SearchParams =&gt; {
                    Title =&gt; 'This is a title',
                    Types =&gt; 'This is a type',
                },
            },
        ],
        CreateTime =&gt; '2016-01-01 08:00:00',
        CreateBy   =&gt; 1,
        ChangeTime =&gt; '2016-01-01 08:00:00',
        ChangeBy   =&gt; 1,
        ValidID    =&gt; 1,
    );
</pre><h2 id="CalendarList">CalendarList()</h2>

<p><a href="#" onclick="toggleCode('method_CalendarList');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CalendarList">
                                        <pre>

sub CalendarList {
    my ( $Self, %Param ) = @_;

    # Make different cache type for list (so we can clear cache by this value)
    my $CacheType     = 'CalendarList';
    my $CacheKeyUser  = $Param{UserID} || 'all-user-ids';
    my $CacheKeyValid = $Param{ValidID} || 'all-valid-ids';

    my $CacheObject = $Kernel::OM->Get('Kernel::System::Cache');

    # get cached value if exists
    my $Data = $CacheObject->Get(
        Type => $CacheType,
        Key  => "$CacheKeyUser-$CacheKeyValid",
    );

    if ( !IsArrayRefWithData($Data) ) {

        # create needed objects
        my $DBObject = $Kernel::OM->Get('Kernel::System::DB');

        my $SQL = '
            SELECT id, group_id, name, color, create_time, create_by, change_time, change_by,
            valid_id
            FROM calendar
            WHERE 1=1
        ';
        my @Bind;

        if ( $Param{ValidID} ) {
            $SQL .= ' AND valid_id=? ';
            push @Bind, \$Param{ValidID};
        }
        $SQL .= 'ORDER BY id ASC';

        # db query
        return if !$DBObject->Prepare(
            SQL  => $SQL,
            Bind => \@Bind,
        );

        my @Result;
        while ( my @Row = $DBObject->FetchrowArray() ) {
            my %Calendar;
            $Calendar{CalendarID}   = $Row[0];
            $Calendar{GroupID}      = $Row[1];
            $Calendar{CalendarName} = $Row[2];
            $Calendar{Color}        = $Row[3];
            $Calendar{CreateTime}   = $Row[4];
            $Calendar{CreateBy}     = $Row[5];
            $Calendar{ChangeTime}   = $Row[6];
            $Calendar{ChangeBy}     = $Row[7];
            $Calendar{ValidID}      = $Row[8];
            push @Result, \%Calendar;
        }

        # cache data
        $CacheObject->Set(
            Type  => $CacheType,
            Key   => "$CacheKeyUser-$CacheKeyValid",
            Value => \@Result,
            TTL   => $Self->{CacheTTL},
        );

        $Data = \@Result;
    }

    if ( $Param{UserID} ) {

        # get user groups
        my %GroupList = $Kernel::OM->Get('Kernel::System::Group')->PermissionUserGet(
            UserID => $Param{UserID},
            Type   => $Param{Permission} || 'ro',
        );

        my @Result;

        for my $Item ( @{$Data} ) {
            if ( grep { $Item->{GroupID} == $_ } keys %GroupList ) {
                push @Result, $Item;
            }
        }

        $Data = \@Result;
    }

    return @{$Data};
}
</pre></div>

<p>Get calendar list.</p>

<pre>    my @Result = $CalendarObject-&gt;CalendarList(
        UserID     =&gt; 4,            # (optional) For permission check
        Permission =&gt; 'rw',         # (optional) Required permission (default ro)
        ValidID    =&gt; 1,            # (optional) Default 0.
                                    # 0 - All states
                                    # 1 - All valid
                                    # 2 - All invalid
                                    # 3 - All temporary invalid
    );
</pre><p>Returns:</p>

<pre>    @Result = [
        {
            CalendarID   =&gt; 2,
            GroupID      =&gt; 3,
            CalendarName =&gt; 'Meetings',
            Color        =&gt; '#FF7700',
            CreateTime   =&gt; '2016-01-01 08:00:00',
            CreateBy     =&gt; 3,
            ChangeTime   =&gt; '2016-01-01 08:00:00',
            ChangeBy     =&gt; 3,
            ValidID      =&gt; 1,
        },
        {
            CalendarID   =&gt; 3,
            GroupID      =&gt; 3,
            CalendarName =&gt; 'Customer presentations',
            Color        =&gt; '#BB00BB',
            CreateTime   =&gt; '2016-01-01 08:00:00',
            CreateBy     =&gt; 3,
            ChangeTime   =&gt; '2016-01-01 08:00:00',
            ChangeBy     =&gt; 3,
            ValidID      =&gt; 0,
        },
        ...
    ];
</pre><h2 id="CalendarUpdate">CalendarUpdate()</h2>

<p><a href="#" onclick="toggleCode('method_CalendarUpdate');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CalendarUpdate">
                                        <pre>

sub CalendarUpdate {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    for my $Needed (qw(CalendarID GroupID CalendarName Color UserID ValidID)) {
        if ( !$Param{$Needed} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Need $Needed!",
            );
            return;
        }
    }

    # check color
    if ( !( $Param{Color} =~ /#[A-F0-9]{3,6}/i ) ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => 'Color must be in hexadecimal RGB notation, eg. #FFFFFF.',
        );
        return;
    }

    # reset ticket appointments
    if ( !( scalar @{ $Param{TicketAppointments} // [] } ) ) {
        $Param{TicketAppointments} = undef;
    }

    # make it uppercase for the sake of consistency
    $Param{Color} = uc $Param{Color};

    # serialize and encode ticket appointment data
    my $TicketAppointments;
    if ( $Param{TicketAppointments} ) {
        $TicketAppointments = $Kernel::OM->Get('Kernel::System::Storable')->Serialize(
            Data => $Param{TicketAppointments},
        );
        $Kernel::OM->Get('Kernel::System::Encode')->EncodeOutput($TicketAppointments);
        $TicketAppointments = MIME::Base64::encode_base64($TicketAppointments);
    }

    my $SQL = '
        UPDATE calendar
        SET group_id=?, name=?, color=?, ticket_appointments=?, change_time=current_timestamp,
        change_by=?, valid_id=?
    ';

    my @Bind;
    push @Bind, \$Param{GroupID}, \$Param{CalendarName}, \$Param{Color}, \$TicketAppointments,
        \$Param{UserID}, \$Param{ValidID};

    $SQL .= '
        WHERE id=?
    ';
    push @Bind, \$Param{CalendarID};

    # create db record
    return if !$Kernel::OM->Get('Kernel::System::DB')->Do(
        SQL  => $SQL,
        Bind => \@Bind,
    );

    # get cache object
    my $CacheObject = $Kernel::OM->Get('Kernel::System::Cache');

    # clear cache
    $CacheObject->CleanUp(
        Type => 'CalendarList',
    );

    $CacheObject->Delete(
        Type => $Self->{CacheType},
        Key  => $Param{CalendarID},
    );

    # fire event
    $Self->EventHandler(
        Event => 'CalendarUpdate',
        Data  => {
            CalendarID => $Param{CalendarID},
        },
        UserID => $Param{UserID},
    );

    return 1;
}
</pre></div>

<p>updates an existing calendar.</p>

<pre>    my $Success = $CalendarObject-&gt;CalendarUpdate(
        CalendarID       =&gt; 1,                   # (required) CalendarID
        GroupID          =&gt; 2,                   # (required) Calendar group
        CalendarName     =&gt; 'Meetings',          # (required) Personal calendar name
        Color            =&gt; '#FF9900',           # (required) Color in hexadecimal RGB notation
        UserID           =&gt; 4,                   # (required) UserID (who made update)
        ValidID          =&gt; 1,                   # (required) ValidID

        TicketAppointments =&gt; [                 # (optional) Ticket appointments, array ref of hashes
            {
                StartDate =&gt; 'FirstResponse',
                EndDate   =&gt; 'Plus_5',
                QueueID   =&gt; [ 2 ],
                SearchParams =&gt; {
                    Title =&gt; 'This is a title',
                    Types =&gt; 'This is a type',
                },
            },
        ],
    );
</pre><p>Returns 1 if successful.</p>

<p>Events:
    CalendarUpdate</p>

<h2 id="CalendarImport">CalendarImport()</h2>

<p><a href="#" onclick="toggleCode('method_CalendarImport');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CalendarImport">
                                        <pre>

sub CalendarImport {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    for my $Needed (qw(Data UserID)) {
        if ( !$Param{$Needed} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Need $Needed!",
            );
            return;
        }
    }

    return if !IsHashRefWithData( $Param{Data} );
    return if !IsHashRefWithData( $Param{Data}->{CalendarData} );

    if (
        defined $Param{Data}->{CalendarData}->{TicketAppointments}
        && IsArrayRefWithData( $Param{Data}->{CalendarData}->{TicketAppointments} )
        )
    {
        # Get queue create permissions for the user.
        my %UserGroups = $Kernel::OM->Get('Kernel::System::Group')->PermissionUserGet(
            UserID => $Param{UserID},
            Type   => 'create',
        );

        my @ValidIDs = $Kernel::OM->Get('Kernel::System::Valid')->ValidIDsGet();

        my $QueueObject = $Kernel::OM->Get('Kernel::System::Queue');

        # Queue field in ticket appointments is mandatory, check if it's present and valid.
        for my $Rule ( @{ $Param{Data}->{CalendarData}->{TicketAppointments} } ) {
            if ( defined $Rule->{QueueID} && IsArrayRefWithData( $Rule->{QueueID} ) ) {

                QUEUE_ID:
                for my $QueueID ( sort @{ $Rule->{QueueID} || [] } ) {
                    my %QueueData = $QueueObject->QueueGet( ID => $QueueID );

                    if (
                        !grep { $_ eq $QueueData{ValidID} } @ValidIDs
                        || !$UserGroups{ $QueueData{GroupID} }
                        )
                    {
                        $Kernel::OM->Get('Kernel::System::Log')->Log(
                            Priority => 'error',
                            Message  => "Invalid queue ID $QueueID in ticket appointment rule or no permissions!",
                        );
                        return;
                    }
                }
            }
            else {
                $Kernel::OM->Get('Kernel::System::Log')->Log(
                    Priority => 'error',
                    Message  => 'Need queue ID in ticket appointment rules!',
                );
                return;
            }
        }
    }

    # check for an existing calendar
    my %ExistingCalendar = $Self->CalendarGet(
        CalendarName => $Param{Data}->{CalendarData}->{CalendarName},
    );

    my $CalendarID;

    # create new calendar
    if ( !IsHashRefWithData( \%ExistingCalendar ) ) {
        my %Calendar = $Self->CalendarCreate(
            %{ $Param{Data}->{CalendarData} },
            UserID => $Param{UserID},
        );
        return if !$Calendar{CalendarID};

        $CalendarID = $Calendar{CalendarID};
    }

    # update existing calendar
    else {
        if ( $Param{OverwriteExistingEntities} ) {
            my $Success = $Self->CalendarUpdate(
                %{ $Param{Data}->{CalendarData} },
                CalendarID => $ExistingCalendar{CalendarID},
                UserID     => $Param{UserID},
            );
            return if !$Success;
        }

        $CalendarID = $ExistingCalendar{CalendarID};
    }

    # import appointments
    if ( $CalendarID && IsArrayRefWithData( $Param{Data}->{AppointmentData} ) ) {
        my $AppointmentObject = $Kernel::OM->Get('Kernel::System::Calendar::Appointment');
        my $AppointmentID;

        APPOINTMENT:
        for my $Appointment ( @{ $Param{Data}->{AppointmentData} } ) {

            # add to existing calendar
            $Appointment->{CalendarID} = $CalendarID;

            # create new appointment if NOT overwriting existing entities
            $Appointment->{UniqueID} = undef if !$Param{OverwriteExistingEntities};

            # skip adding automatic recurring occurrences
            if ( $Appointment->{Recurring} ) {
                $Appointment->{RecurringRaw} = 1;
            }

            # set parent id to last appointment id
            if ( $Appointment->{ParentID} ) {
                $Appointment->{ParentID} = $AppointmentID;
            }

            $AppointmentID = $AppointmentObject->AppointmentCreate(
                %{$Appointment},
                UserID => $Param{UserID},
            );
            return if !$AppointmentID;
        }
    }

    return 1;
}
</pre></div>

<p>import a calendar</p>

<pre>    my $Success = $CalendarObject-&gt;CalendarImport(
        Data =&gt; {
            CalendarData =&gt; {
                CalendarID   =&gt; 2,
                GroupID      =&gt; 3,
                CalendarName =&gt; 'Meetings',
                Color        =&gt; '#FF7700',
                ValidID      =&gt; 1,
            },
            AppointmentData =&gt; {
                {
                    AppointmentID       =&gt; 2,
                    ParentID            =&gt; 1,
                    CalendarID          =&gt; 1,
                    UniqueID            =&gt; '20160101T160000-71E386@localhost',
                    ...
                },
                ...
            },
        },
        OverwriteExistingEntities =&gt; 0,     # (optional) Overwrite existing calendar and appointments, default: 0
                                            # Calendar with same name will be overwritten
                                            # Appointments with same UniqueID in existing calendar will be overwritten
        UserID =&gt; 1,
    );
</pre><p>returns 1 if successful</p>

<h2 id="CalendarExport">CalendarExport()</h2>

<p><a href="#" onclick="toggleCode('method_CalendarExport');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CalendarExport">
                                        <pre>

sub CalendarExport {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    for my $Needed (qw(CalendarID UserID)) {
        if ( !$Param{$Needed} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Need $Needed!",
            );
            return;
        }
    }

    # get calendar data
    my %CalendarData = $Self->CalendarGet(
        CalendarID => $Param{CalendarID},
        UserID     => $Param{UserID},
    );
    return if !IsHashRefWithData( \%CalendarData );

    # get appointment object
    my $AppointmentObject = $Kernel::OM->Get('Kernel::System::Calendar::Appointment');

    # get list of appointments
    my @Appointments = $AppointmentObject->AppointmentList(
        CalendarID => $Param{CalendarID},
        Result     => 'ARRAY',
    );

    my @AppointmentData;

    APPOINTMENTID:
    for my $AppointmentID (@Appointments) {
        my %Appointment = $AppointmentObject->AppointmentGet(
            AppointmentID => $AppointmentID,
        );
        next APPOINTMENTID if !%Appointment;
        next APPOINTMENTID if $Appointment{TicketAppointmentRuleID};

        push @AppointmentData, \%Appointment;
    }

    my %Result = (
        CalendarData    => \%CalendarData,
        AppointmentData => \@AppointmentData,
    );

    return %Result;
}
</pre></div>

<p>export a calendar</p>

<pre>    my %Data = $CalendarObject-&gt;CalendarExport(
        CalendarID =&gt; 2,
        UserID     =&gt; 1,
    }
</pre><p>returns calendar hash with data:</p>

<pre>    %Data = (
        CalendarData =&gt; {
            CalendarID   =&gt; 2,
            GroupID      =&gt; 3,
            CalendarName =&gt; 'Meetings',
            Color        =&gt; '#FF7700',
            ValidID      =&gt; 1,
        },
        AppointmentData =&gt; (
            {
                AppointmentID       =&gt; 2,
                ParentID            =&gt; 1,
                CalendarID          =&gt; 1,
                UniqueID            =&gt; '20160101T160000-71E386@localhost',
                ...
            },
            ...
        ),
    );
</pre><h2 id="CalendarPermissionGet">CalendarPermissionGet()</h2>

<p><a href="#" onclick="toggleCode('method_CalendarPermissionGet');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_CalendarPermissionGet">
                                        <pre>

sub CalendarPermissionGet {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    for my $Needed (qw(CalendarID UserID)) {
        if ( !$Param{$Needed} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Need $Needed!",
            );
            return;
        }
    }

    # make sure super user has read/write permission
    return 'rw' if $Param{UserID} eq 1;

    my %Calendar = $Self->CalendarGet(
        CalendarID => $Param{CalendarID},
    );

    my $Result = '';

    my $GroupObject = $Kernel::OM->Get('Kernel::System::Group');

    TYPE:
    for my $Type (qw(ro move_into create rw)) {

        my %GroupData = $GroupObject->PermissionUserGet(
            UserID => $Param{UserID},
            Type   => $Type,
        );

        if ( $GroupData{ $Calendar{GroupID} } ) {
            $Result = $Type;
        }
        else {
            last TYPE;
        }
    }

    return $Result;
}
</pre></div>

<p>Get permission level for given CalendarID and UserID.</p>

<pre>    my $Permission = $CalendarObject-&gt;CalendarPermissionGet(
        CalendarID  =&gt; 1,                   # (required) CalendarID
        UserID      =&gt; 4,                   # (required) UserID
    );
</pre><p>Returns:</p>

<pre>    $Permission = 'rw';    # 'ro', 'rw', ...
</pre><h2 id="TicketAppointmentProcessTicket">TicketAppointmentProcessTicket()</h2>

<p><a href="#" onclick="toggleCode('method_TicketAppointmentProcessTicket');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_TicketAppointmentProcessTicket">
                                        <pre>

sub TicketAppointmentProcessTicket {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    if ( !$Param{TicketID} ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => 'Need TicketID!',
        );
        return;
    }

    # get all valid calendars
    my @Calendars = $Self->CalendarList(
        ValidID => 1,
    );
    return if !@Calendars;

    # get ticket appointment types
    my %TicketAppointmentTypes = $Self->TicketAppointmentTypesGet();

    # get ticket object
    my $TicketObject = $Kernel::OM->Get('Kernel::System::Ticket');

    # go through all calendars with defined ticket appointments
    CALENDAR:
    for my $Calendar (@Calendars) {
        my %CalendarData = $Self->CalendarGet(
            CalendarID => $Calendar->{CalendarID},
        );
        next CALENDAR if !$CalendarData{TicketAppointments};

        TICKET_APPOINTMENTS:
        for my $TicketAppointments ( @{ $CalendarData{TicketAppointments} } ) {

            # check appointment types
            for my $Field (qw(StartDate EndDate)) {

                # allow special time presets for EndDate
                if ( $Field ne 'EndDate' && !( $TicketAppointments->{$Field} =~ /^Plus_/ ) ) {

                    # skip if ticket appointment type is invalid
                    if ( !$TicketAppointmentTypes{ $TicketAppointments->{$Field} } ) {
                        next TICKET_APPOINTMENTS;
                    }
                }
            }

            # check if ticket satisfies the search filter from the ticket appointment rule
            # pass all configured parameters to ticket search, including ticket id
            my $Filtered = $TicketObject->TicketSearch(
                Result   => 'COUNT',
                TicketID => $Param{TicketID},
                QueueIDs => $TicketAppointments->{QueueID},
                UserID   => 1,
                %{ $TicketAppointments->{SearchParam} // {} },
            );

            # ticket was found
            if ($Filtered) {

                # process ticket appointment rule
                $Self->TicketAppointmentProcessRule(
                    CalendarID => $Calendar->{CalendarID},
                    Config     => \%TicketAppointmentTypes,
                    Rule       => $TicketAppointments,
                    TicketID   => $Param{TicketID},
                );
            }

            # ticket was not found
            else {

                # remove any existing ticket appointment
                $Self->TicketAppointmentDelete(
                    CalendarID => $Calendar->{CalendarID},
                    TicketID   => $Param{TicketID},
                    RuleID     => $TicketAppointments->{RuleID},
                );
            }
        }
    }

    if ( $Kernel::OM->Get('Kernel::Config')->Get('Debug') ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'debug',
            Message  => "Processed ticket appointments for ticket $Param{TicketID}.",
        );
    }

    return;
}
</pre></div>

<p>Handle the automatic ticket appointments for the ticket.</p>

<pre>    $CalendarObject-&gt;TicketAppointmentProcessTicket(
        TicketID =&gt; 1,
    );
</pre><p>This method does not have return value.</p>

<h2 id="TicketAppointmentProcessCalendar">TicketAppointmentProcessCalendar()</h2>

<p><a href="#" onclick="toggleCode('method_TicketAppointmentProcessCalendar');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_TicketAppointmentProcessCalendar">
                                        <pre>

sub TicketAppointmentProcessCalendar {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    if ( !$Param{CalendarID} ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => 'Need CalendarID!',
        );
        return;
    }

    my $TicketObject = $Kernel::OM->Get('Kernel::System::Ticket');

    # Get calendar configuration.
    my %Calendar = $Self->CalendarGet(
        CalendarID => $Param{CalendarID},
    );
    if ( !%Calendar ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => "Could not find calendar $Param{CalendarID}!",
        );
        return;
    }

    # Get ticket appointment types
    my %TicketAppointmentTypes = $Self->TicketAppointmentTypesGet();

    my @Process;
    my @Cleanup;
    my %RuleIDLookup;

    # Check ticket appointments config.
    if ( $Calendar{TicketAppointments} && IsArrayRefWithData( $Calendar{TicketAppointments} ) ) {

        # Get active rule IDs from the calendar configuration.
        %RuleIDLookup = map { $_->{RuleID} => 1 } @{ $Calendar{TicketAppointments} };

        TICKET_APPOINTMENTS:
        for my $TicketAppointments ( @{ $Calendar{TicketAppointments} } ) {

            # Check appointment types.
            for my $Field (qw(StartDate EndDate)) {

                # Allow special time presets for EndDate.
                if ( $Field ne 'EndDate' && !( $TicketAppointments->{$Field} =~ /^Plus_/ ) ) {

                    # Skip if ticket appointment type is invalid.
                    if ( !$TicketAppointmentTypes{ $TicketAppointments->{$Field} } ) {
                        next TICKET_APPOINTMENTS;
                    }
                }
            }

            # Get previously created ticket appointments for this rule.
            my %OldAppointments = $Self->_TicketAppointmentList(
                CalendarID => $Param{CalendarID},
                RuleID     => $TicketAppointments->{RuleID},
            );

            # Find tickets that match search filter
            my @TicketIDs = $TicketObject->TicketSearch(
                Result   => 'ARRAY',
                QueueIDs => $TicketAppointments->{QueueID},
                UserID   => 1,
                %{ $TicketAppointments->{SearchParam} // {} },
            );

            # Process each ticket based on ticket appointment rule.
            TICKETID:
            for my $TicketID ( sort @TicketIDs ) {
                my $Success = $Self->TicketAppointmentProcessRule(
                    CalendarID => $Param{CalendarID},
                    Config     => \%TicketAppointmentTypes,
                    Rule       => $TicketAppointments,
                    TicketID   => $TicketID,
                );

                push @Process, {
                    TicketID => $TicketID,
                    RuleID   => $TicketAppointments->{RuleID},
                    Success  => $Success,
                };
            }

            # Remove previously created ticket appointments if they don't match the rule anymore.
            OLDTICKETID:
            for my $OldTicketID ( sort keys %OldAppointments ) {
                next OLDTICKETID if grep { $OldTicketID == $_ } @TicketIDs;

                my $Success = $Self->TicketAppointmentDelete(
                    AppointmentID => $OldAppointments{$OldTicketID},
                    TicketID      => $OldTicketID,
                    CalendarID    => $Param{CalendarID},
                    RuleID        => $TicketAppointments->{RuleID},
                );

                push @Cleanup, {
                    AppointmentID => $OldAppointments{$OldTicketID},
                    TicketID      => $OldTicketID,
                    RuleID        => $TicketAppointments->{RuleID},
                    Success       => $Success,
                };
            }
        }
    }

    my @RuleIDs = $Self->TicketAppointmentRuleIDsGet(
        CalendarID => $Param{CalendarID},
    );

    # Remove ticket appointments for missing rules.
    for my $RuleID (@RuleIDs) {
        if ( !$RuleIDLookup{$RuleID} ) {
            my $Success = $Self->TicketAppointmentDelete(
                CalendarID => $Param{CalendarID},
                RuleID     => $RuleID,
            );

            push @Cleanup, {
                RuleID  => $RuleID,
                Success => $Success,
            };
        }
    }

    if ( $Kernel::OM->Get('Kernel::Config')->Get('Debug') ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'debug',
            Message  => "Processed ticket appointments for calendar $Param{CalendarID}.",
        );
    }

    return (
        Process => \@Process,
        Cleanup => \@Cleanup,
    );
}
</pre></div>

<p>Handle the automatic ticket appointments for the calendar.</p>

<pre>    my %Result = $CalendarObject-&gt;TicketAppointmentProcessCalendar(
        CalendarID =&gt; 1,
    );
</pre><p>Returns log of processed tickets and rules:</p>

<pre>    %Result = (
        Process =&gt; [
            {
                TicketID =&gt; 1,
                RuleID   =&gt; '9bb20ea035e7a9930652a9d82d00c725',
                Success  =&gt; 1,
            },
            {
                TicketID =&gt; 2,
                RuleID   =&gt; '9bb20ea035e7a9930652a9d82d00c725',
                Success  =&gt; 1,
            },
        ],
        Cleanup =&gt; [
            {
                RuleID  =&gt; 'b272a035ed82d65a927a99300e00c9b5',
                Success =&gt; 1,
            },
        ],
    );
</pre><h2 id="TicketAppointmentProcessRule">TicketAppointmentProcessRule()</h2>

<p><a href="#" onclick="toggleCode('method_TicketAppointmentProcessRule');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_TicketAppointmentProcessRule">
                                        <pre>

sub TicketAppointmentProcessRule {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    for my $Needed (qw(CalendarID Config Rule TicketID)) {
        if ( !$Param{$Needed} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Need $Needed!",
            );
            return;
        }
    }

    return if !IsHashRefWithData( $Param{Config} );
    return if !IsHashRefWithData( $Param{Rule} );

    my $Error;
    my $AppointmentType;
    my %AppointmentData;

    my $MainObject = $Kernel::OM->Get('Kernel::System::Main');

    # get start and end time values
    for my $Field (qw(StartDate EndDate)) {
        my $Type = $Param{Rule}->{$Field};

        # appointment fields are named differently
        my $AppointmentField = $Field;
        $AppointmentField =~ s/Date$/Time/;

        # check if we are dealing with a registered type
        if ( $Param{Config}->{$Type} && $Param{Config}->{$Type}->{Module} ) {
            my $GenericModule = $Param{Config}->{$Type}->{Module};

            # get the time value via the module method
            if ( $MainObject->Require($GenericModule) ) {
                $AppointmentData{$AppointmentField} = $GenericModule->new( %{$Self} )->GetTime(
                    Type     => $Type,
                    TicketID => $Param{TicketID},
                );
                $Error = 1 if !$AppointmentData{$AppointmentField};
            }
        }

        # time presets are valid only for end time and existing start time
        elsif ( $Field eq 'EndDate' && $AppointmentData{StartTime} ) {
            if ( $Type =~ /^Plus_([0-9]+)$/ ) {
                my $Preset = int $1;

                # Get start time.
                my $StartTimeObject = $Kernel::OM->Create(
                    'Kernel::System::DateTime',
                    ObjectParams => {
                        String => $AppointmentData{StartTime},
                    },
                );

                # Calculate end time using preset value.
                my $EndTimeObject = $StartTimeObject->Clone();
                $EndTimeObject->Add(
                    Minutes => $Preset,
                );

                $AppointmentData{EndTime} = $EndTimeObject->ToString();
            }
            else {
                $Error = 1;
                $Kernel::OM->Get('Kernel::System::Log')->Log(
                    Priority => 'error',
                    Message  => "Invalid time preset: $Type",
                );
            }
        }

        # unknown type
        else {
            $Error = 1;
        }
    }

    # Prevent end time before start time.
    if ( $AppointmentData{StartTime} && $AppointmentData{EndTime} ) {
        my $StartTimeObject = $Kernel::OM->Create(
            'Kernel::System::DateTime',
            ObjectParams => {
                String => $AppointmentData{StartTime},
            },
        );
        my $EndTimeObject = $Kernel::OM->Create(
            'Kernel::System::DateTime',
            ObjectParams => {
                String => $AppointmentData{EndTime},
            },
        );
        if ( $EndTimeObject < $StartTimeObject ) {
            $AppointmentData{EndTime} = $AppointmentData{StartTime};
        }
    }

    # get appointment title
    if ( !$Error ) {

        my $ConfigObject = $Kernel::OM->Get('Kernel::Config');

        my $TicketHook        = $ConfigObject->Get('Ticket::Hook');
        my $TicketHookDivider = $ConfigObject->Get('Ticket::HookDivider');
        my %Ticket            = $Kernel::OM->Get('Kernel::System::Ticket')->TicketGet(
            TicketID      => $Param{TicketID},
            DynamicFields => 0,
            UserID        => 1,
        );
        $AppointmentData{Title} = "[$TicketHook$TicketHookDivider$Ticket{TicketNumber}] $Ticket{Title}";
    }

    my $Success;

    # check if ticket appointment already exists
    my $AppointmentID = $Self->_TicketAppointmentGet(
        CalendarID => $Param{CalendarID},
        TicketID   => $Param{TicketID},
        RuleID     => $Param{Rule}->{RuleID},
    );

    # ticket appointment was found
    if ($AppointmentID) {

        # delete the ticket appointment, if error was raised
        if ($Error) {
            $Success = $Self->TicketAppointmentDelete(
                CalendarID    => $Param{CalendarID},
                TicketID      => $Param{TicketID},
                RuleID        => $Param{Rule}->{RuleID},
                AppointmentID => $AppointmentID,
            );
        }

        # update the ticket appointment, otherwise
        else {
            $Success = $Self->_TicketAppointmentUpdate(
                CalendarID    => $Param{CalendarID},
                AppointmentID => $AppointmentID,
                TicketID      => $Param{TicketID},
                RuleID        => $Param{Rule}->{RuleID},
                %AppointmentData,
            );
        }
    }

    # create ticket appointment if not found
    elsif ( !$Error ) {
        $Success = $Self->_TicketAppointmentCreate(
            CalendarID => $Param{CalendarID},
            TicketID   => $Param{TicketID},
            RuleID     => $Param{Rule}->{RuleID},
            %AppointmentData,
        );
    }

    return $Success;
}
</pre></div>

<p>Process the ticket appointment rule and create, update or delete appointment if necessary.</p>

<pre>    my $Success = $CalendarObject-&gt;TicketAppointmentProcessRule(
        CalendarID =&gt; 1,
        Config =&gt; {
            DynamicField_TestDate =&gt; {
                Module =&gt; 'Kernel::System::Calendar::Ticket::DynamicField',
            },
            ...
        },
        Rule =&gt; {
            StartDate =&gt; 'DynamicField_TestDate',
            EndDate   =&gt; 'Plus_5',
            QueueID   =&gt; [ 2 ],
            RuleID    =&gt; '9bb20ea035e7a9930652a9d82d00c725',
            SearchParams =&gt; {
                Title =&gt; 'Welcome*',
            },
        },
        TicketID =&gt; 1,
    );
</pre><p>Returns 1 if successful.</p>

<h2 id="TicketAppointmentUpdateTicket">TicketAppointmentUpdateTicket()</h2>

<p><a href="#" onclick="toggleCode('method_TicketAppointmentUpdateTicket');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_TicketAppointmentUpdateTicket">
                                        <pre>

sub TicketAppointmentUpdateTicket {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    for my $Needed (qw(AppointmentID TicketID)) {
        if ( !$Param{$Needed} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Need $Needed!",
            );
            return;
        }
    }

    # get appointment data
    my %AppointmentData = $Kernel::OM->Get('Kernel::System::Calendar::Appointment')->AppointmentGet(
        AppointmentID => $Param{AppointmentID},
    );

    # stop if not ticket appointment
    return if !$AppointmentData{TicketAppointmentRuleID};

    # get ticket appointment rule
    my $Rule = $Self->TicketAppointmentRuleGet(
        CalendarID => $AppointmentData{CalendarID},
        RuleID     => $AppointmentData{TicketAppointmentRuleID},
    );
    return if !IsHashRefWithData($Rule);

    # get ticket appointment types
    my %TicketAppointmentTypes = $Self->TicketAppointmentTypesGet();

    my $MainObject   = $Kernel::OM->Get('Kernel::System::Main');
    my $TicketObject = $Kernel::OM->Get('Kernel::System::Ticket');

    # process start and end time values
    for my $Field (qw(StartDate EndDate)) {
        my $Type = $Rule->{$Field};

        # appointment fields are named differently
        my $AppointmentField = $Field;
        $AppointmentField =~ s/Date$/Time/;

        # check if we are dealing with a registered type
        if ( $TicketAppointmentTypes{$Type} && $TicketAppointmentTypes{$Type}->{Module} ) {
            my $GenericModule = $TicketAppointmentTypes{$Type}->{Module};

            # set the time value via the module method
            if ( $MainObject->Require($GenericModule) ) {

                # loop protection: prevent ticket event module from running
                $TicketObject->{'_TicketAppointments::AlreadyProcessed'}->{ $Param{TicketID} }++;

                my $Success = $GenericModule->new( %{$Self} )->SetTime(
                    Type     => $Type,
                    Value    => $AppointmentData{$AppointmentField},
                    TicketID => $Param{TicketID},
                );
                if ( !$Success ) {
                    $Kernel::OM->Get('Kernel::System::Log')->Log(
                        Priority => 'error',
                        Message  => "Error setting $Type for ticket $Param{TicketID}!",
                    );
                }
            }
        }
    }

    if ( $Kernel::OM->Get('Kernel::Config')->Get('Debug') ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'debug',
            Message  => "Updated ticket $Param{TicketID} from appointment $Param{AppointmentID}.",
        );
    }

    return;
}
</pre></div>

<p>Updates the ticket with data from ticket appointment.</p>

<pre>    $CalendarObject-&gt;TicketAppointmentUpdateTicket(
        AppointmentID =&gt; 1,
        TicketID      =&gt; 1,
    );
</pre><p>This method does not have return value.</p>

<h2 id="TicketAppointmentTicketID">TicketAppointmentTicketID()</h2>

<p><a href="#" onclick="toggleCode('method_TicketAppointmentTicketID');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_TicketAppointmentTicketID">
                                        <pre>

sub TicketAppointmentTicketID {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    if ( !$Param{AppointmentID} ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => 'Need AppointmentID!',
        );
        return;
    }

    # get database object
    my $DBObject = $Kernel::OM->Get('Kernel::System::DB');

    # db query
    return if !$DBObject->Prepare(
        SQL => '
            SELECT ticket_id
            FROM calendar_appointment_ticket
            WHERE appointment_id = ?
        ',
        Bind  => [ \$Param{AppointmentID}, ],
        Limit => 1,
    );

    my $TicketID;
    while ( my @Row = $DBObject->FetchrowArray() ) {
        $TicketID = $Row[0];
    }

    return $TicketID;
}
</pre></div>

<p>get ticket id of a ticket appointment.</p>

<pre>    my $TicketID = $CalendarObject-&gt;TicketAppointmentTicketID(
        AppointmentID =&gt; 1,
    );
</pre><p>returns appointment ID if successful.</p>

<h2 id="TicketAppointmentRuleIDsGet">TicketAppointmentRuleIDsGet()</h2>

<p><a href="#" onclick="toggleCode('method_TicketAppointmentRuleIDsGet');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_TicketAppointmentRuleIDsGet">
                                        <pre>

sub TicketAppointmentRuleIDsGet {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    for my $Needed (qw(CalendarID)) {
        if ( !$Param{$Needed} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Need $Needed!",
            );
            return;
        }
    }

    my $SQL = '
        SELECT rule_id
        FROM calendar_appointment_ticket
        WHERE calendar_id = ?
    ';
    my @Bind;
    push @Bind, \$Param{CalendarID};

    # specific ticket query condition
    if ( $Param{TicketID} ) {
        $SQL .= '
            AND ticket_id = ?
        ';
        push @Bind, \$Param{TicketID};
    }

    my $DBObject = $Kernel::OM->Get('Kernel::System::DB');

    # db query
    return if !$DBObject->Prepare(
        SQL  => $SQL,
        Bind => \@Bind,
    );

    my %RuleIDs;
    while ( my @Row = $DBObject->FetchrowArray() ) {
        $RuleIDs{ $Row[0] } = 1;
    }

    # return unique rule ids
    return keys %RuleIDs;
}
</pre></div>

<p>get used ticket appointment rules for specific calendar.</p>

<pre>    my @RuleIDs = $CalendarObject-&gt;TicketAppointmentRuleIDsGet(
        CalendarID =&gt; 1,
        TicketID   =&gt; 1,    # (optional) Return rules used only for specific ticket
    );
</pre><p>returns array of rule IDs if found.</p>

<h2 id="TicketAppointmentRuleGet">TicketAppointmentRuleGet()</h2>

<p><a href="#" onclick="toggleCode('method_TicketAppointmentRuleGet');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_TicketAppointmentRuleGet">
                                        <pre>

sub TicketAppointmentRuleGet {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    for my $Needed (qw(CalendarID RuleID)) {
        if ( !$Param{$Needed} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Need $Needed!",
            );
            return;
        }
    }

    my %Calendar = $Self->CalendarGet(
        CalendarID => $Param{CalendarID},
    );
    return if !$Calendar{TicketAppointments};

    my $Result;

    RULE:
    for my $Rule ( @{ $Calendar{TicketAppointments} || [] } ) {
        if ( $Rule->{RuleID} eq $Param{RuleID} ) {
            $Result = $Rule;
            last RULE;
        }
    }
    return if !$Result;

    return $Result;
}
</pre></div>

<p>get ticket appointment rule.</p>

<pre>    my $Rule = $CalendarObject-&gt;TicketAppointmentRuleGet(
        CalendarID =&gt; 1,
        RuleID     =&gt; '9bb20ea035e7a9930652a9d82d00c725',
    );
</pre><p>returns rule hash:</p>

<h2 id="TicketAppointmentTypesGet">TicketAppointmentTypesGet()</h2>

<p><a href="#" onclick="toggleCode('method_TicketAppointmentTypesGet');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_TicketAppointmentTypesGet">
                                        <pre>

sub TicketAppointmentTypesGet {
    my ( $Self, %Param ) = @_;

    # get ticket appointment types
    my $TicketAppointmentConfig = $Kernel::OM->Get('Kernel::Config')->Get('AppointmentCalendar::TicketAppointmentType')
        // {};
    return if !$TicketAppointmentConfig;

    my %TicketAppointmentTypes;

    my $DynamicFieldObject = $Kernel::OM->Get('Kernel::System::DynamicField');

    TYPE:
    for my $TypeKey ( sort keys %{$TicketAppointmentConfig} ) {
        next TYPE if !$TicketAppointmentConfig->{$TypeKey}->{Key};

        if ( $TypeKey =~ /DynamicField$/ ) {

            # get list of all valid date and date/time dynamic fields
            my $DynamicFieldList = $DynamicFieldObject->DynamicFieldListGet(
                ObjectType => 'Ticket',
            );

            DYNAMICFIELD:
            for my $DynamicField ( @{$DynamicFieldList} ) {
                next DYNAMICFIELD if $DynamicField->{FieldType} ne 'Date' && $DynamicField->{FieldType} ne 'DateTime';

                my $Key = sprintf( $TicketAppointmentConfig->{$TypeKey}->{Key}, $DynamicField->{Name} );
                $TicketAppointmentTypes{$Key} = $TicketAppointmentConfig->{$TypeKey};
            }

            next TYPE;
        }

        $TicketAppointmentTypes{ $TicketAppointmentConfig->{$TypeKey}->{Key} } =
            $TicketAppointmentConfig->{$TypeKey};
    }

    return %TicketAppointmentTypes;
}
</pre></div>

<p>get defined ticket appointment types from config.</p>

<pre>    my %TicketAppointmentTypes = $CalendarObject-&gt;TicketAppointmentTypesGet();
</pre><p>returns hash of appointment types:</p>

<pre>    %TicketAppointmentTypes = ();
</pre><h2 id="TicketAppointmentDelete">TicketAppointmentDelete()</h2>

<p><a href="#" onclick="toggleCode('method_TicketAppointmentDelete');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_TicketAppointmentDelete">
                                        <pre>

sub TicketAppointmentDelete {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    for my $Needed (qw(CalendarID RuleID)) {
        if ( !$Param{$Needed} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Need $Needed!",
            );
            return;
        }
    }

    my @AppointmentIDs;
    push @AppointmentIDs, $Param{AppointmentID} if $Param{AppointmentID};

    # get database object
    my $DBObject = $Kernel::OM->Get('Kernel::System::DB');

    # appointment id is unknown
    if ( !@AppointmentIDs ) {
        my $SQL = '
            SELECT appointment_id
            FROM calendar_appointment_ticket
            WHERE calendar_id = ? AND rule_id = ?
        ';
        my @Bind;
        push @Bind, \$Param{CalendarID}, \$Param{RuleID};

        if ( $Param{TicketID} ) {
            $SQL .= '
                AND ticket_id = ?
            ';
            push @Bind, \$Param{TicketID};
        }

        # db query
        return if !$DBObject->Prepare(
            SQL  => $SQL,
            Bind => \@Bind,
        );

        while ( my @Row = $DBObject->FetchrowArray() ) {
            push @AppointmentIDs, $Row[0];
        }
    }

    # remove the relation(s) from database
    my $SQL = '
        DELETE FROM calendar_appointment_ticket
        WHERE calendar_id = ? AND rule_id = ?
    ';
    my @Bind;
    push @Bind, \$Param{CalendarID}, \$Param{RuleID};

    if ( $Param{TicketID} ) {
        $SQL .= '
            AND ticket_id = ?
        ';
        push @Bind, \$Param{TicketID};
    }

    return if !$DBObject->Do(
        SQL  => $SQL,
        Bind => \@Bind,
    );

    # get appointment object
    my $AppointmentObject = $Kernel::OM->Get('Kernel::System::Calendar::Appointment');

    # cleanup ticket appointments
    APPOINTMENT_ID:
    for my $AppointmentID (@AppointmentIDs) {

        # check if appointment exists
        next APPOINTMENT_ID if !$AppointmentObject->AppointmentGet(
            AppointmentID => $AppointmentID,
        );

        # delete the appointment
        return if !$AppointmentObject->AppointmentDelete(
            AppointmentID => $AppointmentID,
            UserID        => 1,
        );
    }

    return 1;
}
</pre></div>

<p>delete ticket appointment(s).</p>

<pre>    my $Success = $CalendarObject-&gt;TicketAppointmentDelete(
        CalendarID    =&gt; 1,
        RuleID        =&gt; '9bb20ea035e7a9930652a9d82d00c725',
        TicketID      =&gt; 1,                                     # (optional) Ticket ID is known
        AppointmentID =&gt; 1,                                     # (optional) Appointment ID is known
    );
</pre><p>returns 1 if successful.</p>

<h2 id="GetAccessToken">GetAccessToken()</h2>

<p><a href="#" onclick="toggleCode('method_GetAccessToken');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_GetAccessToken">
                                        <pre>

sub GetAccessToken {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    for my $Needed (qw(CalendarID UserLogin)) {
        if ( !$Param{$Needed} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Need $Needed!",
            );
            return;
        }
    }

    # create db object
    my $DBObject = $Kernel::OM->Get('Kernel::System::DB');

    # db query
    return if !$DBObject->Prepare(
        SQL   => 'SELECT salt_string FROM calendar WHERE id = ?',
        Bind  => [ \$Param{CalendarID} ],
        Limit => 1,
    );

    # fetch the result
    my $SaltString;
    while ( my @Row = $DBObject->FetchrowArray() ) {
        $SaltString = $Row[0];
    }

    return if !$SaltString;

    # Encode user login to UTF8 representation, since MD5 is defined only for strings of bytes.
    #   If login contains a Unicode character, MD5 might fail otherwise. Please see bug#12593 for
    #   more information.
    $Kernel::OM->Get('Kernel::System::Encode')->EncodeOutput( \$Param{UserLogin} );

    # calculate md5 sum
    my $String = "$Param{UserLogin}-$SaltString";
    my $MD5    = Digest::MD5->new()->add($String)->hexdigest();

    return $MD5;
}
</pre></div>

<p>get access token for the calendar.</p>

<pre>    my $Token = $CalendarObject-&gt;GetAccessToken(
        CalendarID =&gt; 1,              # (required) CalendarID
        UserLogin  =&gt; 'agent-1',      # (required) User login
    );
</pre><p>Returns:</p>

<pre>    $Token = 'rw';
</pre><h2 id="GetTextColor">GetTextColor()</h2>

<p><a href="#" onclick="toggleCode('method_GetTextColor');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_GetTextColor">
                                        <pre>

sub GetTextColor {
    my ( $Self, %Param ) = @_;

    # check needed stuff
    for my $Needed (qw(Background)) {
        if ( !$Param{$Needed} ) {
            $Kernel::OM->Get('Kernel::System::Log')->Log(
                Priority => 'error',
                Message  => "Need $Needed!",
            );
            return;
        }
    }

    # check color
    if ( !( $Param{Background} =~ /#[A-F0-9]{3,6}/i ) ) {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => 'Background must be in hexadecimal RGB notation, eg. #FFFFFF.',
        );
        return;
    }

    # check if value is cached
    my $Data = $Kernel::OM->Get('Kernel::System::Cache')->Get(
        Type => $Self->{CacheType} . 'GetTextColor',
        Key  => $Param{Background},
    );
    return $Data if $Data;

    # get RGB values
    my @BackgroundColor;
    my $RGBHex = substr( $Param{Background}, 1 );

    # six character hexadecimal string (eg. #FFFFFF)
    if ( length $RGBHex == 6 ) {
        $BackgroundColor[0] = hex substr( $RGBHex, 0, 2 );
        $BackgroundColor[1] = hex substr( $RGBHex, 2, 2 );
        $BackgroundColor[2] = hex substr( $RGBHex, 4, 2 );
    }

    # three character hexadecimal string (eg. #FFF)
    elsif ( length $RGBHex == 3 ) {
        $BackgroundColor[0] = hex( substr( $RGBHex, 0, 1 ) . substr( $RGBHex, 0, 1 ) );
        $BackgroundColor[1] = hex( substr( $RGBHex, 1, 1 ) . substr( $RGBHex, 1, 1 ) );
        $BackgroundColor[2] = hex( substr( $RGBHex, 2, 1 ) . substr( $RGBHex, 1, 1 ) );
    }

    # invalid hexadecimal string
    else {
        $Kernel::OM->Get('Kernel::System::Log')->Log(
            Priority => 'error',
            Message  => 'Background must be in valid 3 or 6 character hexadecimal RGB notation, eg. #FFF or #FFFFFF.',
        );
        return;
    }

    # predefined text colors
    my %TextColors = (
        White => [ '255', '255', '255' ],
        Gray  => [ '128', '128', '128' ],
        Black => [ '0',   '0',   '0' ],
    );

    # calculate background luminosity
    my $BackgroundLum =
        0.2126 * ( $BackgroundColor[0] / 255**2.2 ) +
        0.7152 * ( $BackgroundColor[1] / 255**2.2 ) +
        0.0722 * ( $BackgroundColor[2] / 255**2.2 );

    # calculate luminosity difference
    my %LumDiff;
    for my $TextColor ( sort keys %TextColors ) {
        my $TextLum =
            0.2126 * ( $TextColors{$TextColor}->[0] / 255**2.2 ) +
            0.7152 * ( $TextColors{$TextColor}->[1] / 255**2.2 ) +
            0.0722 * ( $TextColors{$TextColor}->[2] / 255**2.2 );

        if ( $BackgroundLum > $TextLum ) {
            $LumDiff{$TextColor} = ( $BackgroundLum + 0.05 ) / ( $TextLum + 0.05 );
        }
        else {
            $LumDiff{$TextColor} = ( $TextLum + 0.05 ) / ( $BackgroundLum + 0.05 );
        }
    }

    # get maximum luminosity difference
    my ($MaxLumDiff) = sort { $b <=> $a } values %LumDiff;
    return if !$MaxLumDiff;

    # identify best suited color
    my ($BestTextColor) = grep { $LumDiff{$_} eq $MaxLumDiff } keys %LumDiff;
    return if !$BestTextColor;

    # convert to hex string
    my $TextColor = sprintf(
        '#%X%X%X',
        $TextColors{$BestTextColor}->[0],
        $TextColors{$BestTextColor}->[1],
        $TextColors{$BestTextColor}->[2],
    );

    # cache
    $Kernel::OM->Get('Kernel::System::Cache')->Set(
        Type  => $Self->{CacheType} . 'GetTextColor',
        Key   => $Param{Background},
        Value => $TextColor,
        TTL   => $Self->{CacheTTL},
    );

    return $TextColor;
}
</pre></div>

<p>Returns best text color for supplied background, based on luminosity difference algorithm.</p>

<pre>    my $BestTextColor = $CalendarObject-&gt;GetTextColor(
        Background =&gt; '#FFF',    # (required) must be in valid hexadecimal RGB notation
    );
</pre><p>Returns:</p>

<pre>    $BestTextColor = '#000';
</pre><h1 id="TERMS_AND_CONDITIONS">TERMS AND CONDITIONS <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>This software is part of the OTRS project (<a href="http://otrs.org/">http://otrs.org/</a>).</p>

<p>This software comes with ABSOLUTELY NO WARRANTY. For details, see
the enclosed file COPYING for license information (AGPL). If you
did not receive this file, see <a href="http://www.gnu.org/licenses/agpl.txt">http://www.gnu.org/licenses/agpl.txt</a>.</p>


</div><div class="footer">generated by <a href="http://search.cpan.org/perldoc?Pod%3A%3AProjectDocs">Pod::ProjectDocs</a></div></body>
</html>
