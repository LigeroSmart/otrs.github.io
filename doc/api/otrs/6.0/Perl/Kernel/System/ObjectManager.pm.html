<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.1//EN" "http://www.w3.org/TR/xhtml11/DTD/xhtml11.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en">
<head>
<!-- otrs.github.io -->
<link href="https://netdna.bootstrapcdn.com/font-awesome/4.0.3/css/font-awesome.css" rel="stylesheet">
<link href="https://fonts.googleapis.com/css?family=Roboto" rel="stylesheet" type="text/css">
<link rel="stylesheet" href="../../../../../../documentation.css">
<script src="https://code.jquery.com/jquery-1.11.0.min.js"></script>
<script type="text/javascript" src="../../../../../../documentation.js"></script>
<link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/9.9.0/styles/default.min.css">
<script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/9.9.0/highlight.min.js"></script>
<script>
$(document).ready(function() {

    // Hint languages to prevent false matches (php for example).
    hljs.configure({
        languages: ['perl', 'javascript', 'xml', 'html', 'css', 'json', 'yaml']
    });

    // programlistings in manuals
    $('pre.programlisting').each(function(i, block) {
        hljs.highlightBlock(block);
    });
    // code snippets in Perl API docs
    $('.pod pre').addClass('perl').each(function(i, block) {
        hljs.highlightBlock(block);
    });
});</script>
<!-- otrs.github.io -->

	<title>Kernel::System::ObjectManager</title>
<meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
  <script type="text/javascript">

  function toggleCode( id ) {
    if ( document.getElementById )
      elem = document.getElementById( id );
    else if ( document.all )
      elem = eval( "document.all." + id );
    else
      return false;

    elemStyle = elem.style;

    if ( elemStyle.display != "block" ) {
      elemStyle.display = "block"
    } else {
      elemStyle.display = "none"
    }

    return true;
  }
  document.writeln( "<style type=\"text/css\">div.method-source-code { display: none }</style>" )
  </script></head>
<body>
<div class="box">
  <h1 class="t1">OTRS 6.0 API Reference Perl</h1>
  <table>
    <tr>
      <td class="label">Description</td>
      <td class="cell">manuals and libraries</td>
    </tr>
  </table>
</div>
<div class="path">
  <a href="../../index.html">OTRS 6.0 API Reference Perl</a> &gt; Perl Modules &gt;
  Kernel::System::ObjectManager
</div>
<div>
<a href="../../src/Kernel/System/ObjectManager.pm">Source</a>
</div>
<div class="pod">
<!-- INDEX START -->
<h3 id="TOP">Index</h3>
<ul>
	<li><a href="#NAME">NAME</a></li>
	<li><a href="#SYNOPSIS">SYNOPSIS</a></li>
	<li><a href="#DESCRIPTION">DESCRIPTION</a><br />
<ul>
	<li><a href="#How_does_singleton_management_work">How does singleton management work?</a></li>
	<li><a href="#How_to_use_it">How to use it?</a></li>
	<li><a href="#Which_objects_can_be_loaded">Which objects can be loaded?</a></li>
</ul>
</li>
	<li><a href="#PUBLIC_INTERFACE">PUBLIC INTERFACE</a><br />
<ul>
	<li><a href="#new">new()</a></li>
	<li><a href="#Get">Get()</a></li>
	<li><a href="#Create">Create()</a></li>
	<li><a href="#ObjectInstanceRegister">ObjectInstanceRegister()</a></li>
	<li><a href="#ObjectParamAdd">ObjectParamAdd()</a></li>
	<li><a href="#ObjectEventsHandle">ObjectEventsHandle()</a></li>
	<li><a href="#ObjectsDiscard">ObjectsDiscard()</a></li>
	<li><a href="#ObjectRegisterEventHandler">ObjectRegisterEventHandler()</a></li>
</ul>
</li>
	<li><a href="#TERMS_AND_CONDITIONS">TERMS AND CONDITIONS</a></li>
</ul>
<hr />
<!-- INDEX END -->

<h1 id="NAME">NAME <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>Kernel::System::ObjectManager - Central singleton manager and object instance generator</p>

<h1 id="SYNOPSIS">SYNOPSIS <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<pre>    # In toplevel scripts only!
    local $Kernel::OM = Kernel::System::ObjectManager-&gt;new();

    # Everywhere: get a singleton instance (and create it, if needed).
    my $ConfigObject = $Kernel::OM-&gt;Get('Kernel::Config');

    # Remove singleton objects and all their dependencies.
    $Kernel::OM-&gt;ObjectsDiscard(
        Objects            =&gt; ['Kernel::System::Ticket', 'Kernel::System::Queue'],
    );
</pre><h1 id="DESCRIPTION">DESCRIPTION <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>The ObjectManager is the central place to create and access singleton OTRS objects (via <code><a href="#Get">Get()</a></code>)
as well as create regular (unmanaged) object instances (via <code><a href="#Create">Create()</a></code>).</p>

<h2 id="How_does_singleton_management_work">How does singleton management work?</h2>

<p>It creates objects as late as possible and keeps references to them. Upon destruction the objects
are destroyed in the correct order, based on their dependencies (see below).</p>

<h2 id="How_to_use_it">How to use it?</h2>

<p>The ObjectManager must always be provided to OTRS by the toplevel script like this:</p>

<pre>    use Kernel::System::ObjectManager;
    local $Kernel::OM = Kernel::System::ObjectManager-&gt;new(
        # possible options for module constructors here
        LogObject {
            LogPrefix =&gt; 'OTRS-MyTestScript',
        },
    );
</pre><p>Then in the code any singleton object can be retrieved that the ObjectManager can handle,
like Kernel::System::DB:</p>

<pre>    return if !$Kernel::OM-&gt;Get('Kernel::System::DB')-&gt;Prepare('SELECT 1');
</pre><h2 id="Which_objects_can_be_loaded">Which objects can be loaded?</h2>

<p>The ObjectManager can load every object that declares its dependencies like this in the Perl package:</p>

<pre>    package Kernel::System::Valid;

    use strict;
    use warnings;

    our @ObjectDependencies = (
        'Kernel::System::Cache',
        'Kernel::System::DB',
        'Kernel::System::Log',
    );
</pre><p>The <code>@ObjectDependencies</code> is the list of objects that the current object will depend on. They will
be destroyed only after this object is destroyed (only for singletons).</p>

<p>If you want to signal that a package can NOT be loaded by the ObjectManager, you can use the
<code>$ObjectManagerDisabled</code> flag:</p>

<pre>    package Kernel::System::MyBaseClass;

    use strict;
    use warnings;

    our $ObjectManagerDisabled = 1;
</pre><p>There are a few flags available to convey meta data about the packages to the object manager.</p>

<p>To indicate that a certain package can <strong>only</strong> be loaded as a singleton, you can use the
<code>IsSingleton</code> flag. Similarly, you can indicate that a certain package can <strong>only</strong> be
created as unmanaged instance, and <strong>not</strong> as a singleton via the <code>NonSingleton</code> flag.
By default, the ObjectManager will die if a constructor does not return an object.
To suppress this in the <code><a href="#Create">Create()</a></code> method, you can use the <code>AllowConstructorFailure</code>
flag (this will not work with <code><a href="#Get">Get()</a></code>).</p>

<pre>    package Kernel::System::MyPackage;

    use strict;
    use warnings;

    our %ObjectManagerFlags = (
        IsSingleton             =&gt; 1,  # default 0
        NonSingleton            =&gt; 0,  # default 0
        AllowConstructorFailure =&gt; 0,  # default 0
    );
</pre><h1 id="PUBLIC_INTERFACE">PUBLIC INTERFACE <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<h2 id="new">new()</h2>

<p><a href="#" onclick="toggleCode('method_new');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_new">
                                        <pre>

sub new {
    my ( $Type, %Param ) = @_;
    my $Self = bless {}, $Type;

    $Self->{Debug} = delete $Param{Debug};

    for my $Parameter ( sort keys %Param ) {
        $Self->{Param}->{$Parameter} = $Param{$Parameter};
    }

    # Kernel::System::Encode->new() initializes the environment, so we need to
    #   already create an instance here to make sure it is always done and done
    #   at the beginning of things.
    $Self->Get('Kernel::System::Encode');

    return $Self;
}
</pre></div>

<p>Creates a new instance of Kernel::System::ObjectManager.</p>

<p>This is typically <strong>only</strong> needed in toplevel (bin/) scripts! All parts of the OTRS API assume
the ObjectManager to be present in <code>$Kernel::OM</code> and use it.</p>

<p>Sometimes objects need parameters to be sent to their constructors,
these can also be passed to the ObjectManager's constructor like in the following example.
The hash reference will be flattened and passed to the constructor of the object(s).</p>

<pre>    local $Kernel::OM = Kernel::System::ObjectManager-&gt;new(
        Kernel::System::Log =&gt; {
            LogPrefix =&gt; 'OTRS-MyTestScript',
        },
    );
</pre><p>Alternatively, <code><a href="#ObjectParamAdd">ObjectParamAdd()</a></code> can be used to set these parameters at runtime (but this
must happen before the object was created).</p>

<p>If the <code>Debug =&gt; 1</code> option is present, destruction of objects
is checked, and a warning is emitted if objects persist after the
attempt to destroy them.</p>

<h2 id="Get">Get()</h2>

<p><a href="#" onclick="toggleCode('method_Get');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_Get">
                                        <pre>

sub Get {

    # No param unpacking for increased performance
    if ( $_[1] && $_[0]->{Objects}->{ $_[1] } ) {
        return $_[0]->{Objects}->{ $_[1] };
    }

    if ( !$_[1] ) {
        $_[0]->_DieWithError(
            Error => "Error: Missing parameter (object name)",
        );
    }

    return $_[0]->_ObjectBuild( Package => $_[1] );
}
</pre></div>

<p>Retrieves a singleton object, and if it not yet exists, implicitly creates one for you.</p>

<pre>    my $ConfigObject = $Kernel::OM-&gt;Get('Kernel::Config');

    # On the second call, this returns the same ConfigObject as above.
    my $ConfigObject2 = $Kernel::OM-&gt;Get('Kernel::Config');
</pre><h2 id="Create">Create()</h2>

<p><a href="#" onclick="toggleCode('method_Create');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_Create">
                                        <pre>

sub Create {
    my ( $Self, $Package, %Param ) = @_;

    if ( !$Package ) {
        $Self->_DieWithError(
            Error => "Error: Missing parameter (object name)",
        );
    }

    return $Self->_ObjectBuild(
        %Param,
        Package     => $Package,
        NoSingleton => 1,
    );
}
</pre></div>

<p>Creates a new object instance. This instance will not be managed by the object manager later on.</p>

<pre>    my $DateTimeObject = $Kernel::OM-&gt;Create('Kernel::System::DateTime');

    # On the second call, this creates a new independent instance.
    my $DateTimeObject2 = $Kernel::OM-&gt;Create('Kernel::System::DateTime');
</pre><p>It is also possible to pass in constructor parameters:</p>

<pre>    my $DateTimeObject = $Kernel::OM-&gt;Create(
        'Kernel::System::DateTime',
        ObjectParams =&gt; {
            Param1 =&gt; 'Value1',
        },
    );
</pre><p>By default, this method will <code>die</code>, if the package cannot be instantiated or the constructor returns undef.
You can suppress this with <code>Silent =&gt; 1</code>, for example to not cause exceptions when trying
to load modules based on user configuration.</p>

<pre>    my $CustomObject = $Kernel::OM-&gt;Create(
        'Kernel::System::CustomObject',
        Silent =&gt; 1,
    );
</pre><h2 id="ObjectInstanceRegister">ObjectInstanceRegister()</h2>

<p><a href="#" onclick="toggleCode('method_ObjectInstanceRegister');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_ObjectInstanceRegister">
                                        <pre>

sub ObjectInstanceRegister {
    my ( $Self, %Param ) = @_;

    if ( !$Param{Package} || !$Param{Object} ) {
        $Self->_DieWithError( Error => 'Need Package and Object.' );
    }

    if ( defined $Self->{Objects}->{ $Param{Package} } ) {
        $Self->_DieWithError( Error => 'Need $Param{Package} is already registered.' );
    }

    $Self->{Objects}->{ $Param{Package} } = $Param{Object};
    $Self->{ObjectDependencies}->{ $Param{Package} } = $Param{Dependencies} // [];

    return 1;
}
</pre></div>

<p>Adds an existing object instance to the ObjectManager so that it can be accessed by other objects.</p>

<p>This should <strong>only</strong> be used on special circumstances, e. g. in the unit tests to pass <code>$Self</code> to the
ObjectManager so that it is also available from there as 'Kernel::System::UnitTest'.</p>

<pre>    $Kernel::OM-&gt;ObjectInstanceRegister(
        Package      =&gt; 'Kernel::System::UnitTest',
        Object       =&gt; $UnitTestObject,
        Dependencies =&gt; [],         # optional, specify OM-managed packages that the object might depend on
    );
</pre><h2 id="ObjectParamAdd">ObjectParamAdd()</h2>

<p><a href="#" onclick="toggleCode('method_ObjectParamAdd');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_ObjectParamAdd">
                                        <pre>

sub ObjectParamAdd {
    my ( $Self, %Param ) = @_;

    for my $Package ( sort keys %Param ) {
        if ( ref( $Param{$Package} ) eq 'HASH' ) {
            for my $Key ( sort keys %{ $Param{$Package} } ) {
                if ( defined $Key ) {
                    $Self->{Param}->{$Package}->{$Key} = $Param{$Package}->{$Key};
                }
                else {
                    delete $Self->{Param}->{$Package}->{$Key};
                }
            }
        }
        else {
            $Self->{Param}->{$Package} = $Param{$Package};
        }
    }
    return;
}
</pre></div>

<p>Adds arguments that will be passed to constructors of classes
when they are created, in the same format as the <code><a href="http://search.cpan.org/perldoc?new%28%29">new()</a></code> method
receives them.</p>

<pre>    $Kernel::OM-&gt;ObjectParamAdd(
        'Kernel::System::Ticket' =&gt; {
            Key =&gt; 'Value',
        },
    );
</pre><p>To remove a key again, send undef as a value:</p>

<pre>    $Kernel::OM-&gt;ObjectParamAdd(
        'Kernel::System::Ticket' =&gt; {
            Key =&gt; undef,               # this will remove the key from the hash
        },
    );
</pre><h2 id="ObjectEventsHandle">ObjectEventsHandle()</h2>

<p><a href="#" onclick="toggleCode('method_ObjectEventsHandle');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_ObjectEventsHandle">
                                        <pre>

sub ObjectEventsHandle {
    my ( $Self, %Param ) = @_;

    my $HasQueuedTransactions;
    EVENTS:
    for my $Counter ( 1 .. 10 ) {
        $HasQueuedTransactions = 0;
        EVENTHANDLERS:
        for my $EventHandler ( @{ $Self->{EventHandlers} } ) {

            # since the event handlers are weak references,
            # they might be undef by now.
            next EVENTHANDLERS if !defined $EventHandler;
            if ( $EventHandler->EventHandlerHasQueuedTransactions() ) {
                $HasQueuedTransactions = 1;
                $EventHandler->EventHandlerTransaction();
            }
        }
        if ( !$HasQueuedTransactions ) {
            last EVENTS;
        }
    }
    if ($HasQueuedTransactions) {
        warn "Unable to handle all pending events in 10 iterations";
    }
    delete $Self->{EventHandlers};

    return;
}
</pre></div>

<p>Execute all queued (<code>Transaction =&gt; 1</code>) events for all singleton objects
that the ObjectManager created before. This can be used to flush the event queue
before destruction, for example.</p>

<pre>    $Kernel::OM-&gt;ObjectEventsHandle();
</pre><h2 id="ObjectsDiscard">ObjectsDiscard()</h2>

<p><a href="#" onclick="toggleCode('method_ObjectsDiscard');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_ObjectsDiscard">
                                        <pre>

sub ObjectsDiscard {
    my ( $Self, %Param ) = @_;

    # fire outstanding events before destroying anything
    $Self->ObjectEventsHandle();

    # destroy objects before their dependencies are destroyed

    # first step: get the dependencies into a single hash,
    # so that the topological sorting goes faster
    my %ReverseDependencies;
    my @AllObjects;
    for my $Object ( sort keys %{ $Self->{Objects} } ) {
        my $Dependencies = $Self->{ObjectDependencies}->{$Object};

        for my $Dependency (@$Dependencies) {

            # undef happens to be the value that uses the least amount
            # of memory in Perl, and we are only interested in the keys
            $ReverseDependencies{$Dependency}->{$Object} = undef;
        }
        push @AllObjects, $Object;
    }

    # During an OTRS package upgrade the packagesetup code module has just
    # recently been copied to it's location in the file system.
    # In a persistent Perl environment an old version of the module might still be loaded,
    # as watchdogs like Apache2::Reload haven't had a chance to reload it.
    # So we need to make sure that the new version is being loaded.
    # Kernel::System::Main::Require() checks the relative file path, so we need to remove that from %INC.
    # This is only needed in persistent Perl environment, but does no harm in a CGI environment.
    if ( $Param{ForcePackageReload} ) {

        my @Objects;
        if ( $Param{Objects} && @{ $Param{Objects} } ) {
            @Objects = @{ $Param{Objects} };
        }
        else {
            @Objects = @AllObjects;
        }

        for my $Object (@Objects) {

            # convert :: to / in order to build a file system path name
            my $ObjectPath = $Object;
            $ObjectPath =~ s/::/\//g;

            # attach .pm as file extension
            $ObjectPath .= '.pm';

            # delete from global %INC hash
            delete $INC{$ObjectPath};
        }
    }

    # second step: post-order recursive traversal
    my %Seen;
    my @OrderedObjects;
    my $Traverser;
    $Traverser = sub {
        my ($Object) = @_;
        return if $Seen{$Object}++;
        for my $ReverseDependency ( sort keys %{ $ReverseDependencies{$Object} } ) {
            $Traverser->($ReverseDependency);
        }
        push @OrderedObjects, $Object;
    };

    if ( $Param{Objects} ) {
        for my $Object ( @{ $Param{Objects} } ) {
            $Traverser->($Object);
        }
    }
    else {
        for my $Object (@AllObjects) {
            $Traverser->($Object);
        }
    }
    undef $Traverser;

    # third step: destruction
    if ( $Self->{Debug} ) {

        # If there are undeclared dependencies between objects, destruction
        # might not work in the order that we calculated, but might still work
        # out in the end.
        my %DestructionFailed;
        for my $Object (@OrderedObjects) {
            my $Checker = $Self->{Objects}->{$Object};
            weaken($Checker);
            delete $Self->{Objects}->{$Object};

            if ( defined $Checker ) {
                $DestructionFailed{$Object} = $Checker;
                weaken( $DestructionFailed{$Object} );
            }
        }
        for my $Object ( sort keys %DestructionFailed ) {
            if ( defined $DestructionFailed{$Object} ) {
                warn "DESTRUCTION OF $Object FAILED!\n";
                if ( eval { require Devel::Cycle; 1 } ) {
                    Devel::Cycle::find_cycle( $DestructionFailed{$Object} );
                }
                else {
                    warn "To get more debugging information, please install Devel::Cycle.";
                }
            }
        }
    }
    else {
        for my $Object (@OrderedObjects) {
            delete $Self->{Objects}{$Object};
        }
    }

    # if an object requests an already destroyed object
    # in its DESTROY method, we might hold it again, and must try again
    # (but not infinitely)
    if ( !$Param{Objects} && keys %{ $Self->{Objects} } ) {
        if ( $Self->{DestroyAttempts} && $Self->{DestroyAttempts} > 3 ) {
            $Self->_DieWithError( Error => "Loop while destroying objects!" );
        }

        $Self->{DestroyAttempts}++;
        $Self->ObjectsDiscard();
        $Self->{DestroyAttempts}--;
    }

    return 1;
}
</pre></div>

<p>Discards internally stored objects, so that the next access to objects
creates them newly. If a list of object names is passed, only
the supplied objects and their recursive dependencies are destroyed.
If no list of object names is passed, all stored objects are destroyed.</p>

<pre>    $Kernel::OM-&gt;ObjectsDiscard();

    $Kernel::OM-&gt;ObjectsDiscard(
        Objects            =&gt; ['Kernel::System::Ticket', 'Kernel::System::Queue'],

        # optional
        # forces the packages to be reloaded from the file system
        # sometimes necessary with mod_perl when running CodeUpgrade during a package upgrade
        # if no list of object names is passed, all stored objects are destroyed
        # and forced to be reloaded
        ForcePackageReload =&gt; 1,
    );
</pre><p>Mostly used for tests that rely on fresh objects, or to avoid large
memory consumption in long-running processes.</p>

<p>Note that if you pass a list of objects to be destroyed, they are destroyed
in in the order they were passed; otherwise they are destroyed in reverse
dependency order.</p>

<h2 id="ObjectRegisterEventHandler">ObjectRegisterEventHandler()</h2>

<p><a href="#" onclick="toggleCode('method_ObjectRegisterEventHandler');return false;">[Source]</a></p>
                                        <div class="method-source-code" id="method_ObjectRegisterEventHandler">
                                        <pre>

sub ObjectRegisterEventHandler {
    my ( $Self, %Param ) = @_;
    if ( !$Param{EventHandler} ) {
        die "Missing parameter EventHandler";
    }
    push @{ $Self->{EventHandlers} }, $Param{EventHandler};
    weaken( $Self->{EventHandlers}[-1] );
    return 1;
}
</pre></div>

<p>Registers an object that can handle asynchronous events.</p>

<pre>    $Kernel::OM-&gt;ObjectRegisterEventHandler(
        EventHandler =&gt; $EventHandlerObject,
    );
</pre><p>The <code>EventHandler</code> object should inherit from <a href="EventHandler.pm.html">Kernel::System::EventHandler</a>.
The object manager will call that object's <code>EventHandlerHasQueuedTransactions</code>
method, and if that returns a true value, calls its <code>EventHandlerTransaction</code> method.</p>

<h1 id="TERMS_AND_CONDITIONS">TERMS AND CONDITIONS <a href="#TOP" class="toplink"><img alt="^" src="../../up.gif" /></a></h1>

<p>This software is part of the OTRS project (<a href="http://otrs.org/">http://otrs.org/</a>).</p>

<p>This software comes with ABSOLUTELY NO WARRANTY. For details, see
the enclosed file COPYING for license information (AGPL). If you
did not receive this file, see <a href="http://www.gnu.org/licenses/agpl.txt">http://www.gnu.org/licenses/agpl.txt</a>.</p>


</div><div class="footer">generated by <a href="http://search.cpan.org/perldoc?Pod%3A%3AProjectDocs">Pod::ProjectDocs</a></div></body>
</html>
